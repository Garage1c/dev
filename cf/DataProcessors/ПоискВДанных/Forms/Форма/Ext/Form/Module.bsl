&НаСервере
// Процедура выполняет полнотекстовый поиск
Процедура ИскатьВыполнитьСервер(Направление) Экспорт
	
	// В Демо-конфигурации перед поиском обновляем индекс
	// В реальных конфигурациях следует избегать такого подхода, т.к. это 
	// может привести к существенному замедлению поиска
	Если ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска() = РежимПолнотекстовогоПоиска.Разрешить И
		НЕ ПолнотекстовыйПоиск.ИндексАктуален() И
		ПравоДоступа("Администрирование", Метаданные) Тогда
		
		ПолнотекстовыйПоиск.ОбновитьИндекс();
		
	КонецЕсли;	
	
	РазмерПорции = 20; 
	
	СписокПоиска = ПолнотекстовыйПоиск.СоздатьСписок(СтрокаПоиска, РазмерПорции);
	
	Если Направление = 0 Тогда
		СписокПоиска.ПерваяЧасть();      
	ИначеЕсли Направление = -1 Тогда
		СписокПоиска.ПредыдущаяЧасть(ТекущаяПозиция);      
	ИначеЕсли Направление = 1 Тогда
		СписокПоиска.СледующаяЧасть(ТекущаяПозиция);      
	КонецЕсли;        	
	
	РезультатыПоиска.Очистить();
	Для Каждого Результат Из СписокПоиска Цикл
		РезультатыПоиска.Добавить(Результат.Значение);
	КонецЦикла; 	
	
	HTMLТекст = СписокПоиска.ПолучитьОтображение(ВидОтображенияПолнотекстовогоПоиска.HTMLТекст);
	
	HTMLТекст = СтрЗаменить(HTMLТекст, "<td>", "<td><font face=""Arial"" size=""2"">");
	HTMLТекст = СтрЗаменить(HTMLТекст, "<td valign=top width=1>", "<td valign=top width=1><font face=""Arial"" size=""1"">");
	HTMLТекст = СтрЗаменить(HTMLТекст, "<body>", "<body topmargin=0 leftmargin=0>");
	HTMLТекст = СтрЗаменить(HTMLТекст, "</td>", "</font></td>");
	HTMLТекст = СтрЗаменить(HTMLТекст, "<b>", "");
	HTMLТекст = СтрЗаменить(HTMLТекст, "</b>", "");
	
	ТекущаяПозиция = СписокПоиска.НачальнаяПозиция();
	ПолноеКоличество = СписокПоиска.ПолноеКоличество();       	
	
	Если РезультатыПоиска.Количество() <> 0 Тогда
		ПоказаныРезультаты_С_По = 
			"Показаны " + 
			Строка(ТекущаяПозиция + 1) + " - " +  
			Строка(ТекущаяПозиция + РезультатыПоиска.Количество()) + 
			" из " + Строка(ПолноеКоличество);
			
		Элементы.Далее.Доступность = (ПолноеКоличество - ТекущаяПозиция) > РезультатыПоиска.Количество();
		Элементы.Назад.Доступность = (ТекущаяПозиция > 0);
	Иначе
		ПоказаныРезультаты_С_По = "Не найдено";
		Элементы.Далее.Доступность = Ложь;
		Элементы.Назад.Доступность = Ложь;
	КонецЕсли;
	
	Если Элементы.ПолеВводаПоиска.СписокВыбора.НайтиПоЗначению(СтрокаПоиска) = Неопределено Тогда
		Элементы.ПолеВводаПоиска.СписокВыбора.Вставить(0, СтрокаПоиска);
		Строки = Элементы.ПолеВводаПоиска.СписокВыбора.ВыгрузитьЗначения();
		
		ХранилищеОбщихНастроек.Сохранить("ПолнотекстовыйПоискСтрокиПолнотекстовогоПоиска", , Строки);
	КонецЕсли;
КонецПроцедуры
        
&НаКлиенте
// Обработка команды Найти
Процедура ИскатьВыполнить()
	Искать(0);
КонецПроцедуры

&НаКлиенте
// Обработчик команды Назад
Процедура НазадВыполнить()
	Искать(-1);
КонецПроцедуры

&НаКлиенте
// Обработчик команды Далее
Процедура ДалееВыполнить()
	Искать(1);
КонецПроцедуры

&НаКлиенте
// Процедура поиска, получение и отображение результата
Процедура Искать(Направление)
	Если ПустаяСтрока(СтрокаПоиска) Тогда
		ПоказатьПредупреждение(,"Не задана строка поиска.",,"Предупреждение");
		Возврат;
	КонецЕсли;
	
	ИскатьВыполнитьСервер(Направление);
КонецПроцедуры

&НаКлиенте
Процедура HTMLТекстПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	ЭлементHTML = ДанныеСобытия.Event.srcElement;
	
	Если (ЭлементHTML.id = "FullTextSearchListItem") Тогда
		НомерВСписке = Число(ЭлементHTML.nameProp);
		ВыбраннаяСтрока = РезультатыПоиска[НомерВСписке].Значение;
		ОткрытьЗначение(ВыбраннаяСтрока);
		
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска() <> РежимПолнотекстовогоПоиска.Разрешить Тогда
		Отказ = Истина;
		Сообщить("Использование полнотекстового поиска не разрешено. Вы можете включить его в диалоге ""Управление полнотекстовым поиском""");
		Возврат;
	КонецЕсли;
	
	ПоказаныРезультаты_С_По = "Введите строку поиска";
	ТекущаяПозиция = 0;
	
	Элементы.Далее.Доступность = Ложь;
	Элементы.Назад.Доступность = Ложь;
	
	Массив = ХранилищеОбщихНастроек.Загрузить("ПолнотекстовыйПоискСтрокиПолнотекстовогоПоиска");
	
	Если Массив <> Неопределено Тогда
		Элементы.ПолеВводаПоиска.СписокВыбора.ЗагрузитьЗначения(Массив);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ПолеВводаПоискаОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	СтрокаПоиска = Текст;
	ИскатьВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура ПолеВводаПоискаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтрокаПоиска = ВыбранноеЗначение;
	ИскатьВыполнить();
КонецПроцедуры

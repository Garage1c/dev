
&НаСервере
Процедура ОбновитьРашифровкуНаСервере()
	
	Заголовок = "Несоответствие резервов: " + ВыбраннаяНоменклатура;
	
	ТаблицаДвижений.Очистить();
	ТаблицаДанных81 = ТаблицаДвижений.Выгрузить(); // подготовим структру для будущей таблицы
	
	Connector = КэшируемыеФункции.ИницилизироватьCOMConnector81();
	
	// Вытащим данные из 81
	
	СтрОшибки = "";
	
	РезультатЗапроса81 = COMServer.ПолучитьВыполнениеЗапроса81("
		|ВЫБРАТЬ
		|	Период,
		|	Регистратор,
		|	ВЫБОР КОГДА ВидДвижения = &ВидДвиженияПриход ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ КАК ЭтоПриход,
		|	Склад,
		|	Количество,
		|	ВЫБОР 
		|		КОГДА Регистратор ССЫЛКА Документ.ВнутреннийЗаказ ТОГДА ""ВнутреннийЗаказ""
		|		КОГДА Регистратор ССЫЛКА Документ.ЗаказПокупателя ТОГДА ""ЗаказПокупателя""
		|		КОГДА Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг ТОГДА ""РеализацияТоваров""
		|	ИНАЧЕ
		|		""Неопределено""
		|	КОНЕЦ КАК ВидДокумента
		|ИЗ
		|	РегистрНакопления.ТоварыВРезервеНаСкладах
		|ГДЕ
		|	Номенклатура = &Номенклатура
		|",, 
		Новый Структура("Номенклатура, ВидДвиженияПриход", 
							Connector.Справочники.Номенклатура.ПолучитьСсылку(Connector.NewObject("UUID", Строка(ВыбраннаяНоменклатура.УникальныйИдентификатор()))),
							Connector.ВидДвиженияНакопления.Приход),
		СтрОшибки);
		
	Если РезультатЗапроса81 = Неопределено Тогда
		
		ОбщиеФункции.СообщитьТекст(СтрОшибки);
		Возврат;
		
	КонецЕсли;
		
	Если Не РезультатЗапроса81.Пустой() Тогда
			
		// Загрузим сконвертировав данные из 81 в таблицу сравнения
		
		Выборка81 = РезультатЗапроса81.Выбрать();
		Пока Выборка81.Следующий() Цикл
			
			НовСтрока = ТаблицаДанных81.Добавить();
			НовСтрока.Склад 		= Справочники.Склады.ПолучитьСсылку(Новый УникальныйИдентификатор(Connector.String("UUID", Выборка81.Склад.УникальныйИдентификатор())));
			НовСтрока.ВидДвижения  	= ?(Выборка81.ЭтоПриход, ВидДвиженияНакопления.Приход, ВидДвиженияНакопления.Расход);
			НовСтрока.Количество  	= Выборка81.Количество;
			НовСтрока.Период  		= Выборка81.Период;
			НовСтрока.База			= "8.1";
			
			Если Выборка81.ВидДокумента <> "Неопределено" Тогда
				НовСтрока.Регистратор = Документы[Выборка81.ВидДокумента].ПолучитьСсылку(Новый УникальныйИдентификатор(Connector.string(Выборка81.Регистратор.УникальныйИдентификатор())));
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	// Загрузим в SQL табличку из 81
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Таблица81.База,
	|	Таблица81.Период,
	|	Таблица81.Регистратор,
	|	Таблица81.ВидДвижения,
	|	Таблица81.Склад,
	|	Таблица81.Количество
	|ПОМЕСТИТЬ
	|	Таблица81
	|ИЗ
	|	&Таблица81 КАК Таблица81
	|");
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Таблица81", ТаблицаДанных81);
	Запрос.Выполнить();
	
	// Сравним и найдем различия
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ЕСТЬNULL(Таблица81.База, ""8.2"") 						КАК База,
	|	ЕСТЬNULL(Таблица81.Период, Таблица82.Период)			КАК Период,
	|	ЕСТЬNULL(Таблица81.Регистратор, Таблица82.Регистратор)	КАК Регистратор,
	|	ЕСТЬNULL(Таблица81.ВидДвижения, Таблица82.ВидДвижения) 	КАК ВидДвижения,
	|	ЕСТЬNULL(Таблица81.Склад, Таблица82.Размещение) 		КАК Склад,
	|	ЕСТЬNULL(Таблица81.Количество, Таблица82.Количество) 	КАК Количество
	|ИЗ
	|	Таблица81 КАК Таблица81
	|
	|ПОЛНОЕ СОЕДИНЕНИЕ
	|	(ВЫБРАТЬ * ИЗ РегистрНакопления.ТоварыВРезерве ГДЕ Номенклатура = &Номенклатура) КАК Таблица82
	|ПО
	|	Таблица81.Регистратор = Таблица82.Регистратор И
	|	Таблица81.ВидДвижения = Таблица82.ВидДвижения И
	|	Таблица81.Склад = Таблица82.Размещение И
	|	Таблица81.Количество = Таблица82.Количество И
	|	Таблица81.Период = Таблица82.Период
	|
	|ГДЕ
	|	Таблица81.Регистратор ЕСТЬ NULL ИЛИ Таблица82.Регистратор ЕСТЬ NULL
	|";

	Запрос.УстановитьПараметр("Номенклатура", ВыбраннаяНоменклатура);
	
	ТаблицаДвижений.Загрузить(Запрос.Выполнить().Выгрузить());
    		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьРашифровку() Экспорт
	
	ОбновитьРашифровкуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьРашифровку();
	
КонецПроцедуры

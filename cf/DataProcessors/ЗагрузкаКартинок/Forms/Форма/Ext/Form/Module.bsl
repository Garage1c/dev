&НаСервере
Функция ПолучитьСсылкиНоменклатуры(Код, Артикул, ДвойнойПоиск)
	
	Если ДвойнойПоиск Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ Ссылка ИЗ Справочник.Номенклатура ГДЕ Код = """ + Код + """ И Артикул = """ + Артикул + """");
	Иначе
		Запрос = Новый Запрос("ВЫБРАТЬ Ссылка ИЗ Справочник.Номенклатура ГДЕ " + Объект.ПоискПо + " = """ + Код + """");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат КонвертацияТипов.ПолучитьМассивИзПоляВыборки(Выборка, "Ссылка");
	
КонецФункции

&НаКлиенте
Функция ПолучитьКартинку(ПутьКартинке)
	
	Если Не ПустаяСтрока(ПутьКартинке) Тогда
			
		ФайлКартинки = Новый Файл(ПутьКартинке);
			
		Если Не ФайлКартинки.Существует() Тогда
			ОбщиеФункции.СообщитьТекст("Не найдена картинка по пути " + ПутьКартинке);
			Возврат Неопределено;
		КонецЕсли;
		
		Попытка
			Картинка = Новый Картинка(ПутьКартинке);
		Исключение
			стрОшибки = ОписаниеОшибки();
			ОбщиеФункции.СообщитьТекст("Ошибка при создании картинки " + ПутьКартинке + "
											|" + стрОшибки);
			Возврат Неопределено;
		КонецПопытки;
		
		Возврат Картинка;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура Загрузка(Команда)
	
	стрОшибки = "";
	
	ОбозначениеОсновнойКартинки 	= "main_img";
	НачальнаяСтрока 				= 2;
	НомерКолонкиМаленькойКартинки	= 6;
	НомерКолонкиБольшойКартинки		= 4;
	НомерКолонкиФлагаАватара		= 5;
	НомерКолонкиКодаНоменклатуры 	= 1;
	НомерКолонки2КодаНоменклатуры 	= 2;
	ДвойнойПоиск 					= Объект.ПоискПо = "Код+Артикул";	
	
	Ошибка = Ложь;
	
	Если Объект.ПоискПо = "" Тогда
	    ОбщиеФункции.СообщитьТекст("Не задан поиск", Объект.ПоискПо);
		Ошибка = Истина;
	КонецЕсли;
	
	ФайлЭкселя = Новый Файл(Объект.ИмяФайлаЭкселя);
	Если Не ФайлЭкселя.Существует() Тогда
		ОбщиеФункции.СообщитьТекст("Нет файла экселя", Объект.ИмяФайлаЭкселя);
		Ошибка = Истина;
	КонецЕсли;
	
	ФайлКартинок = Новый Файл(Объект.ИмяКаталога);
	Если Не ФайлКартинок.Существует() Тогда
		ОбщиеФункции.СообщитьТекст("Не найден каталог", Объект.ИмяКаталога);
		Ошибка = Истина;
	КонецЕсли;
	
	Если Ошибка Тогда
		Возврат;
	КонецЕсли;
	
	// Получим эксель
	
	Эксель = COMФункцииДиалогов.ОткрытьФайлЭкселя(Объект.ИмяФайлаЭкселя, стрОшибки);
	
	Если Эксель = Неопределено Тогда
		ОбщиеФункции.СообщитьТекст(стрОшибки);
		Возврат;
	КонецЕсли;
	
	// Подготовим таблицу для считывания
	
	Попытка
		Лист = Эксель.Sheets(Объект.ЛистЭкселя);
	Исключение
		ОбщиеФункции.СообщитьТекст("Не найден лист экселя по имени """ + Объект.ЛистЭкселя + """");
		COMФункцииДиалогов.ЗакрытьЭксель(Эксель);
		Возврат;
	КонецПопытки;
	
	КолВоКолонок 	= Лист.Cells(1,1).SpecialCells(11).Column;
	КолВоСтрок 		= Лист.Cells(1,1).SpecialCells(11).Row;
	
	Для Ном = НачальнаяСтрока По КолВоСтрок Цикл
		
		ОбработкаПрерыванияПользователя();
		
		// Получим мелкую картинку
		
		ПутьККартинкам = Объект.ИмяКаталога + ?(Прав(Объект.ИмяКаталога,1) = "\","","\");
		
		ИмяКартинки = СокрЛП(Лист.Cells(Ном, НомерКолонкиБольшойКартинки).Text);
		
		МаленькаяКартинка 	= ПолучитьКартинку(ПутьККартинкам + СокрЛП(Лист.Cells(Ном, НомерКолонкиМаленькойКартинки).Text));
		БольшаяКартинка 	= ПолучитьКартинку(ПутьККартинкам + ИмяКартинки);
		ЭтоОсновнаяКартинка = Врег(СокрЛП(Лист.Cells(Ном, НомерКолонкиБольшойКартинки).Text) = Врег(ОбозначениеОсновнойКартинки));
		
		Если 	МаленькаяКартинка <> Неопределено И
				БольшаяКартинка <> Неопределено Тогда
				
			Код 		= Лист.Cells(Ном, НомерКолонкиКодаНоменклатуры).Text;
			Артикул 	= Лист.Cells(Ном, НомерКолонки2КодаНоменклатуры).Text;
			
			Ссылки = ПолучитьСсылкиНоменклатуры(Код, Артикул, ДвойнойПоиск);
			
			Если Ссылки.Количество() = 0 Тогда
				ОбщиеФункции.СообщитьТекст("Не найден товар по " + Объект.ПоискПо + " - " + Код + ?(ДвойнойПоиск," + " + Артикул,""));
			ИначеЕсли Ссылки.Количество() > 1 Тогда
				ОбщиеФункции.СообщитьТекст("Найдено " + Ссылки.Количество() +  " товаров с одинаковым " + Объект.ПоискПо + " - " + Код + ?(ДвойнойПоиск," + " + Артикул,""));
			КонецЕсли;
			
			Для Каждого Ссылка Из Ссылки Цикл
				
				Если ЭтоОсновнаяКартинка Тогда	
					
					Если Не Картинки.ОбновитьКартинкуОсновногоИзображения(
								МаленькаяКартинка,
								Ссылка,
								ИмяКартинки,
								Истина,
								стрОшибки) Тогда
								
						ОбщиеФункции.СообщитьТекст(стрОшибки);
					КонецЕсли;
					
					Если Не Картинки.ОбновитьКартинкуОсновногоИзображения(
							БольшаяКартинка,
							Ссылка,
							ИмяКартинки,
							Ложь,
							стрОшибки) Тогда
							
						ОбщиеФункции.СообщитьТекст(стрОшибки);
					КонецЕсли;
					
				Иначе
					
					Если Не Картинки.Сохранить(
						МаленькаяКартинка,
						Ссылка, 
						ИмяКартинки,
						,
						,
						Истина,
						стрОшибки) Тогда
						
						ОбщиеФункции.СообщитьТекст(стрОшибки);
					КонецЕсли;
					
					Если Не Картинки.Сохранить(
						БольшаяКартинка,
						Ссылка, 
						ИмяКартинки,
						,
						,
						Ложь,
						стрОшибки) Тогда
						
						ОбщиеФункции.СообщитьТекст(стрОшибки);
					КонецЕсли;
					
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Всего = КолВоСтрок - НачальнаяСтрока;
		ПорНомер = Ном - НачальнаяСтрока;
		Состояние("Загрузка картинок из экселя", ПорНомер / (КолВоСтрок - НачальнаяСтрока) * 100, "" + ПорНомер + " из " + Всего);
		
	КонецЦикла;
	
	COMФункцииДиалогов.ЗакрытьЭксель(Эксель);
				
	ОбщиеФункции.СообщитьТекст("Данные загружены.");
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлЭкселяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДВ = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДВ.Фильтр =  "Эксель (*.xls)|*.xls*";
	
	Если ДВ.Выбрать() Тогда
		
		Объект.ИмяФайлаЭкселя = ДВ.ПолноеИмяФайла;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ИмяКаталогаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДВ = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	
	Если ДВ.Выбрать() Тогда
		
		Объект.ИмяКаталога = ДВ.Каталог;
		
	КонецЕсли;

КонецПроцедуры

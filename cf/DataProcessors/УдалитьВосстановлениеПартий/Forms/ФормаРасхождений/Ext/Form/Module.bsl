
&НаСервере
Процедура ОбновитьНаСервере()
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ВидДвижения, Период, Регистратор, Склад, //МАКСИМУМ(Партия), 
	|	Количество, МАКСИМУМ(Товары), МАКСИМУМ(Партии)
	|ИЗ
	|(
	|	ВЫБРАТЬ	ВидДвижения, Период, Регистратор, Склад, //NULL Партия, 
	|			СУММА(Количество) Количество, Истина Товары, Ложь Партии
	|	ИЗ		РегистрНакопления.ТоварыНаСкладах
	|	ГДЕ		Номенклатура = &Номенклатура
	|	СГРУППИРОВАТЬ ПО ВидДвижения, Период, Регистратор, Склад
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ	ВидДвижения, Период, Регистратор, Склад, //Партия, 
	|			СУММА(Количество), Ложь, Истина
	|	ИЗ		РегистрНакопления.ПартииТоваров
	|	ГДЕ		Номенклатура = &Номенклатура
	|	СГРУППИРОВАТЬ ПО ВидДвижения, Период, Регистратор, Склад
	|) Запрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВидДвижения, Период, Регистратор, Склад, Количество
	|
	|УПОРЯДОЧИТЬ ПО Период
	|");
	
	Запрос.УстановитьПараметр("Номенклатура", Объект.КосякТовар);
	Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.КосякТовар = Параметры.Номенклатура;
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КосякТоварПриИзменении(Элемент)
	
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаСервере
Функция ИмяДокумента(СсылкаДок)
	
	Возврат СсылкаДок.Метаданные().Имя;
	
КонецФункции
&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОткрытьФорму("Документ." + ИмяДокумента(Элемент.ТекущиеДанные.Регистратор) + ".ФормаОбъекта", Новый Структура("Ключ", Элемент.ТекущиеДанные.Регистратор));
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОстатки(Регистратор)
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ Склад, КоличествоОстаток Количество 								ИЗ РегистрНакопления.ТоварыНаСкладах.Остатки(&МоментВремени, Номенклатура = &Номенклатура);
	|ВЫБРАТЬ Партия, Склад, КоличествоОстаток Количество, СуммаОстаток Сумма	ИЗ РегистрНакопления.ПартииТоваров.Остатки(&МоментВремени, Номенклатура = &Номенклатура)");
	
	Запрос.УстановитьПараметр("Номенклатура", 	Объект.КосякТовар);
	Запрос.УстановитьПараметр("МоментВремени", 	Новый МоментВремени(Регистратор.Дата, Регистратор));
	
	Пакет = Запрос.ВыполнитьПакет();
	ТоварыНаСкладах.Загрузить(Пакет[0].Выгрузить());
	ПартииТоваров.Загрузить(Пакет[1].Выгрузить());
	
КонецПроцедуры
&НаКлиенте
Процедура ТоварыПриАктивизацииСтроки(Элемент)
	
	ОбновитьОстатки(Элементы.Товары.ТекущиеДанные.Регистратор);
	
КонецПроцедуры

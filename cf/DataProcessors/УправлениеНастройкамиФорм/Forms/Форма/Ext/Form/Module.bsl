//////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры и функции
&НаСервере
Процедура ЗаполнитьСписокПользователей()
	
	Пользователи = ПользователиИнформационнойБазы.ПолучитьПользователей();
	
	Для Каждого ТекПользователь Из Пользователи Цикл
		
		Элементы.Пользователь.СписокВыбора.Добавить(ТекПользователь.Имя, ТекПользователь.ПолноеИмя);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокФорм()
	
	Обработка = ДанныеФормыВЗначение(Объект, Тип("ОбработкаОбъект.УправлениеНастройкамиФорм"));
	СписокФорм = Новый СписокЗначений;
	Обработка.ПолучитьСписокФорм(СписокФорм);
	Формы.Очистить();
	Обработка.ПолучитьСписокСохраненныхНастроек(СписокФорм, Пользователь, Формы);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивВыделенныхНастроек()
	
	МассивНастроек = Новый Массив;
	
	ВыделенныеЭлементы = Элементы.ОтфильтрованныеФормы.ВыделенныеСтроки;
	
	Для Каждого ВыделенныйЭлемент Из ВыделенныеЭлементы Цикл
		
		МассивНастроек.Добавить(Формы[ВыделенныйЭлемент].Значение);
		
	КонецЦикла;
	
	Возврат МассивНастроек;
	
КонецФункции

&НаСервере
Процедура СкопироватьНаСервере(ПользователиПриемник)

	МассивНастроекДляКопирования = ПолучитьМассивВыделенныхНастроек();
	
	Обработка = ДанныеФормыВЗначение(Объект, Тип("ОбработкаОбъект.УправлениеНастройкамиФорм"));
	Обработка.СкопироватьНастройкиФорм(Пользователь, ПользователиПриемник, МассивНастроекДляКопирования);
		
КонецПроцедуры

&НаСервере
Процедура УдалитьНаСервере()
	
	МассивНастроекДляУдаления = ПолучитьМассивВыделенныхНастроек();
	
	Обработка = ДанныеФормыВЗначение(Объект, Тип("ОбработкаОбъект.УправлениеНастройкамиФорм"));
	Обработка.УдалитьНастройкиФорм(Пользователь, МассивНастроекДляУдаления);
	
КонецПроцедуры

Процедура ПрименитьФильтр()
	
	ОтфильтрованныеФормы.Очистить();
	
	Для Каждого ЭлементФорма Из Формы Цикл
		
		Если Поиск = "" Или Найти(ВРег(ЭлементФорма.Представление), ВРег(Поиск)) <> 0 Тогда
			
			ОтфильтрованныеФормы.Добавить(ЭлементФорма.Значение, ЭлементФорма.Представление, ЭлементФорма.Пометка, ЭлементФорма.Картинка);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПримененныйПоиск = Поиск;
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////
// Обработчики команд
&НаКлиенте
Процедура ОбновитьВыполнить()
	
	ОбновитьСписокФорм();
	ПрименитьФильтр();
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьВыполнить()
	
	Если Элементы.ОтфильтрованныеФормы.ВыделенныеСтроки.Количество() = 0 Тогда
		
		ПоказатьПредупреждение(,"Для копирования нужно выбрать настройки, которые требуется скопировать.",,"Предупреждение");
		Возврат;
		
	КонецЕсли;
	
	Пользователи = Элементы.Пользователь.СписокВыбора.Скопировать();
	Пользователи.Удалить(Пользователи.НайтиПоЗначению(Пользователь));
	
	Если Пользователи.ОтметитьЭлементы("Отметьте пользователей, которым нужно скопировать настройки") Тогда
	
		ПользователиПриемник = Новый Массив;

		Для Каждого Элемент Из Пользователи Цикл
			
			Элементы.Пользователь.СписокВыбора.НайтиПоЗначению(Элемент.Значение).Пометка = Элемент.Пометка;
			Если Элемент.Пометка Тогда
				
				ПользователиПриемник.Добавить(Элемент.Значение);
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если ПользователиПриемник.Количество() = 0 Тогда
			
			ПоказатьПредупреждение(,"Для копирования нужно отметить пользователей, которым требуется скопировать настройки.",,"предупреждение");
			Возврат;
			
		КонецЕсли;
		
		Если Вопрос("После копирования настроек пользователю форма у пользователя будет открываться с настройками, которые ему скопируются. При этом его собственные настройки будут потеряны. Вы действительно хотите скопировать настройки выбранным пользователям?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да) <> КодВозвратаДиалога.Да Тогда
			
			Возврат;
			
		КонецЕсли;

		СкопироватьНаСервере(ПользователиПриемник);
		ПоказатьОповещениеПользователя("Настройки скопированы");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьВыполнить()
	
	Если Элементы.ОтфильтрованныеФормы.ВыделенныеСтроки.Количество() = 0 Тогда
		
		ПоказатьПредупреждение(,"Для удаления нужно выбрать настройки, которые требуется удалить.",,"Предупреждение");
		Возврат;
		
	КонецЕсли;
	
	Если Вопрос("После удаления настроек форма будет открываться с настройками по умолчанию. Вы действительно хотите удалить настройки?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да) <> КодВозвратаДиалога.Да Тогда
		
		Возврат;
		
	КонецЕсли;
	
	УдалитьНаСервере();
	ОбновитьСписокФорм();
	ПрименитьФильтр();
	
	ПоказатьОповещениеПользователя("Настройки удалены");
	
КонецПроцедуры

&НаКлиенте
Процедура ИскатьВыполнить()
	
	ПрименитьФильтр();
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////
// Обработчики событий формы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписокПользователей();
	Пользователь = ИмяПользователя();
	ОбновитьСписокФорм();
	ПрименитьФильтр();
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////
// Обработчики событий элементов управления
&НаКлиенте
Процедура ПользовательПриИзменении(Элемент)
	
	ОбновитьСписокФорм();
	ПрименитьФильтр();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискПриИзменении(Элемент)
	
	ПрименитьФильтр();
	
КонецПроцедуры


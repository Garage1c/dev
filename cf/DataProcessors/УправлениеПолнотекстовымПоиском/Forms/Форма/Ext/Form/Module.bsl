&НаСервере
// Процедура обновляет индекс полнотекстового поиска
Процедура ОбновитьИндекс()
	ПолнотекстовыйПоиск.ОбновитьИндекс();
	ОбновитьСтатусВыполнить();
КонецПроцедуры

&НаКлиенте
// Обработчик команды ОбновитьИндекс
Процедура ОбновитьИндексВыполнить()
	Состояние("Подождите!" + Символы.ВК + "Идет обновление полнотекстового индекса...");
	ОбновитьИндекс();
	Состояние("Обновление полнотекстового индекса завершено");
КонецПроцедуры
        

&НаСервере
// Процедура выполняет очистку индекса полнотекстового поиска
Процедура ОчиститьИндексСервер() Экспорт
	ПолнотекстовыйПоиск.ОчиститьИндекс(); 
	ОбновитьСтатусВыполнить();
КонецПроцедуры	

&НаКлиенте
// Обработчик команды ОчиститьИндекс
Процедура ОчиститьИндексВыполнить()
	ОчиститьИндексСервер();	
КонецПроцедуры

&НаСервере
// Процедура обновляет информацию об индексе и управляет доступностью кнопок
Процедура ОбновитьСтатусВыполнить()
	ЭтаФорма.Элементы.ОбновитьИндекс.Доступность = Ложь;
	ЭтаФорма.Элементы.ОчиститьИндекс.Доступность = Ложь;
	
	РазрешитьПолнотекстовыйПоиск = ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска() = РежимПолнотекстовогоПоиска.Разрешить;
	ДатаАктуальностиИндекса = '00010101';
	СтатусИндекса = "";
	
	Если ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска() = РежимПолнотекстовогоПоиска.Разрешить Тогда
		ДатаАктуальностиИндекса = ПолнотекстовыйПоиск.ДатаАктуальности();
		
		Если ПолнотекстовыйПоиск.ИндексАктуален() Тогда
			СтатусИндекса = "Обновление индекса не требуется";
		Иначе                                                 
			СтатусИндекса = "Требуется обновление индекса";
		КонецЕсли;
		
		Если ПравоДоступа("Администрирование", Метаданные) Тогда
			ЭтаФорма.Элементы.ОчиститьИндекс.Доступность = Истина;
			ЭтаФорма.Элементы.ОбновитьИндекс.Доступность = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


&НаСервере
// Процедура устанавливает режим полнотекстового поиска
Процедура УстановитьПолнотекстовыйПоискРазрешен(Разрешен) Экспорт
	Если Разрешен Тогда
		ПолнотекстовыйПоиск.УстановитьРежимПолнотекстовогоПоиска(РежимПолнотекстовогоПоиска.Разрешить);
	Иначе
		ПолнотекстовыйПоиск.УстановитьРежимПолнотекстовогоПоиска(РежимПолнотекстовогоПоиска.Запретить);
	КонецЕсли;
	ОбновитьСтатусВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьПолнотекстовыйПоискПриИзменении(Элемент)
	Попытка
		УстановитьПолнотекстовыйПоискРазрешен(РазрешитьПолнотекстовыйПоиск);
	Исключение
		РазрешитьПолнотекстовыйПоиск = НЕ РазрешитьПолнотекстовыйПоиск;
		Инфо = ИнформацияОбОшибке();
		СтрокаИсключения = Инфо.Описание + "." + Символы.ВК + "Вероятно, запущено регламентное задание обновления индекса. Попробуйте еще раз некоторое время спустя.";
		ВызватьИсключение СтрокаИсключения;
	КонецПопытки;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьСтатусВыполнить();
КонецПроцедуры


&НаСервере
Функция ЗагрузитьСтроку(стрОшибки)
	
	Возврат COMServer.ЗагрузитьСтрокуЭкселя();
	
КонецФункции

&НаКлиенте
Процедура Загрузить(Команда)
	
	стрОшибки = "";
	
	// Проверим файл
	
	Если ПустаяСтрока(Объект.ИмяФайла) Тогда
		ОбщиеФункции.СообщитьТекст("Не выбран файл", "ИмяФайла", Объект);
		Возврат;
	КонецЕсли;
	
	// Проверим правило
	
	Если Объект.Правило.Пустая() Тогда
		ОбщиеФункции.СообщитьТекст("Не указано правило", "Правило", Объект);
		Возврат;
	КонецЕсли;
	
	// Получим структуру правил
	
	СтруктураПравил = КонвертацияТипов.ПолучитьСтруктуруПравилОбменаСЭкселем(Объект.Правило, стрОшибки);
	Если СтруктураПравил = Неопределено Тогда
		ОбщиеФункции.СообщитьТекст(стрОшибки);
		Возврат;
	КонецЕсли;
	
	// Получим эксель
	
	Эксель = COMФункцииДиалогов.ОткрытьФайлЭкселя(Объект.ИмяФайла, стрОшибки);
	
	Если Эксель = Неопределено Тогда
		ОбщиеФункции.СообщитьТекст(стрОшибки);
		Возврат;
	КонецЕсли;
	
	// Подготовим таблицу для считывания
	
	стрОшибки = "";
	
	Попытка
		Лист = Эксель.Sheets(структураПравил.ЛистЭкселя);
	Исключение
		ОбщиеФункции.СообщитьТекст("Не найден лист экселя по имени """ + структураПравил.ЛистЭкселя + """");
		COMФункцииДиалогов.ЗакрытьЭксель(Эксель);
		Возврат;
	КонецПопытки;
	
	КолВоКолонок 	= Лист.Cells(1,1).SpecialCells(11).Column;
	КолВоСтрок 		= Лист.Cells(1,1).SpecialCells(11).Row;
	
	КолонкиЭкселя 	= СтруктураПравил.ЗагружаемыеСтроки;
	
	Для Ном = структураПравил.НачальнаяСтрока По КолВоСтрок Цикл
		
		// загружать будем по одной строки и передовать на сервак	
		
		ТаблицаДанных.Очистить();
		
		Для КАждого КолонкаЭкселя Из КолонкиЭкселя Цикл
			
			новСтрока = ТаблицаДанных.Добавить();
			ЗаполнитьЗначенияСвойств(новСтрока, КолонкаЭкселя);
			
			новСтрока.ТекстВЯчейке 	= СокрЛП(Лист.Cells(Ном, КолонкаЭкселя.НомерКолонки).Value);
			
		КонецЦикла;
		
		// передадим серваку на обработку
		
		Если Не ЗагрузитьНаСервере(стрОшибки) Тогда
			ОбщиеФункции.СообщитьТекст(стрОшибки);
			Если Объект.ОстанавливатьсяПоОшибке Тогда
				ОбщиеФункции.СообщитьТекст(стрОшибки);
				COMФункцииДиалогов.ЗакрытьЭксель(Эксель);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		// отобразим ход загрузки
		
		Всего = КолВоСтрок - структураПравил.НачальнаяСтрока;
		ПорНомер = Ном - структураПравил.НачальнаяСтрока;
		Состояние("Загрузка данных из экселя", ПорНомер / (КолВоСтрок - структураПравил.НачальнаяСтрока) * 100, "" + ПорНомер + " из " + Всего, КартинкаЗагрузки);
		
		ОбработкаПрерыванияПользователя();
		
	КонецЦикла;
	
	COMФункцииДиалогов.ЗакрытьЭксель(Эксель);
				
	ОбщиеФункции.СообщитьТекст("Данные загружены.");
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьНаСервере(стрОшибки)
	
	Возврат COMServer.ЗагрузитьОбъектИзЭкселя(
			Вычислить("Метаданные." + Объект.Правило.ВидОбъекта), 
			РеквизитФормыВЗначение("ТаблицаДанных"), 
			
				КонвертацияТипов.ПолучитьСтруктуруИзСправочника(Объект.правило),
			//Новый Структура("ЭтоГруппа, НеСоздавать", 
			//			Объект.правило.Группа, 
			//			Объект.правило.НеСоздавать), 
			
		стрОшибки);
	
КонецФункции

&НаКлиенте
Процедура ИмяФайлаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДВ = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДВ.Фильтр =  "Эксель (*.xls)|*.xls*";
	
	Если ДВ.Выбрать() Тогда
		
		Объект.ИмяФайла = ДВ.ПолноеИмяФайла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КартинкаЗагрузки = Метаданные.ОбщиеКартинки.Эксель;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИмяФайлаПоПравилу()
	
	Объект.ИмяФайла = Объект.Правило.ИмяФайла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПравилоПриИзменении(Элемент)
	
	Если ПустаяСтрока(Объект.ИмяФайла) Тогда
		УстановитьИмяФайлаПоПравилу();
	КонецЕсли;
	
КонецПроцедуры

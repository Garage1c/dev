Процедура ПолучитьUpdate()Экспорт 
	
	ЗапросМакс = Новый Запрос("ВЫБРАТЬ
	                          |	МАКСИМУМ(update.update_id) КАК update_id
	                          |ИЗ
	                          |	РегистрСведений.tg_update КАК update");
	Выборка = ЗапросМакс.Выполнить().Выбрать();
	Если Выборка.Следующий() ТОгда
		OffSet = "?offset="+Формат(Выборка.update_id, "ЧГ=");
	Иначе
		OffSet = "";
	КонецЕсли;
	
	Ключ = Константы.tg_КлючAPI.Получить();
	Сервер = "api.telegram.org";
	Команда = "getUpdates"+OffSet;
	Ресурс = "bot"+Ключ+"/"+Команда;
	Соединение = Новый HTTPСоединение(Сервер,,,,,,Новый ЗащищенноеСоединениеOpenSSL);
	Запрос = Новый HTTPЗапрос(Ресурс); 
	
	Ответ = Соединение.Получить(Запрос);
	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
	
	ОтветСоотв = ПрочитатьJSON(Чтение,Истина);
	Если ОтветСоотв["ok"] Тогда
		getUpdatesОбОтв(ОтветСоотв);
	КонецЕсли;
	
КонецПроцедуры

Процедура setWebhook(ok)Экспорт 
	
	Ключ = Константы.tg_КлючAPI.Получить();
	Сервер = "api.telegram.org";
	Команда = "setWebhook";
	Ресурс = "bot"+Ключ+"/"+Команда+"?url="+КодироватьСтроку(Константы.tg_URLWebhook.Получить(),СпособКодированияСтроки.КодировкаURL);
	Соединение = Новый HTTPСоединение(Сервер,,,,,,Новый ЗащищенноеСоединениеOpenSSL);
	Запрос = Новый HTTPЗапрос(Ресурс); 
	
	Ответ = Соединение.Получить(Запрос);
	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
	
	ОтветСоотв = ПрочитатьJSON(Чтение,Истина);
	ok=ОтветСоотв["ok"];
	
КонецПроцедуры

Процедура getUpdatesОбОтв(ОтветСоотв)
	
	ТабID = Новый ТаблицаЗначений;
	ТабID.Колонки.Добавить("update_id", tg_ОбщегоНазначенияКлиентСервервер.ПолучитьОписаниеТиповЧисла(15));
	
	Для Каждого upd Из ОтветСоотв["result"] Цикл
		
		СтрТаб = ТабID.Добавить();
		СтрТаб.update_id = upd["update_id"];
				  
	КонецЦикла;
			
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ТабID.update_id
	                      |ПОМЕСТИТЬ ТабID
	                      |ИЗ
	                      |	&ТабID КАК ТабID
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	ТабID.update_id КАК update_id
	                      |ИЗ
	                      |	ТабID КАК ТабID
	                      |ГДЕ
	                      |	НЕ ТабID.update_id В
	                      |				(ВЫБРАТЬ
	                      |					update.update_id
	                      |				ИЗ
	                      |					РегистрСведений.tg_update КАК update) ");
						  
	Запрос.УстановитьПараметр("ТабID",ТабID);
	
	НовыеАпдейты = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("update_id");
	
	Для Каждого upd Из ОтветСоотв["result"] Цикл
		Если НовыеАпдейты.Найти(upd["update_id"]) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		message = upd["message"];
		мз = РегистрыСведений.tg_update.СоздатьМенеджерЗаписи();
		мз.update_id = upd["update_id"];
		мз.date = МестноеВремя(Дата('19700101')+message["date"]);
		мз.text = message["text"];
		мз.message_id 	= message["message_id"];
		мз.first_name 	= message["from"]["first_name"];
		мз.id 			= message["from"]["id"];
		мз.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьЗапросы() Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	update.update_id,
	                      |	update.date,
	                      |	update.text,
	                      |	update.message_id КАК message_id,
	                      |	update.first_name,
	                      |	update.id КАК id
	                      |ИЗ
	                      |	РегистрСведений.tg_update КАК update
	                      |ГДЕ
	                      |	НЕ update.Обработан
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	message_id
	                      |ИТОГИ ПО
	                      |	id");
	ВыборкаЮзеров = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаЮзеров.Следующий() Цикл
		ВыборкаЗапросов = ВыборкаЮзеров.Выбрать();
		Пока ВыборкаЗапросов.Следующий() Цикл
			ok = Ложь;
			ОбработатьЗапрос(ВыборкаЗапросов, ok);
			Если ok Тогда
				мз = РегистрыСведений.tg_update.СоздатьМенеджерЗаписи();
				мз.update_id =ВыборкаЗапросов.update_id;
				мз.Прочитать();
				мз.Обработан = Истина;
				мз.Записать();
			КонецЕсли;
		
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьЗапрос(ЗапросПользователя, ok)
	
	//Если СокрЛП(ЗапросПользователя.text) = "/start"  Тогда
	
	Если ЗапросПользователя.text = "/takeMyMoney" Тогда
		ОтправитьФото("AgADAgADqqcxGyFJZAUlBdilQOYUgM_VWSoABF8-r36VSA9ZZSMAAgI",ЗапросПользователя.id, ok)
	Иначе
		ОтправитьСообщение(СформироватьСообщение(ЗапросПользователя.text,ЗапросПользователя), ЗапросПользователя.id, ok);		
	КонецЕсли;
	 
		
	//ИначеЕсли СокрЛП(ЗапросПользователя.text) = "/time" Тогда
		//ОтправитьСообщение(СформироватьСообщение("time",ЗапросПользователя), ЗапросПользователя.id, ok);		
	//Иначе
	//	ОтправитьСообщение(СформироватьСообщение("errorType",ЗапросПользователя), ЗапросПользователя.id, ok);
		
	//КонецЕсли;	
	
КонецПроцедуры

Процедура ОтправитьФото (ТекстСообщения, ПолучательID,ok)
	
	Ключ = Константы.tg_КлючAPI.Получить();
	Сервер = "api.telegram.org";
	Команда = "sendPhoto";
	Ресурс = "bot"+Ключ+"/"+Команда+"?chat_id="+Формат(ПолучательID,"ЧГ=")+"&photo="+СокрЛП(ТекстСообщения); 
	Соединение = Новый HTTPСоединение(Сервер,,,,,,Новый ЗащищенноеСоединениеOpenSSL);
	Запрос = Новый HTTPЗапрос(Ресурс); 
	
	Ответ = Соединение.Получить(Запрос);
	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
	
	ОтветСоотв = ПрочитатьJSON(Чтение,Истина);
	ok = ОтветСоотв["ok"] ;
		
КонецПроцедуры

Процедура ОтправитьСообщение (ТекстСообщения, ПолучательID,ok)
	
	Ключ = Константы.tg_КлючAPI.Получить();
	Сервер = "api.telegram.org";
	Команда = "sendMessage";
	Ресурс = "bot"+Ключ+"/"+Команда+"?chat_id="+Формат(ПолучательID,"ЧГ=")+"&text="+СокрЛП(ТекстСообщения); 
	Соединение = Новый HTTPСоединение(Сервер,,,,,,Новый ЗащищенноеСоединениеOpenSSL);
	Запрос = Новый HTTPЗапрос(Ресурс); 
	
	Ответ = Соединение.Получить(Запрос);
	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
	
	ОтветСоотв = ПрочитатьJSON(Чтение,Истина);
	ok = ОтветСоотв["ok"] ;
		
КонецПроцедуры

Функция СформироватьСообщение(ТипЗапроса, ЗапросПользователя)
	
	Результат = "Неизвестный запрос";
	
	Если ТипЗапроса = "/start"  тогда
		Результат = СокрЛП(ПолучитьОбщийМакет("tg_ОтветStart").ПолучитьТекст()) ;
	КонецЕсли;
	Если ТипЗапроса = "/wt"  тогда
		Результат = "Шепчет!";
	КонецЕсли;
	
	Если ТипЗапроса = "/time"  тогда
		Результат = Формат(ТекущаяДата(),"ДЛФ=DDT") ;
	КонецЕсли;
	
	Если ТипЗапроса = "/hello"  тогда
		Результат = "Привет," +ЗапросПользователя.first_name;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьСпособВЗ() Экспорт
	
	СпособВЗ = Константы.tg_СпособВыполненияЗаданий.Получить();
	Если СпособВЗ.Пустая() Тогда
		СпособВЗ = Перечисления.tg_СпособыВыполненияЗаданий.ФормаОбработки;
		Константы.tg_СпособВыполненияЗаданий.Установить(СпособВЗ);		
	КонецЕсли;
	
КонецПроцедуры

Функция ЗапускатьКлиент() Экспорт 
	
	
	Возврат Константы.tg_СпособВыполненияЗаданий.Получить() = Перечисления.tg_СпособыВыполненияЗаданий.ФормаОбработки 
		        И ЗначениеЗаполнено(Константы.tg_КлючAPI.Получить()) 
	
	
КонецФункции

// ПОЛУЧЕНИЕ ДАННЫХ

Функция ПолучитьВсеЗаказыЗадач(выбЗадачи) Экспорт
	
	// возвращает все заказы связанные с переданными задачми
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ Заказ Поместить Заказы ИЗ Задача.ЗадачаПользователю ГДЕ Ссылка В (&Задачи);
	|ВЫБРАТЬ РАЗЛИЧНЫЕ 	Заказ
	|ИЗ 				Документ.СборочныйЛист.Товары ГДЕ Ссылка В(
	|						ВЫБРАТЬ Ссылка ИЗ Документ.СборочныйЛист.Товары ГДЕ Заказ В(ВЫБРАТЬ Заказ Из Заказы))
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ Заказ ИЗ Заказы
	|");
	
	Запрос.УстановитьПараметр("Задачи", выбЗадачи);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Заказ");
	
КонецФункции
Функция ПолучитьЗадачиБизнесПроцессыСОбщимСборочникомВЗаказах(Заказ, Склад, СборочныйЛист) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ Ссылка Задача, БизнесПроцесс, БизнесПроцесс.СборочныйЛист СборочныйЛист ИЗ Задача.ЗадачаПользователю 
	|ГДЕ 
	|	Выполнена 		= ЛОЖЬ И
	|	Склад 			= &Склад И
	|	ТочкаМаршрута 	= &ТочкаСобратьТовар И
	|	Ссылка.Заказ В(
	|				ВЫБРАТЬ РАЗЛИЧНЫЕ Заказ 
	|				ИЗ Документ.СборочныйЛист.Товары 
	|				ГДЕ Ссылка = &СборочныйЛист И Заказ <> &Заказ)
	|");
	
	Запрос.УстановитьПараметр("СборочныйЛист", 		СборочныйЛист);
	Запрос.УстановитьПараметр("Склад", 				Склад);
	Запрос.УстановитьПараметр("Заказ", 				Заказ);
	Запрос.УстановитьПараметр("ТочкаСобратьТовар", 	БизнесПроцессы.СборкаТовара.ТочкиМаршрута.СобратьТовар);

	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ПолучитьСписокЗаказовКОбъединению(ЗадачаСсылка, ДопУсловия = Неопределено) Экспорт
	
	// ДопУсловия - структура
	//	- Параметры 			- (массив) параметры запроса
	//	- ТекстФильтраЗароса 	- (строка) текст который подставляется в блок условий запроса
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Заказ, Заказ.Номер Номер, Заказ.Дата Дата, ЕСТЬNULL(Заказ.Контрагент, Заказ.Заказчик) Заказчик, Заказ.Сумма Сумма, ТИПЗНАЧЕНИЯ(Заказ) ТипЗаказа
	|ИЗ
	|	Задача.ЗадачаПользователю
	|ГДЕ
	|	НЕ ПометкаУдаления И НЕ Выполнена И
	|	Склад = &Склад И Пользователь = &Пользователь И Роль = &Роль И
	|	ТочкаМаршрута = &ТочкаМаршрута
	|	// Доп. параметры
	|
	|УПОРЯДОЧИТЬ ПО 
	|	Заказ.Дата
	|");
	
	Запрос.УстановитьПараметр("Склад", 			ЗадачаСсылка.Склад);
	Запрос.УстановитьПараметр("Пользователь", 	ЗадачаСсылка.Пользователь);
	Запрос.УстановитьПараметр("Роль", 			ЗадачаСсылка.Роль);
	Запрос.УстановитьПараметр("ТочкаМаршрута", 	ЗадачаСсылка.ТочкаМаршрута);
	
	Если ДопУсловия <> Неопределено Тогда
		Для Каждого Параметр Из ДопУсловия.Параметры Цикл Запрос.УстановитьПараметр(Параметр.Ключ, Параметр.Значение) КонецЦикла;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "// Доп. параметры", ДопУсловия.ТекстФильтраЗароса); КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Список 	= Новый СписокЗначений;
	
	Пока Выборка.Следующий() Цикл
		Список.Добавить(Выборка.Заказ, СтрШаблон("%3: %1 %2 (%4) %5", 
					?(Выборка.ТипЗаказа = Тип("ДокументСсылка.ВнутреннийЗаказ"), "Внутренний заказ", ?(Выборка.ТипЗаказа = Тип("ДокументСсылка.ИнтернетЗаказПокупателя"), "Интернет заказ", "Заказ")),
					Выборка.Номер,
					Формат(Выборка.Дата, "ДФ=yyyy-MM-dd"),
					Выборка.Заказчик,
					Выборка.Сумма)); КонецЦикла;
					
	Возврат Список;
	
КонецФункции

Процедура ОбновитьТаблицуТоваров(ТаблицаТоваров, МассивЗаказов) Экспорт

	ТаблицаТоваров.Загрузить(Заказы.ПолучитьСостояниеТоваров(МассивЗаказов));
		
КонецПроцедуры
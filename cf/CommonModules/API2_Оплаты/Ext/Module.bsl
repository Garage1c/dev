
Функция ВернутьСтатусОплаты(IDЗаказа, Статус = Ложь, стрОшибки = "", СсылкаОплаты = Неопределено)
	
	Возврат Новый Структура("pay_status, id, guid, err", Статус, IDЗаказа, СсылкаОплаты, стрОшибки);
	
КонецФункции
Функция ОплатаПоЗаказу(order, стрОшибки) Экспорт
	
#Область Проверка
	
	СтруктураОплаты = w1_Json.UnJSON36(order, Новый Структура("order_guid", "Документ.ИнтернетЗаказ"), стрОшибки);
	Если СтруктураОплаты = Неопределено Тогда
		Возврат ВернутьСтатусОплаты("",,"Не удалось преобразовать order в структуру
											|" + стрОшибки); КонецЕсли;
	Ошибка = Ложь;
	
	Если СтруктураОплаты.Свойство("order_guid") Тогда
		ЗаказСсылка = HTTP.ПолучитьОбъектПоСсылке(Документы.ИнтернетЗаказПокупателя, СтруктураОплаты.order_guid, стрОшибки); 
		Если ЗаказСсылка = Неопределено Тогда 
			стрОшибки = "Не найден заказ по гуиду: " + СтруктураОплаты.order_guid;
			Ошибка = Истина; КонецЕсли;
	Иначе
		Ошибка = Истина;
		стрОшибки = "Не обнаружено свойство order_guid"; КонецЕсли;
	
	Если СтруктураОплаты.Свойство("pay_total") Тогда
		Попытка		Сумма = Число(СтруктураОплаты.pay_total);
		Исключение	стрОшибки = "Не удалось преобразовать в число: " + СтруктураОплаты.pay_total;
					Ошибка = Истина; КонецПопытки;
	Иначе
		Ошибка = Истина;
		стрОшибки = "Не обнаружено свойство pay_total"; КонецЕсли;
	
	ПроцентКомиссии = 0;
	 
	Если СтруктураОплаты.Свойство("payment_variant_guid") Тогда
		ВариантОплатыСсылка = HTTP.ПолучитьОбъектПоСсылке(Справочники.ВидОплатыИнтернетЗаказа, СтруктураОплаты.payment_variant_guid , стрОшибки); 
		Если ВариантОплатыСсылка = Неопределено Тогда 
			стрОшибки = "Не найден вариант оплаты по гуиду: " + СтруктураОплаты.payment_variant_guid ;
			Ошибка = Истина;
		Иначе
			Отбор = Новый Структура();
			Отбор.Вставить("ВидОплаты",ВариантОплатыСсылка);
			ПроцентКомиссииЗапись = РегистрыСведений.РазмерКомиссииДляВидаОплат.ПолучитьПоследнее(ТекущаяДата(),Отбор);
			ПроцентКомиссии = ПроцентКомиссииЗапись.ПроцентКомиссии;
		КонецЕсли;
	Иначе // это не считаем ошибкой можем и без
		СтруктураОплаты.Вставить("payment_variant_guid", Неопределено);КонецЕсли;
	
	idЗаказа = ?(СтруктураОплаты.Свойство("id"), СтруктураОплаты.id, "");
	Если Ошибка Тогда Возврат ВернутьСтатусОплаты(idЗаказа,,стрОшибки);	КонецЕсли;
	
#КонецОбласти

#Область Формирование_документа
    //если есть документ отгрузки - заполним его
	//оплата электронными деньгами может быть только по одному заказу и отгружается только одним документом
	Запрос=Новый Запрос("ВЫБРАТЬ
	                    |	РеализацияТоваров.Ссылка
	                    |ИЗ
	                    |	Документ.РеализацияТоваров КАК РеализацияТоваров
	                    |ГДЕ
	                    |	РеализацияТоваров.Заказ = &Заказ
	                    |	И РеализацияТоваров.Проведен = Истина");
	Запрос.УстановитьПараметр("Заказ",ЗаказСсылка);	
	Рез=Запрос.Выполнить();
	Если Не Рез.Пустой() Тогда
		ДокументОтгрузки=Рез.Выгрузить()[0].Ссылка;
	Иначе
		ДокументОтгрузки=Неопределено;
	КонецЕсли;	

	
	Док = Документы.ОплатаЭлектроннымиДеньгами.СоздатьДокумент();
	Док.Дата 					= ТекущаяДата();
	Док.ВидОперации 			= Перечисления.ВидыОперацийЧекККМ.Продажа;
	Док.Оплачен 				= Истина;
	Док.ИнтернетЗаказПокупателя = ЗаказСсылка;
	Док.ДокументОтгрузки	    = ДокументОтгрузки;
	Док.Валюта 					= Константы.ВалютаУправленческогоУчета.Получить();
	Док.Контрагент	 			= ЗаказСсылка.Контрагент;
	Док.Партнер 				= ЗаказСсылка.Контрагент.Партнер;
	Док.Плательщик 				= Док.Партнер;
	Док.Сумма 					= Сумма;
	Док.СуммаВключаетНДС 		= Истина;
	Док.УчитыватьНДС 			= Ложь;
	Док.СуммаКомиссии           = Сумма*ПроцентКомиссии/100;   //сумму комиссии оператор нам не передает, поэтому считаем её сами
	Док.ОператорПлатежнойСистемы=Справочники.Контрагенты.Монета;//пока оператор один ищем так, в дальнейшем нужно получать идентификатор с сайта
	
	//организацию берем из карточки контрагента
	Запрос=Новый Запрос("Выбрать Организация из Справочник.Контрагенты.Организации ГДЕ Ссылка=&Контрагент Упорядочить по ЗначениеПоУмолчанию Убыв");
	Запрос.УстановитьПараметр("контрагент",Док.ОператорПлатежнойСистемы);
	Рез=Запрос.Выполнить();
	Если Не Рез.Пустой() Тогда
		Док.Организация = Запрос.Выполнить().Выгрузить()[0].Организация;
	КонецЕсли;	
	
	
	// Запишем
	
	Если ОбщиеФункции.ЗаписатьОбъектИСообщитьЕслиОшибка(Док, РежимЗаписиДокумента.Проведение, стрОшибки, Ложь) Тогда
			Возврат ВернутьСтатусОплаты(idЗаказа, Истина,,Док.Ссылка);
	Иначе	Возврат ВернутьСтатусОплаты(idЗаказа,,стрОшибки); КонецЕсли;

#КонецОбласти
	
КонецФункции
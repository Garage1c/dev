
Функция ПолучитьНапоминания() Экспорт
	
	ЖизньОткрытияНапоминания = 60 * 60; // если больше часа то тогда можно открывать и другому
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ	Ссылка, ДатаНапоминания, Текст
	|ИЗ 		Справочник.Напоминания
	|
	|ГДЕ	ДатаНапоминания < &ТекущаяДата И
	|		(	НапоминаниеОткрыто = &ПустаяДата Или 
	|			ДОБАВИТЬКДАТЕ(НапоминаниеОткрыто, СЕКУНДА, &Жизнь) < &ТекущаяДата 
	|		) И
	|
	|		(
	|			Пользователь = &Пользователь ИЛИ
	|
	|			(	Пользователь = &ПустойПользователь И 
	|				Роль В(&РолИ) И
	|				Склад = &ПустойСклад
	|			) ИЛИ
	|
	|			(	Пользователь = &ПустойПользователь И 
	|				Роль = &ПустаяРоль И 
	|				Склад В(&Склады)
	|			)ИЛИ
	|
	|			(	Пользователь = &ПустойПользователь И 
	|				Роль В(&РолИ) И 
	|				Склад В(&Склады)
	|			)
	|		)
	|");
	
	Запрос.УстановитьПараметр("Пользователь", 	ПараметрыСеанса.ТекущийПользователь);
	//Запрос.УстановитьПараметр("Склад", 			ПараметрыСеанса.ТекущийСклад);
	Запрос.УстановитьПараметр("Склады", 		ПараметрыСеанса.ТекущиеСклады);
	Запрос.УстановитьПараметр("РолИ", 			ПараметрыСеанса.ТекущиеРоли);
	
	Запрос.УстановитьПараметр("ПустойПользователь", 	Справочники.Пользователи.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяРоль", 			Справочники.Роли.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустойСклад", 			Справочники.Склады.ПустаяСсылка());
	
	Запрос.УстановитьПараметр("ПустаяДата",		'00010101');
	Запрос.УстановитьПараметр("Жизнь",			ЖизньОткрытияНапоминания);
	Запрос.УстановитьПараметр("ТекущаяДата",	ТекущаяДата());
	
	Выполнение = Запрос.Выполнить();
	Если Выполнение.Пустой() Тогда
		
		Возврат Новый Массив; // вернем пустой массив
		
	Иначе
		
		Возврат КонвертацияТипов.ПолучитьМассивИзТаблицыЗначений(Выполнение.Выгрузить());
		
	КонецЕсли;
	
КонецФункции

Функция УстановитьНапоминание(Адресация, Источник, Текст) Экспорт
	
	НовСпр = Справочники.Напоминания.СоздатьЭлемент();
	
	НовСпр.ДатаНапоминания 	= ТекущаяДата();
	НовСпр.Источник 		= Источник;
	НовСпр.Текст 			= Текст;
	
	Адресация.Свойство("Склад", 		НовСпр.Склад);
	Адресация.Свойство("Роль", 			НовСпр.Роль);
	Адресация.Свойство("Пользователь", 	НовСпр.Пользователь);
	
	Попытка
		
		НовСпр.Записать();
		
	Исключение
		
		ОбщиеФункции.СообщитьТекст(ОписаниеОшибки());
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
		
КонецФункции

Функция УдалитьНапоминаниеПоИсточнику(Источник) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ Ссылка ИЗ Справочник.Напоминания ГДЕ Источник = &Источник");
	Запрос.УстановитьПараметр("Источник", Источник);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Ошибка 	= Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			Выборка.Ссылка.ПолучитьОбъект().Удалить();
		Исключение
			ОбщиеФункции.СообщитьТекст(ОписаниеОшибки());
			Ошибка = Истина;
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат Не Ошибка;
	
КонецФункции

Функция ПоместитьНоменклатуру(Номенклатура, Адрес = "", стрОшибки = "") Экспорт
	
	// Помещает в регистр номенклатуры и возвращает адрес помещения
	// если указан Адрес тогда поещает в указанный адрес
	// Номенклатура - таблица значений с двумя колонками "Номенклатура" и "Количество"
	
	// Определим адрес
	
	Если Адрес = "" Тогда
		Адрес = Строка(Новый УникальныйИдентификатор);
	КонецЕсли;
	
	// Удалим строки которые содержут пустую номенклатуру
	
	Номенклатура.Свернуть("Номенклатура", "Количество"); 
	
	ПустыеСтроки = Номенклатура.НайтиСтроки(Новый Структура("Номенклатура", Справочники.Номенклатура.ПустаяСсылка()));
	
	КолПустых 	= ПустыеСтроки.Количество();
	Инд			= КолПустых;
	
	Для Ном = 1 По КолПустых Цикл Инд = Инд - 1;
		Номенклатура.Удалить(ПустыеСтроки[Инд]);
	КонецЦикла;
	
	// Добавим колонку
	
	Если Номенклатура.Колонки.Найти("АдресХранилища") = Неопределено Тогда
		Номенклатура.Колонки.Добавить("АдресХранилища", Новый ОписаниеТипов("Строка"));
	КонецЕсли;
	
	Если Номенклатура.Колонки.Найти("Пользователь") = Неопределено Тогда
		Номенклатура.Колонки.Добавить("Пользователь", Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	КонецЕсли;
	
	Если Номенклатура.Колонки.Найти("ДатаДобавления") = Неопределено Тогда
		Номенклатура.Колонки.Добавить("ДатаДобавления", Новый ОписаниеТипов("Дата"));
	КонецЕсли;
	
	// Загрузим таблицу в регистр
	
	Номенклатура.ЗаполнитьЗначения(Адрес, 								"АдресХранилища");
	Номенклатура.ЗаполнитьЗначения(ПараметрыСеанса.ТекущийПользователь, "Пользователь");
	Номенклатура.ЗаполнитьЗначения(ТекущаяДата(), 						"ДатаДобавления");
	
	Набор = РегистрыСведений.ТоварыВДокументеДляПодбора.СоздатьНаборЗаписей();
	Набор.Отбор.АдресХранилища.Установить(Адрес);
	Набор.Загрузить(Номенклатура);
	
	// Запишем
	
	Попытка
		Набор.Записать();
	Исключение
		Ошибка 		= ОписаниеОшибки();
		стрОшибки 	= "Ошибка помещения номенклатуры во временное хранилище
						|" + Ошибка;
		Возврат Неопределено;
	КонецПопытки;
	
	// Вернем адрес
	
	Возврат Адрес;
	
КонецФункции
Функция УдалитьНоменклатуру(Адрес = "", стрОшибки = "") Экспорт
	
	// Удаляет в регистре записи
	// если не указан Адрес тогда удалит все нах
	//
	// Возвращает Истина если удалось удалить и Ложь если произошла ошибка
	
	Набор = РегистрыСведений.ТоварыВДокументеДляПодбора.СоздатьНаборЗаписей();
	Набор.Отбор.АдресХранилища.Установить(Адрес);
	
	// Запишем
	
	Попытка
		Набор.Записать();
	Исключение
		Ошибка 		= ОписаниеОшибки();
		стрОшибки 	= "Ошибка при удалении номенклатуры из временного хранилища
						|" + Ошибка;
		Возврат Ложь;
	КонецПопытки;
	
	// Вернем адрес
	
	Возврат Истина;
	
КонецФункции

Функция ИзменитьКоличествоНоменклатуры(Адрес, Строка, стрОшибки = "") Экспорт
	
	// Удаляет или дбаляет запись из временной таблицы (уменьшает увеличивает на переданное количество и если по нулям то удаляет)
	// строка - множество с двумя колонками "Номенклатура" и "Количество"
	//
	// Возвращает Истина если удалось удалить и Ложь если произошла ошибка
	
	// Получим того что есть
	
	Запрос = Новый Запрос;	
	ТекстЗапроса = 	"ВЫБРАТЬ 
					|	Количество 
					|ИЗ 
					|	РегистрСведений.ТоварыВДокументеДляПодбора 
					|ГДЕ 
					|	АдресХранилища 	= """ + Адрес + """ И
					|	Номенклатура 	= &Номенклатура";
					
	ЕстьЗаказ = Строка.Свойство("ЗаказПоставщику") И ЗначениеЗаполнено(Строка.ЗаказПоставщику);
					
	Если ЕстьЗаказ Тогда				
		ТекстЗапроса = ТекстЗапроса + "	И ЗаказПоставщику = &ЗаказПоставщику";
	    Запрос.УстановитьПараметр("ЗаказПоставщику", Строка.ЗаказПоставщику);
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("Номенклатура", Строка.Номенклатура);
	
	Выполнение 				= Запрос.Выполнить();
	ЕстьЗапись 				= Не Выполнение.Пустой();     
	КоличествоВДокументе 	= 0;
	
	Если ЕстьЗапись Тогда
		
		Выборка = Выполнение.Выбрать();
		Выборка.Следующий();
		КоличествоВДокументе = Выборка.Количество;
		
	КонецЕсли;
	
	// Узнаем сколько стало
	
	СталоКоличество = КоличествоВДокументе + Строка.Количество;
	
	Если СталоКоличество Тогда
		
		// Изменим количество
		
		Запись = РегистрыСведений.ТоварыВДокументеДляПодбора.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, Строка);
		Запись.АдресХранилища 	= Адрес;
		Запись.Пользователь 	= ПараметрыСеанса.ТекущийПользователь;
		Запись.ДатаДобавления 	= ТекущаяДата();
		Запись.Количество 		= СталоКоличество;

	Иначе
		
		// Удалим запись
		
		Запись = РегистрыСведений.ТоварыВДокументеДляПодбора.СоздатьНаборЗаписей();
		Запись.Отбор.АдресХранилища.Установить(Адрес);
		Запись.Отбор.Номенклатура.Установить(Строка.Номенклатура);
		Запись.Отбор.ЗаказПоставщику.Установить(Строка.ЗаказПоставщику);
	КонецЕсли;
	
	// Запишем результат
	
	Попытка
		Запись.Записать();                   
	Исключение
		Ошибка 		= ОписаниеОшибки();
		стрОшибки 	= "Ошибка при помещении записи номенклатуры во временное хранилище
						|" + Ошибка;
		Возврат Ложь;
	КонецПопытки;
	
	// Вернем адрес
	
	Возврат Истина;
	
КонецФункции


Функция УвеличитьГуид(стр)
	
	Возврат Лев(стр, 8) + "-" + Сред(стр, 9, 4) + "-" + Сред(стр, 13, 4) + "-" + Сред(стр, 17, 4) + "-" + Прав(Стр, 12);	// 8d583a41-0e7c-4a76-87dd-cba4f4310365
	
КонецФункции

Процедура НажатиеСкрепка(Ссылка, Форма) Экспорт

	Если Ссылка.Пустая() Тогда
		ПоказатьПредупреждение(,"Перед добавлением файла необходимо записать объект");
		
	Иначе
	
		ДВ = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		ДВ.МножественныйВыбор = Истина;
		//ДВ.Фильтр = "Скан (pdf)|*.pdf|Эксель (xls)|*.xls*|Ворд (doc)|*.doc*|Текст (txt)|*.txt";
		ДВ.Фильтр = "Сканы, эксели, ворды, текстуха|*.pdf;*.xls;*.xlsx;*.doc;*.docx;*.txt";
		
		НачатьПомещениеФайлов(Новый ОписаниеОповещения("ВыбраныФайлы", ПрикрепленныеФайлыКлиент, Новый Структура("Ссылка, Форма", Ссылка, Форма)),,ДВ,,Форма.УникальныйИдентификатор); КонецЕсли;
	
КонецПроцедуры
Процедура ВыбраныФайлы(ПомещенныеФайлы, ДопПараметры) Экспорт
	
	Если ПомещенныеФайлы <> Неопределено Тогда
		
		Ном = 0; Кол = ПомещенныеФайлы.Количество();
		Для Каждого Файл Из ПомещенныеФайлы Цикл Ном = Ном + 1;
			
			Если Не ПрикрепленныеФайлы.ПоместитьФайлВХранилище(Файл, ДопПараметры.Ссылка) Тогда
				ПоказатьПредупреждение(,"Ошибка помещения файла " + Файл.Имя,,"Ошибка"); КонецЕсли;
			
			ОбработкаПрерыванияПользователя();
			Состояние("Загрузка файла на сервер", Ном / Кол * 100, Файл.Имя, БиблиотекаКартинок.Сохранить); КонецЦикла; 
		
		ДопПараметры.Форма.ОбновитьВидимостьПрикрепленныхФайлов(); КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьФайл(КодВозврата, ДополнительныеПараметры) Экспорт
	
	//пока удалять не будем, т.к. не все это могут НачатьУдалениеФайлов(,ДополнительныеПараметры.Имя);
	
КонецПроцедуры
Процедура ФайлПолучен(ПолученныеФайлы, ДопПараметры) Экспорт
	
	Если ПолученныеФайлы <> Неопределено Тогда
		НачатьЗапускПриложения(Новый ОписаниеОповещения("УдалитьФайл", ПрикрепленныеФайлыКлиент, Новый Структура("Имя, Каталог", ПолученныеФайлы[0].Имя, ДопПараметры.Каталог)),ПолученныеФайлы[0].Имя,ДопПараметры.Каталог, Истина); КонецЕсли;
	
	// Убираемся раз насвинячили
	УдалитьИзВременногоХранилища(ДопПараметры.Адрес);
	
КонецПроцедуры
Процедура КаталогВрФайловДляПолученияПолучен(ИмяКаталогаВременныхФайлов, Файл) Экспорт
	
	Если ИмяКаталогаВременныхФайлов <> Неопределено Тогда
		
		Файлы = Новый Массив;
		Файлы.Добавить(Новый ОписаниеПередаваемогоФайла(Файл.Имя, Файл.Адрес));
		
		НачатьПолучениеФайлов(Новый ОписаниеОповещения("ФайлПолучен", ПрикрепленныеФайлыКлиент, Новый Структура("Адрес, Каталог", Файл.Адрес, ИмяКаталогаВременныхФайлов)), Файлы, ИмяКаталогаВременныхФайлов ,Ложь);  КонецЕсли;
	
КонецПроцедуры
Процедура ОткрытьПрикрепленныйФайл(ИмяЭлемента) Экспорт
	
	guid = УвеличитьГуид(СтрЗаменить(ИмяЭлемента, "Файл", ""));
	
	Файл = ПрикрепленныеФайлы.ПолучитьФайлИзХранилища(guid);
	Если Файл = Неопределено Тогда
			ПоказатьПредупреждение(,"Файл не найден на сервере");
	Иначе	НачатьПолучениеКаталогаВременныхФайлов(Новый ОписаниеОповещения("КаталогВрФайловДляПолученияПолучен", ПрикрепленныеФайлыКлиент, Новый Структура("Имя, Адрес", Файл.Имя, Файл.Адрес))); КонецЕсли;
	
КонецПроцедуры

Процедура ВыбранФайлДляПросмотра(ВыбЭлемент, ДопПараметры) Экспорт
	
	Если ВыбЭлемент <> Неопределено Тогда
		ОткрытьПрикрепленныйФайл(ВыбЭлемент.Значение); КонецЕсли;
	
КонецПроцедуры
Процедура ПоказатьПрикрепленныеФайлы(Ссылка, Форма, Элемент) Экспорт
	
	Форма.ПоказатьВыборИзМеню(Новый ОписаниеОповещения("ВыбранФайлДляПросмотра", ПрикрепленныеФайлыКлиент), ПрикрепленныеФайлы.ПолучитьСписокФайловДляВыбора(Ссылка), Элемент);
	
КонецПроцедуры

Процедура УдалитьФайлПоСжатомуГуиду(guid, Форма)
	
	Если ПрикрепленныеФайлы.УдалитьФайл(УвеличитьГуид(guid)) Тогда
			Форма.ОбновитьВидимостьПрикрепленныхФайлов();
			ПоказатьОповещениеПользователя("Файл удален",,,БиблиотекаКартинок.Удалить);
	Иначе	ПоказатьПредупреждение(,"Ошибка удаления файла",,"Ошибка"); КонецЕсли;
	
КонецПроцедуры
Процедура УдалитьФайлОтвет(Ответ, ДопПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		УдалитьФайлПоСжатомуГуиду(ДопПараметры.guid, ДопПараметры. Форма); КонецЕсли;
	
КонецПроцедуры
Процедура ВыбранФайлДляУдаленияИзСписка(ВыбЭлемент, ДопПараметры) Экспорт
	
	Если ВыбЭлемент <> Неопределено Тогда
		УдалитьФайлПоСжатомуГуиду(ВыбЭлемент.Значение, ДопПараметры. Форма); КонецЕсли;
	
КонецПроцедуры
Процедура УдалитьНажатие(Ссылка, Форма, Элемент) Экспорт
	
	Список = ПрикрепленныеФайлы.ПолучитьСписокФайловДляВыбора(Ссылка);
	Если Список.Количество() = 1 Тогда
		
			ПоказатьВопрос(Новый ОписаниеОповещения("УдалитьФайлОтвет", ПрикрепленныеФайлыКлиент, Новый Структура("guid, Форма", Список[0].Значение, Форма)), "Удалить файл?", РежимДиалогаВопрос.ДаНет);
	Иначе	Форма.ПоказатьВыборИзМеню(Новый ОписаниеОповещения("ВыбранФайлДляУдаленияИзСписка", ПрикрепленныеФайлыКлиент, Новый Структура("Форма", Форма)), Список, Элемент); КонецЕсли;
	
КонецПроцедуры
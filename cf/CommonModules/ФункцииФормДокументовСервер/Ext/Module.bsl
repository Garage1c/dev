#Область Грузополучатель

&НаСервере
Процедура УстановитьСвязиГрузополучателя(Объект,Элементы,СтруктураСвязей) Экспорт
	ПустойПартнераКонтрагент = Справочники[?(ПолучитьФункциональнуюОпцию("НемецкийУчет"), "Партнеры", "Грузополучатели")].ПустаяСсылка();
	Если Объект.Свойство("Грузоотправитель") Тогда Если НЕ ЗначениеЗаполнено(Объект.Грузоотправитель) Тогда Объект.Грузоотправитель = ПустойПартнераКонтрагент; ИначеЕсли ТипЗнч(Объект.Грузоотправитель)<>Тип("СправочникСсылка.Грузополучатели") Тогда Элементы.Грузоотправитель.ВыбиратьТип = Истина; КонецЕсли; КонецЕсли;
	Если Объект.Свойство("Грузополучатель")  Тогда Если НЕ ЗначениеЗаполнено(Объект.Грузополучатель)  Тогда Объект.Грузополучатель  = ПустойПартнераКонтрагент; ИначеЕсли ТипЗнч(Объект.Грузополучатель) <>Тип("СправочникСсылка.Грузополучатели") Тогда Элементы.Грузополучатель.ВыбиратьТип  = Истина; КонецЕсли; КонецЕсли;
	Для Каждого ТекЭлемент Из СтруктураСвязей Цикл
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(Новый СвязьПараметраВыбора("Отбор.Владелец", ТекЭлемент.Значение, РежимИзмененияСвязанногоЗначения.Очищать));
		Элементы[ТекЭлемент.Ключ].СвязиПараметровВыбора  = Новый ФиксированныйМассив(НовыйМассив);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ГрузополучательПриИзмененииРеквизита(Реквизит) Экспорт
	Если НЕ ЗначениеЗаполнено(Реквизит) Тогда
		Возврат Справочники.Грузополучатели.ПустаяСсылка();
	КонецЕсли;
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ОсновнойГрузополучатель.Грузополучатель
	                      |ИЗ
	                      |	РегистрСведений.ОсновнойГрузополучатель КАК ОсновнойГрузополучатель
	                      |ГДЕ
	                      |	ОсновнойГрузополучатель.Контрагент = &Контрагент
	                      |	И НЕ ОсновнойГрузополучатель.Грузополучатель.ПометкаУдаления");
	Запрос.УстановитьПараметр("Контрагент",Реквизит);
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		Возврат Результат.Грузополучатель;
	Иначе
		Возврат Справочники.Грузополучатели.ПустаяСсылка();
	КонецЕсли;
КонецФункции

&НаСервере
Функция БанковскийСчетПриИзмененииРеквизита(Грузополучатель) Экспорт
	Если НЕ ЗначениеЗаполнено(Грузополучатель) Тогда
		Возврат Справочники.БанковскиеСчета.ПустаяСсылка();
	КонецЕсли;
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	БанковскиеСчета.Ссылка
	                      |ИЗ
	                      |	Справочник.БанковскиеСчета КАК БанковскиеСчета
	                      |ГДЕ
	                      |	БанковскиеСчета.Владелец = &Владелец
	                      |	И НЕ БанковскиеСчета.ПометкаУдаления
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	БанковскиеСчета.ЗначениеПоУмолчанию УБЫВ");
	Запрос.УстановитьПараметр("Владелец",Грузополучатель);
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		Возврат Результат.Ссылка;
	Иначе
		Возврат Справочники.БанковскиеСчета.ПустаяСсылка();
	КонецЕсли;
КонецФункции

&НаСервере
Функция МенеджерЗаказаПриИзмененииРеквизита(Контрагент,Грузополучатель) Экспорт
	Если ЗначениеЗаполнено(Грузополучатель) Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	ОсновнойМенеджерГрузополучателяСрезПоследних.Менеджер
		                      |ИЗ
		                      |	РегистрСведений.ОсновнойМенеджерГрузополучателя.СрезПоследних(&ТекущаяДата, ) КАК ОсновнойМенеджерГрузополучателяСрезПоследних
		                      |ГДЕ
		                      |	ОсновнойМенеджерГрузополучателяСрезПоследних.Грузополучатель = &Грузополучатель");
		Запрос.УстановитьПараметр("ТекущаяДата",ТекущаяДата());
		Запрос.УстановитьПараметр("Грузополучатель",Грузополучатель);
		Результат = Запрос.Выполнить().Выбрать();
		Если Результат.Следующий() Тогда
			Возврат Результат.Менеджер;
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(Контрагент) Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	ОсновнойМенеджерПартнераСрезПоследних.Менеджер
		                      |ИЗ
		                      |	РегистрСведений.ОсновнойМенеджерПартнера.СрезПоследних(&ТекущаяДата, ) КАК ОсновнойМенеджерПартнераСрезПоследних
		                      |ГДЕ
		                      |	ОсновнойМенеджерПартнераСрезПоследних.Контрагент = &Контрагент");
		Запрос.УстановитьПараметр("ТекущаяДата",ТекущаяДата());
		Запрос.УстановитьПараметр("Контрагент",Контрагент);
		Результат = Запрос.Выполнить().Выбрать();
		Если Результат.Следующий() Тогда
			Возврат Результат.Менеджер;
		КонецЕсли;
	КонецЕсли;
	Возврат Справочники.Пользователи.ПустаяСсылка();
КонецФункции

#КонецОбласти



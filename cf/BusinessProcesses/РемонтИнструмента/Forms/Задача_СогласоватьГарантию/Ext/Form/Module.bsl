
&НаКлиенте
Перем МассивКомментариев Экспорт;

// ТИПОВЫЕ

&НаКлиенте
Процедура ПоказатьСкрытьАдресацию(Команда)
	
	// Реверснем
	
	КоманднаяПанель.ПодчиненныеЭлементы.ПоказатьСкрытьАдресацию.Пометка = Не КоманднаяПанель.ПодчиненныеЭлементы.ПоказатьСкрытьАдресацию.Пометка;
	
	// Обновим видимость
	
	ФункцииФормЗадач.ВидимостьАдресации(ЭтаФорма);
	
КонецПроцедуры
&НаКлиенте
Процедура ВидимостьДоступность()
	
	Элементы.Товары.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ОсновныеДействия.Доступность = Не ТолькоПросмотр;

КонецПроцедуры
&НаСервере
Процедура ПрочитатьРеквизиты()
	
	ФункцииБизнесПроцессов.ЗаполнитьДанные(ЭтаФорма, Объект.БизнесПроцесс, Объект.Ссылка);
		
КонецПроцедуры



&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПрочитатьРеквизиты();
	
	// комментарии
	ФункцииБизнесПроцессов.ДобавитьРаботуСКомментариями(ЭтаФорма);
	
КонецПроцедуры
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ФункцииФормЗадач.ПриОткрытии(Объект, ЭтаФорма, Отказ);
	// комментарии
	ФункцииБизнесПроцессовКлиент.ПолучитьМассивКомментариев(ЭтаФорма, Объект.БизнесПроцесс);
	
	ВидимостьДоступность();
				
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ СохранитьРешениеОГарантии(Ложь) Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры
 &НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ФункцииФормЗадач.ПослеЗаписи(Объект, ЭтаФорма, ПараметрыЗаписи);
	
	ВидимостьДоступность();
	
КонецПроцедуры
 
// ВЫПОЛНЕНИЕ

&НаСервере
Функция СохранитьРешениеОГарантии(ГарантияСогласована, ТаблицаИзменилась = Неопределено)
		
	Процесс = Объект.БизнесПроцесс.ПолучитьОбъект();
	Изменилось = Ложь;
	
	Если Процесс.ГарантияСогласована <> ГарантияСогласована Тогда
		
		Процесс.ГарантияСогласована = ГарантияСогласована;
		Изменилось = Истина;
		
	КонецЕсли;
	
	Если ТаблицаИзменилась = Неопределено Тогда
		ТаблицаИзменилась = ИзменилисьУсловияГарантии();
	КонецЕсли;
	
	Если ТаблицаИзменилась Тогда
		
		Процесс.Товары.Загрузить(Товары.Выгрузить());
		Изменилось = Истина;
		
	КонецЕсли;

	Если Изменилось Тогда
		
		Процесс.Товары.Загрузить(Товары.Выгрузить());
	
		Попытка
			Процесс.Записать();
		Исключение
			стрОшибки = ОписаниеОшибки();
			ОбщиеФункции.СообщитьТекст("Ошибка при записи бизнес процесса
						|" + стрОшибки);
			Возврат Ложь;
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции
&НаСервере
Функция ИзменилисьУсловияГарантии()
	
	Процесс = Объект.БизнесПроцесс;
	
	// Определим изменилась ли гарантия или нет
	
	Если Товары.Количество() <> Процесс.Товары.Количество() Тогда
		
		Возврат Истина;
		
	Иначе
		
		Инд = -1;
		Для Каждого Строка Из Товары Цикл Инд = Инд + 1;
			
			Если Строка.Гарантия <> Процесс.Товары[Инд].Гарантия Тогда
				
				Возврат Истина;
				
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

&НаСервере
Функция ВыполнитьЗадачуНаСервере() Экспорт
	
	ЗадачаОбъект = РеквизитФормыВЗначение("Объект");
	
	Попытка
		ЗадачаОбъект.ВыполнитьЗадачу();
	Исключение
		стрОшибки = ОписаниеОшибки();
		ОбщиеФункции.СообщитьТекст("Ошибка при выполнении задачи
						|" + стрОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ГарантияСогласована(Команда)
	
	ТаблицаИзменилась = ИзменилисьУсловияГарантии();
	
	Если ТаблицаИзменилась И
		Вопрос("Я подтверждаю, что с новыми условиями предоставления гарантии клиент согласен" , РежимДиалогаВопрос.ОКОтмена) = КодВозвратаДиалога.Отмена Тогда
			Возврат;
	КонецЕсли;
		
 	// Выполним задачу
	
	Если 	СохранитьРешениеОГарантии(Истина, ТаблицаИзменилась) И
			ВыполнитьЗадачуНаСервере() Тогда
			
		Модифицированность = Ложь;
		ФункцииФормЗадач.ПослеЗаписи(Объект, ЭтаФорма, Новый Структура);
		Закрыть();
				
	КонецЕсли;

КонецПроцедуры
&НаКлиенте
Процедура КлиентОтказался(Команда)
	
	// Выполним задачу
			
	Если 	СохранитьРешениеОГарантии(Ложь) И
			ВыполнитьЗадачуНаСервере() Тогда
			
		Модифицированность = Ложь;
		ФункцииФормЗадач.ПослеЗаписи(Объект, ЭтаФорма, Новый Структура);
		Закрыть();
				
	КонецЕсли;
	
	
КонецПроцедуры

// КОММЕНТАРИИ

&НаКлиенте
Процедура КомандаПоказатьКомментарий(Команда)
	ФункцииБизнесПроцессовКлиент.КомандаПоказатьКомментарий(ЭтаФорма);
КонецПроцедуры





Функция ПолучитьТаблицуТоваров(СсылкаПроцесс, СсылкаЗадача = Неопределено) Экспорт
	
	// Определим дату считывания
		
	ОперативнаяДата = 
				СсылкаЗадача <> Неопределено И
				СсылкаЗадача.Выполнена;
		
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Номенклатура,
	|	КоличествоОборот 	Количество,
	|	СуммаОборот			Сумма
	|
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(," + ?(ОперативнаяДата, "&ДатаВыполнения","") + ",,Смена = &Ссылка)
	|
	|");
		
	Запрос.УстановитьПараметр("Ссылка", СсылкаПроцесс);
	
	Если ОперативнаяДата Тогда
		Запрос.УстановитьПараметр("ДатаВыполнения", СсылкаЗадача.ДатаВыполнения);
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ЗаполнитьФормуПоБП(Форма, СсылкаПроцесс, СсылкаЗадача = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(СсылкаПроцесс) Тогда
		
		Запрос = Новый Запрос("
		
		// Условия
		
		|ВЫБРАТЬ 	
		|	Касса
		|ПОМЕСТИТЬ
		|	Шапка
		|ИЗ
		|	БизнесПроцесс.ПродажаВРозницу КАК Процесс
		|ГДЕ
		|	Ссылка = &СсылкаБП
		|;
		
		// Шапка
		
		|ВЫБРАТЬ 	
		|	Касса
		|ИЗ
		|	Шапка
		|");
		
		Запрос.УстановитьПараметр("СсылкаБП", СсылкаПроцесс);
		Результаты = Запрос.ВыполнитьПакет();
		
		// Заполним шапку
		
		ВыборкаШапки = Результаты[1].Выбрать();
		ВыборкаШапки.Следующий();
		
		ЗаполнитьЗначенияСвойств(Форма, ВыборкаШапки);	

		// Заполним таблицу
		
		ТаблицаТоваров = ПолучитьТаблицуТоваров(СсылкаПроцесс, СсылкаЗадача);
		Форма.Товары.Загрузить(ТаблицаТоваров);
		Форма.СуммаТоваров 		= ТаблицаТоваров.Итог("Сумма");
		Форма.КоличествоТоваров = ТаблицаТоваров.Итог("Количество");
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьМассивКомментариев(СсылкаПроцесс) Экспорт
	
	Массив = Новый Массив;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Комментарий, Номер, Дата, Автор
	|ИЗ
	|	Документ.ЧекККМ
	|ГДЕ
	|	Процесс = &Ссылка И
	|	ПометкаУдаления = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|");
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаПроцесс);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Не ПустаяСтрока(Выборка.Комментарий) Тогда
		
			Массив.Добавить(Новый Структура("Комментарий, Заголовок, ДатаВыполнения, Исполнитель",
									Выборка.Комментарий, 
									"Чек №" + Выборка.Номер,
									Выборка.Дата, 
									Выборка.Автор));
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Массив;
	
КонецФункции

Функция ПолучитьТекущуюСмену(Касса) Экспорт
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	|	Ссылка КАК Процесс	
	|ИЗ
	|	БизнесПроцесс.ПродажаВРозницу
	|ГДЕ
	|	Касса = &Касса И НЕ Завершен
	|");
	Запрос.УстановитьПараметр("Касса", Касса);
	Выборка  = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Процесс;
	КонецЕсли;
	Возврат Неопределено;
КонецФункции
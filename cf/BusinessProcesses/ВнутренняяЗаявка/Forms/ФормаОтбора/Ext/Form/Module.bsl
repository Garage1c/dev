
&НаКлиенте
Процедура ВыполнитьЗагрузку(Команда)
	АдресТаблицы = ПрочитатьДанные();	
	ВладелецФормы.ЗаполнитьПоРекомендуемымОстаткамНаСервере(АдресТаблицы, СписокСкладов);
	//Оповестить("УстановленОтборДляРазмещения", Новый Структура("СписокСкладов",СписокСкладов));
	Закрыть();
	//Закрыть(Новый Структура("СписокСкладов",СписокСкладов));
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//Параметры открытия формы
	ГУИДВладельца = Параметры.ГУИДВладельца;
	СкладОтправитель = Параметры.СкладОтправитель;
	СкладПолучатель = Параметры.СкладПолучатель;
	//СписокСкладов.Добавить(СкладОтправитель);

	////////////////////////////////////////////////////////////////////////
	///получим схему компоновки использую хитрую процедуру из модуля объекта
	_ЭтотОбъект = РеквизитФормыВЗначение("Объект");
	Схема = _ЭтотОбъект.ПолучитьСхемуКомпоновки();
	Настройки = Схема.НастройкиПоУмолчанию;
	///установим параметры
	
	СпЗначений = Новый СписокЗначений();
	СпЗначений.Добавить(СкладОтправитель);
	ПараметрСкладОтправитель = Настройки.ПараметрыДанных.Элементы.Найти("СкладыОтправитель");
	ПараметрСкладОтправитель.Использование = истина;
	ПараметрСкладОтправитель.Значение = СпЗначений;
	///
	ПараметрСкладПолучатель = Настройки.ПараметрыДанных.Элементы.Найти("СкладПолучатель");
	ПараметрСкладПолучатель.Использование = Истина;
	ПараметрСкладПолучатель.Значение = СкладПолучатель;
	
	///свяжем Компоновщик Настроек КОМПОНОВКИ данных на форме с Компоновщиком Настроек Компоновки Данных
	///чтоб пользователь мог компоновать настройки компоновки данных (в нашем случае это отбор) и компоновщик макета потом мог получить оттуда скомпонованные настройки компоновки данных.
	///Компоновщик компоновал компоновал да не выкомпоновал.
	АдресСхемы = ПоместитьВоВременноеХранилище(Схема, УникальныйИдентификатор);
	КомпНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы));
	КомпНастроек.ЗагрузитьНастройки(Схема.НастройкиПоУмолчанию);
	
	
КонецПроцедуры

&НаСервере
Функция ПрочитатьДанные()
	//Получим схему компоновки данных
	_ЭтотОбъект = РеквизитФормыВЗначение("Объект");
	СКД = _ЭтотОбъект.ПолучитьСхемуКомпоновки();
	
	//Получим настройки
	Настройки = КомпНастроек.ПолучитьНастройки(); //СКД.НастройкиПоУмолчанию;
	
	ПараметрСкладыОтправитель = Настройки.ПараметрыДанных.Элементы.Найти("СкладыОтправитель");
	ПараметрСкладыОтправитель.Использование = истина;
	СписокСкладов = ПараметрСкладыОтправитель.Значение;

		
	//Сформируем макет
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СКД, Настройки,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"),,);
	
	//Выполним компоновку
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки);
	
	//Выведем результат компоновки в таблицу значений
	//ДокументРезультат = РеквизитФормыВЗначение("Табл", Тип("ТаблицаЗначений"));
	ДокументРезультат = Новый ТаблицаЗначений();
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	ПроцессорВывода.УстановитьОбъект(ДокументРезультат); 
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	АдресТаблицы = ПоместитьВоВременноеХранилище(ДокументРезультат, ГУИДВладельца);
	Возврат АдресТаблицы;
	
КонецФункции


Процедура ИницилизироватьДополнительныеДанныеДокумента(Ссылка, ДополнительныеСвойства) Экспорт
	ИменаРегистров = Новый Массив;
	ПоВсем=Ложь;
	Если Не ДополнительныеСвойства.Свойство("ИменаРегистров",ИменаРегистров) Тогда
		ПоВсем=Истина;
		ИменаРегистров = Новый Массив;
	КонецЕсли;	
	
	
	ТекстЗапроса=КэшируемыеФункции.ТектЗапросаПолученияПараметровСистемы() +Символы.ПС+";"+Символы.ПС+"/////////////////////////////////////////"+Символы.ПС+
	
	"ВЫБРАТЬ
	|	Склад, МОЛ
	|ИЗ
	|	Документ.ВыдачаНаМОЛ
	|ГДЕ
	|	Ссылка = &Ссылка";
	
	
	Нтаб=Новый Структура;
	ТекНомер=1;	
	
	Если ИменаРегистров.Найти("ТоварыНаСкладах")<>Неопределено ИЛИ ПоВсем Тогда
		ТекНомер=ТекНомер+1;
		Нтаб.Вставить("ТоварыНаСкладах",ТекНомер);
		
		ТекстЗапроса=ТекстЗапроса+Символы.ПС+";"+Символы.ПС+"/////////////////////////////////////////"+Символы.ПС+
	
		
		// ТОВАРЫ НА СКЛАДАХ
		
		"ВЫБРАТЬ
		|	&Период КАК Период,
		|	&ВидДвиженияРасход КАК ВидДвижения,
		|	ВыдачаНаМОЛТовары.Ссылка.Склад КАК Склад,
		|	ВыдачаНаМОЛТовары.Номенклатура,
		|	ВЫБОР
		|		КОГДА ВыдачаНаМОЛТовары.Упаковка = &ПустаяУпаковка
		|			ТОГДА СУММА(ВыдачаНаМОЛТовары.Количество)
		|		ИНАЧЕ СУММА(ВыдачаНаМОЛТовары.Количество * ВыдачаНаМОЛТовары.Упаковка.Коэффициент)
		|	КОНЕЦ КАК Количество
		|ИЗ
		|	Документ.ВыдачаНаМОЛ.Товары КАК ВыдачаНаМОЛТовары
		|ГДЕ
		|	ВыдачаНаМОЛТовары.Ссылка = &Ссылка
		|	И НЕ ВыдачаНаМОЛТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВыдачаНаМОЛТовары.Ссылка,
		|	ВыдачаНаМОЛТовары.Номенклатура,
		|	ВыдачаНаМОЛТовары.Упаковка,
		|	ВыдачаНаМОЛТовары.Ссылка.Склад"
		
	КонецЕсли;
	
	Если ИменаРегистров.Найти("ТоварыМОЛ")<>Неопределено ИЛИ ПоВсем Тогда
		ТекНомер=ТекНомер+1;
		Нтаб.Вставить("ТоварыМОЛ",ТекНомер);
		
		ТекстЗапроса=ТекстЗапроса+Символы.ПС+";"+Символы.ПС+"/////////////////////////////////////////"+Символы.ПС+
	
		
		// ТОВАРЫ Материально ответственных лиц
		
		"ВЫБРАТЬ
		|	&Период КАК Период,
		|	&ВидДвиженияПриход КАК ВидДвижения,
		|	ВыдачаНаМОЛТовары.Ссылка.МОЛ КАК МОЛ,
		|	ВыдачаНаМОЛТовары.Номенклатура,
		|	ВЫБОР
		|		КОГДА ВыдачаНаМОЛТовары.Упаковка = &ПустаяУпаковка
		|			ТОГДА СУММА(ВыдачаНаМОЛТовары.Количество)
		|		ИНАЧЕ СУММА(ВыдачаНаМОЛТовары.Количество * ВыдачаНаМОЛТовары.Упаковка.Коэффициент)
		|	КОНЕЦ КАК Количество
		|ИЗ
		|	Документ.ВыдачаНаМОЛ.Товары КАК ВыдачаНаМОЛТовары
		|ГДЕ
		|	ВыдачаНаМОЛТовары.Ссылка = &Ссылка
		|	И НЕ ВыдачаНаМОЛТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВыдачаНаМОЛТовары.Ссылка,
		|	ВыдачаНаМОЛТовары.Номенклатура,
		|	ВыдачаНаМОЛТовары.Упаковка,
		|	ВыдачаНаМОЛТовары.Ссылка.Склад"
		
	КонецЕсли;

	
	Запрос=Новый Запрос;
	Запрос.Текст=ТекстЗапроса;
	
	//Запрос.УстановитьПараметр("Область", 			ПараметрыСеанса.ТекущаяОбласть);
	Запрос.УстановитьПараметр("Ссылка", 			Ссылка);
	Запрос.УстановитьПараметр("Период", 			Ссылка.Дата);
	//Запрос.УстановитьПараметр("ПустаяЯчейка", 		Справочники.Ячейки.ПустаяСсылка());
	Запрос.УстановитьПараметр("ВидДвиженияПриход", 	ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("ВидДвиженияРасход", 	ВидДвиженияНакопления.Расход);
	Запрос.УстановитьПараметр("ПустаяУпаковка", 	Справочники.УпаковкиНоменклатуры.ПустаяСсылка());
	
	Запрос.УстановитьПараметр("Организация", 		Ссылка.Организация);
	//Запрос.УстановитьПараметр("Контрагент", 		Ссылка.Контрагент);
	//Запрос.УстановитьПараметр("ВидОперацииОплата", 	Перечисления.ВидыОперацийВзаиморасчетов.Отгрузка);
	//Запрос.УстановитьПараметр("ФормаОтношений", 	Перечисления.ФормаОтношений.Поставщик);
	//Запрос.УстановитьПараметр("ПустойЗаказПоставщику", Документы.ЗаказПоставщику.ПустаяСсылка());
	
	
	Пакет = Запрос.ВыполнитьПакет();
	
	ДополнительныеСвойства.Вставить("ПараметрыСистемы", КонвертацияТипов.ПолучитьСтруктуруИзСТрокиТаблицыЗначений(Пакет[0].Выгрузить()));
	ДополнительныеСвойства.Вставить("Шапка", 			КонвертацияТипов.ПолучитьСтруктуруИзСТрокиТаблицыЗначений(Пакет[1].Выгрузить()));
	
	Для Каждого Элем Из Нтаб Цикл
		ДополнительныеСвойства.Вставить(Элем.Ключ,Пакет[Элем.Значение].Выгрузить());
	КонецЦикла;	
	

КонецПроцедуры

Процедура Печать_М11(ТабДок, Ссылка) Экспорт
	Макет = ПолучитьМакет("М11");
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(ВыдачаНаМОЛ.Организация) КАК Организация,
		|	ПРЕДСТАВЛЕНИЕ(ВыдачаНаМОЛ.МОЛ) КАК Получатель,
		|	ПРЕДСТАВЛЕНИЕ(ВыдачаНаМОЛ.Склад) КАК Отправитель,
		|	ВыдачаНаМОЛ.Дата КАК ДатаСоставления
		|ИЗ
		|	Документ.ВыдачаНаМОЛ КАК ВыдачаНаМОЛ
		|ГДЕ
		|	ВыдачаНаМОЛ.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(ВыдачаНаМОЛТовары.Номенклатура) КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА ВыдачаНаМОЛТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
		|			ТОГДА ПРЕДСТАВЛЕНИЕ(ВыдачаНаМОЛТовары.Номенклатура.ЕдиницаИзмерения)
		|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ВыдачаНаМОЛТовары.Упаковка)
		|	КОНЕЦ КАК ЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА ВыдачаНаМОЛТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
		|			ТОГДА ВыдачаНаМОЛТовары.Номенклатура.ЕдиницаИзмерения.Код
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
		|	ВыдачаНаМОЛТовары.Количество КАК Количество,
		|	ВыдачаНаМОЛТовары.Цена,
		|	ВыдачаНаМОЛТовары.Сумма,
		|	ВыдачаНаМОЛТовары.Номенклатура.Артикул КАК Артикул
		|ИЗ
		|	Документ.ВыдачаНаМОЛ.Товары КАК ВыдачаНаМОЛТовары
		|ГДЕ
		|	ВыдачаНаМОЛТовары.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Пакет = Запрос.ВыполнитьПакет();
	Выборка = Пакет[0].Выбрать();
	Выборка.Следующий();
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.Заполнить(Выборка);
	
	ТабДок.Вывести(ОбластьШапка);
	
	Выборка = Пакет[1].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
		ОбластьСтрока.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(ОбластьСтрока);
	КонецЦикла;
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ТабДок.Вывести(ОбластьПодвал);
КонецПроцедуры	

Процедура Печать_АктПередачиНаИспытания(ТабДок, Ссылка)Экспорт
	//Т = Новый ТабличныйДокумент;
	ТабДок.НижнийКолонтитул.Выводить = Истина;
	ТабДок.НижнийКолонтитул.ТекстСправа = "[&НомерСтраницы]";
	Макет = ПолучитьМакет("АктПередачиНаИспытания");
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(ВыдачаНаМОЛ.Организация) КАК Организация,
		|	ПРЕДСТАВЛЕНИЕ(ВыдачаНаМОЛ.Контрагент) КАК Контрагент,
		|	ВыдачаНаМОЛ.Дата КАК Дата
		|ИЗ
		|	Документ.ВыдачаНаМОЛ КАК ВыдачаНаМОЛ
		|ГДЕ
		|	ВыдачаНаМОЛ.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(ВыдачаНаМОЛТовары.Номенклатура) КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА ВыдачаНаМОЛТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
		|			ТОГДА ПРЕДСТАВЛЕНИЕ(ВыдачаНаМОЛТовары.Номенклатура.ЕдиницаИзмерения)
		|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ВыдачаНаМОЛТовары.Упаковка)
		|	КОНЕЦ КАК ЕдиницаИзмерения,
		|	ВыдачаНаМОЛТовары.Количество КАК Количество,
		|	ВыдачаНаМОЛТовары.Цена,
		|	ВыдачаНаМОЛТовары.Сумма КАК СуммаСНДС,
		|	ВыдачаНаМОЛТовары.Номенклатура.Артикул КАК Артикул,
		|	ВыдачаНаМОЛТовары.Номенклатура КАК НоменклатураСсылка,
		|	ПРЕДСТАВЛЕНИЕ(ВыдачаНаМОЛТовары.Номенклатура.Производитель) КАК Производитель
		|ИЗ
		|	Документ.ВыдачаНаМОЛ.Товары КАК ВыдачаНаМОЛТовары
		|ГДЕ
		|	ВыдачаНаМОЛТовары.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Пакет = Запрос.ВыполнитьПакет();
	ВыборкаШапка = Пакет[0].Выбрать();
	ВыборкаШапка.Следующий();
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.Заполнить(ВыборкаШапка);
	ОбластьШапка.Параметры.ТекДата = Формат(ВыборкаШапка.Дата,"ДФ=dd.MM.yyyy");
	ТабДок.Вывести(ОбластьШапка);
	
	ОбластьШапкаТЧ = Макет.ПолучитьОбласть("ШапкаТЧ");
	//ОбластьШапкаТЧ.Параметры.Заполнить(Выборка);
	
	ТабДок.Вывести(ОбластьШапкаТЧ);

	
	
	Выборка = Пакет[1].Выбрать();
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьПодвал.Параметры.Заполнить(ВыборкаШапка);
	ОбластьСтрокаТЧ = Макет.ПолучитьОбласть("СтрокаТЧ");
	МассивОбластей = Новый Массив;
	Сч = 0;
	Пока Выборка.Следующий() Цикл
		Сч = Сч + 1;
		ОбластьСтрокаТЧ = Макет.ПолучитьОбласть("СтрокаТЧ");
		ОбластьСтрокаТЧ.Параметры.Заполнить(Выборка);
		СтавкаНДС = РаботаСНоменклатурой.ПолучитьСтавкуНДСНоменклатуры(Выборка.НоменклатураСсылка);
		ЧислоСтавки = КэшируемыеФункции.ПолучитьЧислоСтавкиНДС(СтавкаНДС);
		СуммаНДС = Выборка.СуммаСНДС * (ЧислоСтавки/(100 + ЧислоСтавки));
		СуммаБезНДС = Выборка.СуммаСНДС - СуммаНДС;
		ЦенаБезНДС = СуммаБезНДС/Выборка.Количество;
		ОбластьСтрокаТЧ.Параметры.ЦенаБезНДС = ЦенаБезНДС;
		ОбластьСтрокаТЧ.Параметры.СуммаБезНДС = СуммаБезНДС;
		ОбластьСтрокаТЧ.Параметры.СуммаНДС = СуммаНДС;
		ОбластьСтрокаТЧ.Параметры.НомерПП = Сч;
		МассивОбластей.Очистить();
		МассивОбластей.Добавить(ОбластьСтрокаТЧ);
		//МассивОбластей.Добавить(ОбластьСтрокаТЧ);
		//МассивОбластей.Добавить(ОбластьПодвал);
		Если Выборка.Количество() = Сч Тогда
			МассивОбластей.Добавить(ОбластьПодвал);
		КонецЕсли;	
		
		Если Не ТабДок.ПроверитьВывод(МассивОбластей) Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			ТабДок.Вывести(ОбластьШапкаТЧ);
		КонецЕсли;

		ТабДок.Вывести(ОбластьСтрокаТЧ);
	КонецЦикла;
	
	
	ТабДок.Вывести(ОбластьПодвал);


КонецПроцедуры


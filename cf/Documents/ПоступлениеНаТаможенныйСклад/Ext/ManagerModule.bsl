
Процедура ИницилизироватьДополнительныеДанныеДокумента(Ссылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос(
	 
	КэшируемыеФункции.ТектЗапросаПолученияПараметровСистемы() + "
	
	|;
	|ВЫБРАТЬ
	|	СкладОтправитель, СкладПолучатель
	|ИЗ
	|	Документ.ПоступлениеНаТаможенныйСклад
	|ГДЕ
	|	Ссылка = &Ссылка
	|;
	
	// ТОВАРЫ НА СКЛАДАХ
	
	|ВЫБРАТЬ
	|	&Период					Период,
	|	&ВидДвиженияРасход		ВидДвижения,
	|	Ссылка.СкладОтправитель	Склад,
	|	Номенклатура,
	|	СУММА(
	|		ВЫБОР КОГДА Упаковка = &ПустаяУпаковка 
	|		ТОГДА Количество
	|		ИНАЧЕ Количество*Упаковка.Коэффициент КОНЕЦ) Количество
    |ИЗ
	|	Документ.ПоступлениеНаТаможенныйСклад.Товары
	|
	|ГДЕ
	|	Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Ссылка,
	|	Номенклатура,
	|	Упаковка
	|;
	
	// ЯЧЕЙКИ
	
	|ВЫБРАТЬ
	|	&Период				Период,
	|	&ВидДвиженияРасход	ВидДвижения,
	|	ЯчейкаОтправитель 	Ячейка,
	|	Номенклатура,
	|	СУММА(ВЫБОР КОГДА Упаковка = &ПустаяУпаковка 
	|		ТОГДА Количество
	|		ИНАЧЕ Количество*Упаковка.Коэффициент КОНЕЦ) Количество
    |ИЗ
	|	Документ.ПоступлениеНаТаможенныйСклад.Товары
	|
	|ГДЕ
	|	Ссылка = &Ссылка И ЯчейкаОтправитель.Владелец = Ссылка.СкладОтправитель
	|
	|СГРУППИРОВАТЬ ПО
	|	ЯчейкаОтправитель,
	|	Номенклатура,
	|	Упаковка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период				Период,
	|	&ВидДвиженияПриход	ВидДвижения,
	|	ЯчейкаПолучатель 	Ячейка,
	|	Номенклатура,
	|	СУММА(ВЫБОР КОГДА Упаковка = &ПустаяУпаковка 
	|		ТОГДА Количество
	|		ИНАЧЕ Количество*Упаковка.Коэффициент КОНЕЦ) Количество
    |ИЗ
	|	Документ.ПоступлениеНаТаможенныйСклад.Товары
	|
	|ГДЕ
	|	Ссылка = &Ссылка И ЯчейкаПолучатель.Владелец = Ссылка.СкладПолучатель
	|
	|СГРУППИРОВАТЬ ПО
	|	ЯчейкаПолучатель,
	|	Номенклатура,
	|	Упаковка
	|;
	
	// ДВИЖЕНИЕ ТОВАРА
	
	|ВЫБРАТЬ
	|	&Период							Период,
	|	&ВидДвиженияПриход				ВидДвижения,
	|	&СкладПолучатель 				Склад,
	|	Номенклатура,
	|	Упаковка,
	|	СУММА(Количество) Количество
	|ИЗ
	|	Документ.ПоступлениеНаТаможенныйСклад.Товары
	|ГДЕ
	|	Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Номенклатура,
	|	Упаковка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период							Период,
	|	&ВидДвиженияПриход				ВидДвижения,
	|	&СкладОтправитель 				Склад,
	|	Номенклатура,
	|	Упаковка,
	|	СУММА(Количество) Количество
	|ИЗ
	|	Документ.ПоступлениеНаТаможенныйСклад.Товары
	|ГДЕ
	|	Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Номенклатура,
	|	Упаковка;
	
	// ТОВАРЫ НА ТАМОЖНЕ
	
	|ВЫБРАТЬ
	|	&Период					Период,
	|	&ВидДвиженияПриход		ВидДвижения,
	|	Ссылка.СкладПолучатель	Склад,
	|	Ссылка.Валюта			Валюта,
	|	Ссылка					Партия,
	|	Номенклатура,
	|	Упаковка,
	|	Цена,
	|	СУММА(Сумма) 		Сумма,
	|	СУММА(Количество) 	Количество,
	|	СУММА(
	|		ВЫБОР КОГДА Упаковка = &ПустаяУпаковка ТОГДА Количество
	|		ИНАЧЕ		Количество * Упаковка.Коэффициент КОНЕЦ) КоличествоУпр
	|ИЗ
	|	Документ.ПоступлениеНаТаможенныйСклад.Товары
	|
	|ГДЕ
	|	Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Ссылка,
	|	Номенклатура,
	|	Упаковка,
	|	Цена;
	
	// ПАРТИИ ТОВАРОВ
	
	|ВЫБРАТЬ
	|	&Период				КАК Период,
	|	&ВидДвиженияРасход	КАК ВидДвижения,
	|	Ссылка.СкладОтправитель Склад, Партия, Номенклатура,
	|	СУММА(СуммаПартии) Сумма,
	|	ВЫБОР КОГДА Упаковка = &ПустаяУпаковка ТОГДА
	|		СУММА(Количество)
	|	ИНАЧЕ
	|		СУММА(Количество*Упаковка.Коэффициент)
	|	КОНЕЦ				КАК Количество
	|ИЗ
	|	Документ.ПоступлениеНаТаможенныйСклад.Товары
	|ГДЕ
	|	Ссылка = &Ссылка И СуммаПартии <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	Ссылка, Партия, Номенклатура, Упаковка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период				КАК Период,
	|	&ВидДвиженияПриход	КАК ВидДвижения,
	|	Ссылка.СкладПолучатель Склад, Партия, Номенклатура,
	|	СУММА(СуммаПартии) Сумма,
	|	ВЫБОР КОГДА Упаковка = &ПустаяУпаковка ТОГДА
	|		СУММА(Количество)
	|	ИНАЧЕ
	|		СУММА(Количество*Упаковка.Коэффициент)
	|	КОНЕЦ				КАК Количество
	|ИЗ
	|	Документ.ПоступлениеНаТаможенныйСклад.Товары
	|ГДЕ
	|	Ссылка = &Ссылка И СуммаПартии <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	Ссылка, Партия, Номенклатура, Упаковка
	|;");
	
	Запрос.УстановитьПараметр("Ссылка", 			Ссылка);
	Запрос.УстановитьПараметр("Период", 			Ссылка.Дата);
	Запрос.УстановитьПараметр("СкладОтправитель", 	Ссылка.СкладОтправитель);
	Запрос.УстановитьПараметр("СкладПолучатель", 	Ссылка.СкладПолучатель);
	Запрос.УстановитьПараметр("ВидДвиженияПриход", 	ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("ВидДвиженияРасход", 	ВидДвиженияНакопления.Расход);
	Запрос.УстановитьПараметр("ПустаяУпаковка", 	Справочники.УпаковкиНоменклатуры.ПустаяСсылка());
	
	Пакет = Запрос.ВыполнитьПакет();
	
	ДополнительныеСвойства.Вставить("ПараметрыСистемы", КонвертацияТипов.ПолучитьСтруктуруИзСТрокиТаблицыЗначений(Пакет[0].Выгрузить()));
	ДополнительныеСвойства.Вставить("Шапка", 			КонвертацияТипов.ПолучитьСтруктуруИзСТрокиТаблицыЗначений(Пакет[1].Выгрузить()));
	ДополнительныеСвойства.Вставить("ТоварыНаСкладах", 	Пакет[2].Выгрузить());
	ДополнительныеСвойства.Вставить("ТоварыВЯчейках", 	Пакет[3].Выгрузить());
	ДополнительныеСвойства.Вставить("ДвижениеТовара", 	Пакет[4].Выгрузить());
	ДополнительныеСвойства.Вставить("ТоварыНаТаможне", 	Пакет[5].Выгрузить());
	ДополнительныеСвойства.Вставить("ПартииТоваров", 	Пакет[6].Выгрузить());
	
КонецПроцедуры


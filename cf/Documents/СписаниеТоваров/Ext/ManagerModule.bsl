
Процедура ИницилизироватьДополнительныеДанныеДокумента(Ссылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос(
	
	// ПАРАМЕТРЫ СИСТЕМЫ
	
	КэшируемыеФункции.ТектЗапросаПолученияПараметровСистемы() + "
	|;
	
	// ШАПКА

	|ВЫБРАТЬ
	|	Склад
	|ИЗ
	|	Документ.СписаниеТоваров
	|ГДЕ
	|	Ссылка = &Ссылка
	|;
	
	// ТОВАРЫ НА СКЛАДАХ
	
	|ВЫБРАТЬ
	|	&Период						КАК Период,
	|	&ВидДвиженияРасход			КАК ВидДвижения,
	|	Ссылка.Склад				КАК Склад,
	|	Номенклатура	            КАК Номенклатура,
	|	ВЫБОР КОГДА Упаковка = &ПустаяУпаковка ТОГДА
	|		СУММА(Количество)
	|	ИНАЧЕ
	|		СУММА(Количество*Упаковка.Коэффициент)
	|	КОНЕЦ						КАК Количество
    |ИЗ
	|	Документ.СписаниеТоваров.Товары
	|
	|ГДЕ
	|	Ссылка = &Ссылка И НЕ Ссылка.Склад.Таможня
	|
	|СГРУППИРОВАТЬ ПО
	|	Ссылка.Склад,
	|	Номенклатура,
	|	Упаковка
	|;
	
	// ЯЧЕЙКИ
	
	|ВЫБРАТЬ
	|	&Период						КАК Период,
	|	&ВидДвиженияРасход			КАК ВидДвижения,
	|	Ячейка						КАК Ячейка,
	|	Номенклатура	            КАК Номенклатура,
	|	ВЫБОР КОГДА Упаковка = &ПустаяУпаковка ТОГДА
	|		СУММА(Количество)
	|	ИНАЧЕ
	|		СУММА(Количество*Упаковка.Коэффициент)
	|	КОНЕЦ						КАК Количество
    |ИЗ
	|	Документ.СписаниеТоваров.Товары
	|
	|ГДЕ
	|	Ссылка = &Ссылка И Ячейка.Владелец = Ссылка.Склад
	//|	Ячейка <> &ПустаяСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Ячейка,
	|	Номенклатура,
	|	Упаковка;
	
	// ПАРТИИ ТОВАРОВ
	//|ВЫБРАТЬ неопределено ГДЕ Истина=Ложь
	|ВЫБРАТЬ
	|	&Период				КАК Период,
	|	&ВидДвиженияРасход	КАК ВидДвижения,
	|	Ссылка.Склад, Партия, Номенклатура,
	|	СУММА(СуммаПартии) Сумма,
	|	ВЫБОР КОГДА Упаковка = &ПустаяУпаковка ТОГДА
	|		СУММА(Количество)
	|	ИНАЧЕ
	|		СУММА(Количество*Упаковка.Коэффициент)
	|	КОНЕЦ				КАК Количество
	|ИЗ
	|	Документ.СписаниеТоваров.Товары
	|ГДЕ
	|	Ссылка = &Ссылка И СуммаПартии <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	Ссылка, Партия, Номенклатура, Упаковка
	|;
	
	// ИНВЕНТАРИЗАЦИЯ
	
	|ВЫБРАТЬ
	|	&Период				КАК Период,
	//|	&ВидДвиженияРасход	КАК ВидДвижения,
	|	Ссылка.Склад Склад, Номенклатура,
	|	Ссылка.Инвентаризация ДокументОснование,
	|	СУММА(ВЫБОР КОГДА Упаковка = &ПустаяУпаковка 
	|			ТОГДА -Количество
	|			ИНАЧЕ -Количество * Упаковка.Коэффициент
	|		КОНЕЦ) КАК Количество
	|ИЗ
	|	Документ.СписаниеТоваров.Товары
	|ГДЕ
	|	Ссылка = &Ссылка 
	|
	|СГРУППИРОВАТЬ ПО
	|	Ссылка.Склад, Ссылка, Номенклатура
	|");

	
	//Запрос.УстановитьПараметр("Область", 			ПараметрыСеанса.ТекущаяОбласть);
	Запрос.УстановитьПараметр("Ссылка", 			Ссылка);
	Запрос.УстановитьПараметр("Период", 			Ссылка.Дата);
	Запрос.УстановитьПараметр("ВидДвиженияРасход", 	ВидДвиженияНакопления.Расход);
	Запрос.УстановитьПараметр("ПустаяСсылка", 		Справочники.Ячейки.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяУпаковка", 	Справочники.УпаковкиНоменклатуры.ПустаяСсылка());
	
	Пакет = Запрос.ВыполнитьПакет();
	
	ДополнительныеСвойства.Вставить("ПараметрыСистемы", КонвертацияТипов.ПолучитьСтруктуруИзСТрокиТаблицыЗначений(Пакет[0].Выгрузить()));
	ДополнительныеСвойства.Вставить("Шапка", 			КонвертацияТипов.ПолучитьСтруктуруИзСТрокиТаблицыЗначений(Пакет[1].Выгрузить()));
	ДополнительныеСвойства.Вставить("ТоварыНаСкладах", 	Пакет[2].Выгрузить());
	ДополнительныеСвойства.Вставить("ТоварыВЯчейках", 	Пакет[3].Выгрузить());
	Если ПолучитьФункциональнуюОпцию("НемецкийУчет") Тогда
		ДополнительныеСвойства.Вставить("ПартииТоваров", 	Пакет[4].Выгрузить());
	КонецЕсли;
	ДополнительныеСвойства.Вставить("Инвентаризация", 	Пакет[5].Выгрузить());
	
КонецПроцедуры

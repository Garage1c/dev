&НаКлиенте
Процедура ОбщиеРеквизиты(Команда)
	
	ФункцииФормДокументов.ОткрытьОбщиеРеквизиты(ЭтаФорма);
	
КонецПроцедуры

// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
	
		Объект.Процесс 			= Параметры.Процесс;
		Объект.КассаОтправитель = Параметры.Касса;
		Объект.Сумма			= Параметры.Наличность;
	//	Объект.СтатьяДДС		= Константы.СтатьяУчетаДенежныхСредствПоЧекамККМ.Получить();
	КонецЕсли;
	
	УстановитьВидимостьККТ();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение ИЛИ  ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Оповестить(СобытияСистемы.Событие_ИзмениласьНаличность(), , ЭтаФорма);
	КонецЕсли;

	ИнкассациюНаФискальномУстройстве(Ложь);
КонецПроцедуры


#Область СтатьяДДС
&НаСервере
Процедура СтатьяДДСПриИзмененииНаСервере(Ст1, Ст2)
	
	ФункцииФормДокументов.ПриИзмененииСтатьиДДС(Ст1, Ст2);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяДДСПриИзменении(Элемент)
	
	СтатьяДДСПриИзмененииНаСервере(Объект.СтатьяДДС, Объект.СтатьяДДСБух);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяДДСБухПриИзменении(Элемент)
	
//	ФункцииФормДокументов.ПриИзмененииСтатьиДДС(Объект.СтатьяДДСБух, Объект.СтатьяДДС);
	
КонецПроцедуры

#КонецОбласти

#Область ККТ
&НаКлиенте
Процедура ККТПровести(Команда)
	ИнкассациюНаФискальномУстройстве(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ИнкассациюНаФискальномУстройстве(ПровестиИнкассацию)
	Если (ПровестиИнкассацию Или ОбщегоНазначения.НужнаИнкассация(Объект.КассаОтправитель)) И ОбщегоНазначения.НужноПробитиеПоКассе(Объект.КассаОтправитель) Тогда
		ТипИнкассации = 0; //  Тип операции (0 - Выемка / 1 - Внесение)
	ИначеЕсли (ПровестиИнкассацию Или ОбщегоНазначения.НужнаИнкассация(Объект.КассаПолучатель)) И ОбщегоНазначения.НужноПробитиеПоКассе(Объект.КассаПолучатель) Тогда
		ТипИнкассации = 1; //  Тип операции (0 - Выемка / 1 - Внесение)
	Иначе
		Возврат;
	КонецЕсли;
	
	Если Объект.ПроведеноПоККТ Тогда
		ПоказатьПредупреждение(,"Документ уже был проведен по кассе");
		Возврат;
	КонецЕсли;
	Если НЕ Объект.Проведен Тогда
		ПоказатьПредупреждение(,"Для проведения документа по кассе он должен быть проведён в системе");
		Возврат;
	КонецЕсли;
	
	ЭтаФорма.Доступность = Ложь; //При необходимости можно заблокировать интерфейс пользователя.

	СуммаОперации = Объект.Сумма; // Сумма внесения/выемки
	ПараметрыОперации = Новый Структура("ТипИнкассации, Сумма", ТипИнкассации, СуммаОперации);
	ОповещениеПриЗавершении = Новый ОписаниеОповещения("ИнкассациюНаФискальномУстройствеЗавершение", ЭтотОбъект);
	МенеджерОборудованияКлиент.НачатьИнкассациюНаФискальномУстройстве(ОповещениеПриЗавершении, УникальныйИдентификатор, ПараметрыОперации);

КонецПроцедуры

&НаКлиенте
Процедура ИнкассациюНаФискальномУстройствеЗавершение(РезультатВыполнения, Параметры) Экспорт

	ЭтаФорма.Доступность = Истина; //При необходимости разблокируем интерфейс пользователя.

	Если НЕ РезультатВыполнения.Результат Тогда
		ПоказатьПредупреждение(,"Внимание! Не удалось провести документ по ККТ! Для проведения - откройте документ и нажмите кнопку ""ККТ Провести"""+Символы.ПС+Символы.ПС+"Техническая информация: "+РезультатВыполнения.ОписаниеОшибки);
		//ТекстСообщения = НСтр("ru = 'При выполнении операции произошла ошибка:""%ОписаниеОшибки%"".'");
		//ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", РезультатВыполнения.ОписаниеОшибки);
		//Сообщить(ТекстСообщения);
	Иначе
		ОбщегоНазначения.УстановитьРеквизитПроведеноПоККТ(Объект.Ссылка);
		СобытияСистемы.ОповеститьОПробитииЧека(Объект.Ссылка);
		УстановитьВидимостьККТ();
		Если ЭтаФорма.Открыта() Тогда
			Закрыть();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьККТ()
	Если Не Объект.Ссылка.Пустая() И (Объект.КассаОтправитель.ПродажаПоККТ Или Объект.КассаПолучатель.ПродажаПоККТ) И НЕ Объект.ПроведеноПоККТ Тогда
		Элементы.ФормаККТПровести.Видимость = Истина;
	Иначе
		Элементы.ФормаККТПровести.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти



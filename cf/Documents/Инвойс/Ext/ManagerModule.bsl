Процедура ИницилизироватьДополнительныеДанныеДокумента(Ссылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос("
	
	// ЗАКАЗЫ ПОСТАВШИКАМ
	
	|ВЫБРАТЬ
	|	&Период				КАК Период,
	|	&ВидДвиженияРасход	КАК ВидДвижения,
	|   ЗаказПоставщику		КАК ЗаказПоставщику,
	|	Номенклатура,
	|	Упаковка,
	|	Цена,
	|	СтавкаНДС,
	|	МАКСИМУМ(Количество) КАК Количество,
    |	МАКСИМУМ(Сумма)		 КАК Сумма
	|ИЗ
	|	Документ.Инвойс.Товары
	|
	|ГДЕ
	|	Ссылка = &Ссылка И
	|	ЗаказПоставщику <> &ПустойЗаказ
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказПоставщику,
	|	Номенклатура,
	|	Упаковка,
	|	Цена,
	|	СтавкаНДС
	|;
	
	// ТОВАРЫ ПОСТАВЩИКОВ В ПУТИ
	
	|ВЫБРАТЬ
	|	&Период				КАК Период,
	|	&ВидДвиженияПриход	КАК ВидДвижения,
	|   ЗаказПоставщику		КАК ЗаказПоставщику,
	|	Ссылка					Инвойс,
	|	Номенклатура,
	|	Упаковка,
	|	Цена,
	|	СтавкаНДС,
	|	МАКСИМУМ(Количество)	КАК Количество,
    |	МАКСИМУМ(Сумма)			КАК Сумма
	|ИЗ
	|	Документ.Инвойс.Товары
	|
	|ГДЕ
	|	Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Ссылка,
	|	ЗаказПоставщику,
	|	Номенклатура,
	|	Упаковка,
	|	Цена,
	|	СтавкаНДС
	|;
	
	// ОПЛАТА ПО ИМПОРТУ
		
	|ВЫБРАТЬ
	|	&Период				Период,
	|	&ВидДвиженияПриход 	ВидДвижения,
	|	Ссылка	Инвойс,
	|   Сумма
	|ИЗ
	|	Документ.Инвойс
	|ГДЕ
	|	Ссылка = &Ссылка И КонтролироватьОплату
	|;
	
	// ДОЛГИ ПО ИНВОЙСАМ
	
	|ВЫБРАТЬ
	|	&Период				Период,
	|	&ВидДвиженияПриход 	ВидДвижения,
	|	Ссылка				Инвойс,
	|	Ссылка.Контрагент	Контрагент,
	|	ЗаказПоставщику     ЗаказПоставщику,
	|   СУММА(Сумма)        Сумма
	|ИЗ
	|	Документ.Инвойс.Товары
	|ГДЕ
	|	Ссылка = &Ссылка И &КонтролироватьОплату
	|СГРУППИРОВАТЬ ПО Ссылка, ЗаказПоставщику
	|");
	
	Запрос.УстановитьПараметр("Ссылка", 			Ссылка);
	Запрос.УстановитьПараметр("ПустойЗаказ", 		Документы.ЗаказПоставщику.ПустаяСсылка());
	Запрос.УстановитьПараметр("Период", 			Ссылка.Дата);
	Запрос.УстановитьПараметр("ВидДвиженияПриход", 	ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("ВидДвиженияРасход", 	ВидДвиженияНакопления.Расход);
	Запрос.УстановитьПараметр("КонтролироватьОплату", 	Ссылка.КонтролироватьОплату);

	Пакет = Запрос.ВыполнитьПакет();
	
	ДополнительныеСвойства.Вставить("ЗаказыПоставщикам", 		Пакет[0].Выгрузить());
	ДополнительныеСвойства.Вставить("ТоварыПоставщиковВПути", 	Пакет[1].Выгрузить());
	ДополнительныеСвойства.Вставить("ОплатыИмпорт", 			Пакет[2].Выгрузить());
	ДополнительныеСвойства.Вставить("ДолгиПоИнвойсам", 			Пакет[3].Выгрузить());
	
КонецПроцедуры

Функция ПолучитьНомерДляПечати(ДокНомер)
	
	Номер = СокрЛП(ДокНомер);
	
	// удаление ведущих нулей
	Пока Лев(Номер, 1)="0" Цикл
		Номер = Сред(Номер, 2);
	КонецЦикла;
	
	Возврат Номер;
	
КонецФункции

Процедура Печать_Пакинг(ТабДок, Ссылка) Экспорт
	
	Макет = Документы.Инвойс.ПолучитьМакет("Пакинг");
	
	// Определим облости
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка 			= Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаТаблица		= Макет.ПолучитьОбласть("ШапкаТаблица");
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги			= Макет.ПолучитьОбласть("Итоги");
	
	ОбластьЗаголовок.Параметры.Заполнить(Новый Структура("Дата, Номер", Формат(Ссылка.Дата, "ДЛФ=DD"), ПолучитьНомерДляПечати(Ссылка.Номер)));
	
	ТабДок.Вывести(ОбластьЗаголовок);                                                    
	
	ОбластьШапка.Параметры.Заполнить(Новый Структура("Контрагент, Склад", Ссылка.Контрагент.НаименованиеПолное, Ссылка.Склад));
	ОбластьШапка.Параметры.Покупатель = "Общество с ограниченной ответственностью ""Гараж-автоинструмент"", ИНН 7816379474, КПП 781601001, 192236, г.Санкт-Петербург, ул.Софийская, д.14, литер А, тел.:(812)268-14-82, 449-27-09";
	
	ТабДок.Вывести(ОбластьШапка);

	
	// Запрос
	
	Запрос 	= Новый Запрос("ВЫБРАТЬ
	       	               |	Док.Номенклатура КАК Номенклатура,
	       	               |	Док.Номенклатура.Артикул КАК Артикул,
	       	               |	Док.Количество КАК КоличествоТовара,
						   |    Док.Упаковка,
						   |	Док.НоменклатураДляТаможни,
						   |	Док.НоменклатураДляТаможни.Наименование КАК Наименование,
						   |	Док.Ссылка.РасчетВеса,
						   |	Док.КоличествоМест,
						   |	Док.КоличествоВУпаковке Количество,
						   |	Док.ВесНетто 	Нетто,
						   |	Док.ВесБрутто 	Брутто,
						   |	ВЫБОР КОГДА Док.Ссылка.РасчетВеса = ЗНАЧЕНИЕ(Перечисление.ВидыРасчетаВеса.ПоУпаковкам)
						   |		ТОГДА Док.ВесНетто * Док.КоличествоМест
						   |        ИНАЧЕ Док.ВесНетто * Док.Количество	
						   |	КОНЕЦ ИтогоНетто,
						   |	ВЫБОР КОГДА Док.Ссылка.РасчетВеса = ЗНАЧЕНИЕ(Перечисление.ВидыРасчетаВеса.ПоУпаковкам)
						   |		ТОГДА Док.ВесБрутто * Док.КоличествоМест
						   |        ИНАЧЕ Док.ВесБрутто * Док.Количество	
						   |	КОНЕЦ ИтогоБрутто
	       	               |ИЗ
	       	               |	Документ.Инвойс.ТоварыПакинг КАК Док
						   |ГДЕ
	       	               |	Док.Ссылка = &Ссылка
						   |ИТОГИ 
						   |	СУММА(КоличествоМест), 
						   |	СУММА(КоличествоТовара),
						   |	СУММА(ИтогоНетто),
						   |	СУММА(ИтогоБрутто)
						   |ПО ОБЩИЕ	
						   |");
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить(); 
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		// ШАПКА
		
		Ключ = "ИмяНетто, ИмяБрутто";
		Если Ссылка.РасчетВеса = Перечисления.ВидыРасчетаВеса.ПоУпаковкам Тогда
			СтруктураПараметры = Новый Структура(Ключ, "Вес упаковки Нетто", "Вес упаковки Брутто");
		Иначе
			СтруктураПараметры = Новый Структура(Ключ, "Вес товара Нетто", "Вес товара Брутто");
		КонецЕсли;
		
		ОбластьШапкаТаблица.Параметры.Заполнить(СтруктураПараметры);
		ТабДок.Вывести(ОбластьШапкаТаблица);
		
		Выборка = РезультатЗапроса.Выбрать();
		
		// ИТОГИ
		
		Выборка.Следующий();
		Ключ = "ИтогКоличествоТовара, ИтогКоличествоМест, ИтогИтогоНетто, ИтогИтогоБрутто";
		ОбластьИтоги.Параметры.Заполнить(Новый Структура(Ключ, Выборка.КоличествоТовара, Выборка.КоличествоМест, Выборка.ИтогоНетто, Выборка.ИтогоБрутто));
		//Если Ссылка.РасчетВеса = Перечисления.ВидыРасчетаВеса.ПоТоварам Тогда 
		//	ОбластьИтоги.Параметры.ИтогКоличество = Выборка.Количество;
		//КонецЕсли;
		
		// СТРОКИ
		
		Пока Выборка.Следующий() Цикл
			ОбластьСтрока.Параметры.Заполнить(Выборка);
			
			ОбластьСтрока.Параметры.НаименованиеТовара = Выборка.Номенклатура.НаименованиеПолное;
			ОбластьСтрока.Параметры.КодТНВЭД = Выборка.НоменклатураДляТаможни.КодТНВЭД;
			
			ТабДок.Вывести(ОбластьСтрока);
		КонецЦикла;
		
		ТабДок.Вывести(ОбластьИтоги);
		
    КонецЕсли;
 КонецПроцедуры

Процедура Печать_Инвойс(ТабДок, Ссылка) Экспорт
	
	Макет = Документы.Инвойс.ПолучитьМакет("Инвойс");
	
	// Определим облости
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");	
	ОбластьШапка 			= Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаТаблица		= Макет.ПолучитьОбласть("ШапкаТаблица");
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги			= Макет.ПолучитьОбласть("Итоги");
	
	ОбластьЗаголовок.Параметры.Заполнить(Новый Структура("Дата, Номер", Формат(Ссылка.Дата, "ДЛФ=DD"), ПолучитьНомерДляПечати(Ссылка.Номер)));
	
	ТабДок.Вывести(ОбластьЗаголовок);                                                    
	
	ОбластьШапка.Параметры.Заполнить(Новый Структура("Контрагент, Склад", Ссылка.Контрагент.НаименованиеПолное, Ссылка.Склад));
	ОбластьШапка.Параметры.Покупатель = "Общество с ограниченной ответственностью ""Гараж-автоинструмент"", ИНН 7816379474, КПП 781601001, 192236, г.Санкт-Петербург, ул.Софийская, д.14, литер А, тел.:(812)268-14-82, 449-27-09";
	
	ТабДок.Вывести(ОбластьШапка);
	
	ТабДок.Вывести(ОбластьШапкаТаблица);

	// Запрос
	
	Запрос 	= Новый Запрос("ВЫБРАТЬ
	       	               	|	Док.Номенклатура КАК Номенклатура,
	       	               	|	Док.Номенклатура.Артикул КАК Артикул,
							|	Док.Номенклатура.Наименование Наименование,
	       	               	|	Док.Количество КАК Количество,
						   	|   ВЫБОР КОГДА Док.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
							|		ПРЕДСТАВЛЕНИЕ(Док.Упаковка)
							|	ИНАЧЕ
							|		Док.Номенклатура.ЕдиницаИзмерения
							|	КОНЕЦ ЕдиницаИзмерения,
						   	|	Док.Цена,
	       	               	|	Док.Сумма
	       	               	|ИЗ
	       	               	|	Документ.Инвойс.Товары КАК Док
	       	               	|ГДЕ
	       	               	|	Док.Ссылка = &Ссылка
						   	|ИТОГИ 
						   	|	СУММА(Количество), 
						   	|	СУММА(Сумма)
 							|ПО ОБЩИЕ	
						   	|");
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить(); 
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Выборка.Следующий();
		
		Ключ = "ИтогКоличество, ИтогСумма";
		ОбластьИтоги.Параметры.Заполнить(Новый Структура(Ключ, Выборка.Количество, Выборка.Сумма));
		НомерСтроки = 0;
		Пока Выборка.Следующий() Цикл НомерСтроки = НомерСтроки +1;
			ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
			ОбластьСтрока.Параметры.Заполнить(Выборка);
			ТабДок.Вывести(ОбластьСтрока);
		КонецЦикла;
		
		ТабДок.Вывести(ОбластьИтоги);
		
	КонецЕсли;

 КонецПроцедуры




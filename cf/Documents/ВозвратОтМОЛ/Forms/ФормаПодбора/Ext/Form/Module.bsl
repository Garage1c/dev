&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Валюта = Параметры.Валюта;
	МОЛ = Параметры.МОЛ;
	ТипЦен = Параметры.ТипЦен;
	СписокОстатки.Параметры.УстановитьЗначениеПараметра("МОЛ", МОЛ);
	Товары.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресТоваровВХранилище));
	УдалитьИзВременногоХранилища(Параметры.АдресТоваровВХранилище); 
КонецПроцедуры

&НаКлиенте	
Процедура ПеренестиВДокумент()
	Модифицированность 	= Ложь;
	ЗакрытьФорму 		= Истина;
	ОповеститьОВыборе(ПоместитьТоварыВХранилище());
КонецПроцедуры	

&НаСервере
Функция ПоместитьТоварыВХранилище() 
	
	Возврат ПоместитьВоВременноеХранилище(
					Товары.Выгрузить(), 
					УникальныйИдентификатор);
КонецФункции

&НаКлиенте
Процедура ПересчитатьЦенуПриИзменении(ТекСтрока)
	
	ТекСтрока.Цена = РаботаСНоменклатурой.ПолучитьЦену(	ТекСтрока.Номенклатура, ТипЦен, Валюта, ТекСтрока.Упаковка); 
   	ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;

КонецПроцедуры



//СОБЫТИЯ ФОРМЫ
&НаКлиенте
Процедура ТоварыСтрокаПриИзменении(Элемент)
	ТД = Элементы.Товары.ТекущиеДанные;
	ПересчитатьЦенуПриИзменении(ТД);
КонецПроцедуры
&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	ТД = Элементы.Товары.ТекущиеДанные;
	ТД.Сумма = ТД.Количество * ТД.Цена;
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТД = Элементы.СписокОстатки.ТекущиеДанные;
	Строки = Товары.НайтиСтроки(Новый Структура("Номенклатура",ТД.Номенклатура));
	Если Строки.Количество() Тогда
		Строки[0].Количество = Строки[0].Количество + 1;
		Строки[0].Сумма = Строки[0].Цена * Строки[0].Количество;
	Иначе
		НоваяСтрока = Товары.Добавить();
		НоваяСтрока.Количество = 1;
		ЗаполнитьЗначенияСвойств(НоваяСтрока,ТД);
		ПересчитатьЦенуПриИзменении(НоваяСтрока);
		
	КонецЕсли;
	
КонецПроцедуры

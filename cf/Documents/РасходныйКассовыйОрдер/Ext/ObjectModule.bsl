Функция ПолучитьКассуСписания() Экспорт Возврат Касса КонецФункции

// ПРЕДОПРЕДЕЛЕННЫЕ ФУНКЦИИ

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Подготовка
	
	Заголовок = КонтрольПроведения.ПолучитьСтандарнтыйЗаголовокПриПроведенииДокумента(Ссылка);
	
	Документы[Метаданные().Имя].ИницилизироватьДополнительныеДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Проведение
	
	ПроведенияДокументов.ПровестиПоВсемРегистрам(Метаданные().Движения, ДополнительныеСвойства, Движения, Отказ);
	
	// Запишем
	
	Движения.Записать();
	
	// Контроль
	
	КонтрольПроведения.ПроверитьДенежныеСредства(ЭтотОбъект, Касса, Отказ, Заголовок);
		
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	ПроверяемыеРеквизиты.Добавить("Отдел");
	
	Если 	РасшифровкаСуммы.Итог("Сумма") <> Сумма
			И ВидОперации = Перечисления.ВидыОперацийРКО.ВозвратПокупателю Тогда
			
		ОбщиеФункции.СообщитьТекст("Сумма документа не совпадает с суммой расшифровки по документам!");
		Отказ = Истина;
			
	КонецЕсли;
	
	Если ВидОперации <> Перечисления.ВидыОперацийРКО.ПрочийРасход  Тогда
		ПроверяемыеРеквизиты.Добавить("Контрагент");
	КонецЕсли;

КонецПроцедуры


Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВозвратОтПокупателя") Тогда
		
		// Заполнение шапки
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения,, "Ссылка, Проведен, Дата, Номер, ПометкаУдаления");
		
		СтатьяДДС = Справочники.СтатьиДвиженияДенежныхСредств.НайтиПоКоду("000000014"); // Возврат денежных средств покупателю
		СтатьяДДСБух = СтатьяДДС.СтатьяДДСБух;
		
		Основание = ДанныеЗаполнения;
		ВидОперации = Перечисления.ВидыОперацийРКО.ВозвратПокупателю;
		
		Касса = ОбщиеФункции.НастройкаПользователя("ПоУмолчанию_Касса");
		
		ДокументОтгрузки=ДанныеЗаполнения.ДокументОтгрузки;
		Если ЗначениеЗаполнено(ДокументОтгрузки) тогда
			Заказ=ДокументОтгрузки.Заказ;
		Иначе
			Заказ=Неопределено;
		КонецЕсли;	
		
		НовСтрока = РасшифровкаСуммы.Добавить();
		НовСтрока.Заказ = Заказ;
		//23.05.2017 Андриянов Включено по требованию Радюхина для магазинов
		НовСтрока.ДокументОтгрузки = ДокументОтгрузки;
		НовСтрока.Сумма = Сумма;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
		
		// Заполнение шапки
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения,, "Ссылка, Проведен, Дата, Номер, ПометкаУдаления, ПроведеноПоККТ");
		
		СтатьяДДС = Справочники.СтатьиДвиженияДенежныхСредств.НайтиПоКоду("000000014"); // Возврат денежных средств покупателю
		СтатьяДДСБух = СтатьяДДС.СтатьяДДСБух;
		
		Основание = ДанныеЗаполнения;
		ВидОперации = Перечисления.ВидыОперацийРКО.ВозвратПокупателю;
		
		ТЧ=ДанныеЗаполнения.РасшифровкаСуммы.Выгрузить();
		РасшифровкаСуммы.Загрузить(ТЧ);
		Для Каждого Стр Из РасшифровкаСуммы Цикл
			Стр.ДокументОплаты=ДанныеЗаполнения;
		КонецЦикла;	
		
		
	КонецЕсли;
КонецПроцедуры


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	 Если Не ПроверитьЗаполнение() Тогда  	
		Отказ = Истина;                         	
	КонецЕсли;
	
	Для Каждого Стр Из РасшифровкаСуммы Цикл
		
		Если Не ЗначениеЗаполнено(Стр.Заказ) и Не Стр.Заказ = Неопределено Тогда
			Стр.Заказ=Неопределено;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Стр.ДокументОтгрузки) и Не Стр.ДокументОтгрузки = Неопределено Тогда
			Стр.ДокументОтгрузки=Неопределено;
		КонецЕсли;
		
	КонецЦикла;		
	
КонецПроцедуры


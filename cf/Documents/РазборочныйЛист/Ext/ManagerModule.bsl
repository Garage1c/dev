
Процедура ИницилизироватьДополнительныеДанныеДокумента(Ссылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос(
	
	// ПАРАМЕТРЫ СИСТЕМЫ
	
	КэшируемыеФункции.ТектЗапросаПолученияПараметровСистемы() + "
	|;
	
	// ШАПКА
	
	|ВЫБРАТЬ
	|	Заказ, Склад
	|ИЗ
	|	Документ.РазборочныйЛист
	|ГДЕ
	|	Ссылка = &Ссылка
	|;
	
	// ТОВАРЫ В ЯЧЕЙКАХ
	
	|ВЫБРАТЬ
	|	&Период, &ВидДвиженияПриход,
	|	Номенклатура, &ЯчейкаНеНайдено, Сборщик,
	|	Количество
	|ИЗ
	|	Документ.РазборочныйЛист.Товары
	|
	|ГДЕ	Ссылка = &Ссылка 
	|		И Ячейка <> &ПустаяЯчейка 
	|;
	

	// ТОВАРЫ СОБРАННЫЕ
	
	|ВЫБРАТЬ
	|	&Период				КАК Период,
	|	&ВидДвиженияРасход	КАК ВидДвижения,
	|	Ссылка.СборочныйЛист КАК СборочныйЛист,
	|	Ссылка.Склад		КАК Склад,
	|	Ссылка.Заказ		КАК Заказ,
	|	Номенклатура	    КАК Номенклатура,
	|	Упаковка			КАК Упаковка,
	|	Сборщик,
	|	Количество			Количество
	|ИЗ
	|	Документ.РазборочныйЛист.Товары
	|
	|ГДЕ
	|	Ссылка = &Ссылка И Количество > 0 
	|		И Ссылка.ТипСборочногоЛиста <> Значение(Перечисление.ТипыСборочныхЛистов.ФиксацияЯчеек)
	|
	|;
	
	// РЕЗЕРВ ТОВАРОВ
	
	|ВЫБРАТЬ
	|	&Период				КАК Период,
	|	&ВидДвиженияРасход	КАК ВидДвижения,
	|	Ссылка.Склад		КАК Размещение,
	|	Ссылка.Заказ		КАК ДокументРезерва,
	|	Номенклатура	    КАК Номенклатура,
	|	СУММА(НеНайдено) Количество
	|ИЗ
	|	Документ.РазборочныйЛист.Товары
	|
	|ГДЕ
	|	Ссылка = &Ссылка И НеНайдено > 0
	|		И Ссылка.ТипСборочногоЛиста <> Значение(Перечисление.ТипыСборочныхЛистов.ФиксацияЯчеек)
	|
	|СГРУППИРОВАТЬ ПО
	|	Ссылка, Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период				КАК Период,
	|	&ВидДвиженияПриход	КАК ВидДвижения,
	|	Ссылка.Склад		КАК Размещение,
	|	&ЗаказПотеряшек		КАК ДокументРезерва,
	|	Номенклатура	    КАК Номенклатура,
	|	СУММА(НеНайдено) Количество
	|ИЗ
	|	Документ.РазборочныйЛист.Товары
	|
	|ГДЕ
	|	Ссылка = &Ссылка И НеНайдено > 0 
	|		И Ссылка.ТипСборочногоЛиста <> Значение(Перечисление.ТипыСборочныхЛистов.ФиксацияЯчеек)
	|
	|СГРУППИРОВАТЬ ПО
	|	Ссылка, Номенклатура
	|;
	
	// СБОРЩИК ТОВАРА
	
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Ссылка.ДатаСборки = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА &Период
	|		ИНАЧЕ Ссылка.ДатаСборки
	|	КОНЕЦ 				КАК Период,
	|	Сборщик				КАК Сборщик,
	|	Ссылка.Заказ		КАК Заказ,
	|	Ссылка				СборочныйЛист,
	|	Номенклатура		КАК Номенклатура,
	|	Упаковка			КАК Упаковка,
	|	Ссылка.Склад		КАК Склад,
	|   Ячейка,		
	|	СУММА(ВЫБОР КОГДА Количество + НеНайдено > 0 ТОГДА 1 ИНАЧЕ 0 КОНЕЦ)	КАК Количество
	|ИЗ
	|	Документ.РазборочныйЛист.Товары
	|
	|ГДЕ
	|	Ссылка = &Ссылка 
	|		И Ссылка.ТипСборочногоЛиста <> Значение(Перечисление.ТипыСборочныхЛистов.ФиксацияЯчеек)
	|
	|СГРУППИРОВАТЬ ПО
	|	Ссылка,
	|	Сборщик,
	|	Номенклатура,
	|	Упаковка,
	|	Ячейка
	|ИМЕЮЩИЕ СУММА(ВЫБОР КОГДА Количество + НеНайдено > 0 ТОГДА 1 ИНАЧЕ 0 КОНЕЦ) > 0
	|;
	
	|");
	
	Запрос.УстановитьПараметр("Ссылка", 			Ссылка);
	Запрос.УстановитьПараметр("Период", 			Ссылка.Дата);
	Запрос.УстановитьПараметр("ВидДвиженияПриход", 	ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("ВидДвиженияРасход", 	ВидДвиженияНакопления.Расход);
	Запрос.УстановитьПараметр("ПустаяЯчейка", 		Справочники.Ячейки.ПустаяСсылка());
	Запрос.УстановитьПараметр("ЯчейкаНеНайдено", 	Ссылка.Склад.ЯчейкаНеНайдено);
	Запрос.УстановитьПараметр("ПустаяУпаковка", 	Справочники.УпаковкиНоменклатуры.ПустаяСсылка());
	Запрос.УстановитьПараметр("ЗаказПотеряшек", 	Константы.ЗаказРезервПотеряшек.Получить());
	
	Пакет = Запрос.ВыполнитьПакет();
	
	ДополнительныеСвойства.Вставить("ПараметрыСистемы", 		КонвертацияТипов.ПолучитьСтруктуруИзСТрокиТаблицыЗначений(Пакет[0].Выгрузить()));
	ДополнительныеСвойства.Вставить("Шапка", 					КонвертацияТипов.ПолучитьСтруктуруИзСТрокиТаблицыЗначений(Пакет[1].Выгрузить()));
	ДополнительныеСвойства.Вставить("ТоварыВЯчейках", 				Пакет[2].Выгрузить());
	ДополнительныеСвойства.Вставить("ТоварыСобранные", 				Пакет[5].Выгрузить());
	ДополнительныеСвойства.Вставить("ТоварыВРезерве", 				Пакет[6].Выгрузить());
	ДополнительныеСвойства.Вставить("СборщикЗаказа", 				Пакет[3].Выгрузить());
	
КонецПроцедуры


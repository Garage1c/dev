
&НаКлиенте
Процедура Заполнить(Команда)
	
	ПроцентСкидки = 0;
	ПоказатьВводЧисла(Новый ОписаниеОповещения("ВыборПроцентаСкидки", ЭтаФорма), ПроцентСкидки, "Процент ручной скидки:", 5, 2);

КонецПроцедуры
&НаКлиенте
Процедура ВыборПроцентаСкидки(Результат, Параметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Для Каждого Строка Из Товары Цикл
			Строка.Процент = Результат;
			Строка.Цена = Строка.ЦенаСтарая*(1-Строка.Процент/100);
			Строка.Разница = Строка.ЦенаСтарая - Строка.Цена;
		КонецЦикла;
		
	КонецЕсли;	
	
	РазницаИтог = Товары.Итог("Разница");

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Товары.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресТоваровВХранилище));
	УдалитьИзВременногоХранилища(Параметры.АдресТоваровВХранилище);
	
	Для Каждого Строка Из Товары Цикл
		
		Строка.ЦенаСтарая = Строка.Цена;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	
	РазницаИтог = Товары.Итог("Разница");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПроцентПриИзменении(Элемент)
	
	текДанные = Элементы.Товары.ТекущиеДанные;
	текДанные.Цена = текДанные.ЦенаСтарая*(1-текДанные.Процент/100);
	текДанные.Разница = текДанные.ЦенаСтарая - текДанные.Цена;
		
КонецПроцедуры

&НаСервере
Функция ПоместитьТоварыВХранилище() 
	
	Возврат ПоместитьВоВременноеХранилище(
					Товары.Выгрузить(), 
					УникальныйИдентификатор);
КонецФункции


&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	Модифицированность 	= Ложь;
	Закрыть(ПоместитьТоварыВХранилище());

КонецПроцедуры

Процедура ИницилизироватьДополнительныеДанныеДокумента(Ссылка, ДополнительныеСвойства) Экспорт
	
	ИменаРегистров = Новый Массив;
	ПоВсем=Ложь;
	Если Не ДополнительныеСвойства.Свойство("ИменаРегистров",ИменаРегистров) Тогда
		ПоВсем=Истина;
		ИменаРегистров = Новый Массив;
	КонецЕсли;	
	
	ТекстЗапроса=КэшируемыеФункции.ТектЗапросаПолученияПараметровСистемы() +Символы.ПС+";"+Символы.ПС+"/////////////////////////////////////////"+Символы.ПС+

		"ВЫБРАТЬ 
		|	Заказ.Склад Склад, Заказ.ПользовательИнтернет ПользовательИнтернет
		|ИЗ
		|	Документ.КорректировкаИнтернетЗаказа
		|ГДЕ
		|	Ссылка = &Ссылка"
		;
		
	Нтаб=Новый Структура;
	ТекНомер=1;	
	
	Если ИменаРегистров.Найти("ИнтернетЗаказПокупателя")<>Неопределено ИЛИ ПоВсем Тогда
		ТекНомер=ТекНомер+1;
		Нтаб.Вставить("ИнтернетЗаказПокупателя",ТекНомер);
		
		ТекстЗапроса=ТекстЗапроса+Символы.ПС+";"+Символы.ПС+"/////////////////////////////////////////"+Символы.ПС+

		"ВЫБРАТЬ
		|	&Период				КАК Период,
		|	&ВидДвиженияПриход  ВидДвижения,
		|	Ссылка.Заказ		КАК ИнтернетЗаказ,
		|	Номенклатура,
		|	Упаковка,
		|	Цена,
		|	Акция,
		|	Размещение,
		|	ПроцентРучнойСкидки,
		|	ПроцентАвтоматическойСкидки,
		|	СтавкаНДС,
		|	СУММА(Количество)	КАК Количество,
	    |	СУММА(Сумма)		КАК Сумма
		|ИЗ
		|	Документ.КорректировкаИнтернетЗаказа.Товары
		|
		|ГДЕ
		|	Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	&ВидДвиженияПриход,
		|	Номенклатура,
		|	Упаковка,
		|	Цена,
		|	Акция,
		|	Размещение,
		|	ПроцентРучнойСкидки,
		|	ПроцентАвтоматическойСкидки,
		|	СтавкаНДС,
		|	Ссылка.Заказ"
	КонецЕсли;	
	
	
	Если ИменаРегистров.Найти("ТоварыВРезерве")<>Неопределено ИЛИ ПоВсем Тогда
		ТекНомер=ТекНомер+1;
		Нтаб.Вставить("ТоварыВРезерве",ТекНомер);
		
		
		ТекстЗапроса=ТекстЗапроса+Символы.ПС+";"+Символы.ПС+"/////////////////////////////////////////"+Символы.ПС+
		"ВЫБРАТЬ 
		|	&Период				КАК Период,
		|	&ВидДвиженияПриход  КАК ВидДвижения,
		|	Ссылка.Заказ		КАК ДокументРезерва,
		|	Размещение,
		|	Номенклатура,
		|	ВЫБОР 
		|		КОГДА Упаковка = &ПустаяУпаковка 
		|		ТОГДА СУММА(Количество)	
	    |		ИНАЧЕ СУММА(Количество)*Упаковка.Коэффициент 
		|	КОНЕЦ 			КАК Количество
		|ИЗ
		|	Документ.КорректировкаИнтернетЗаказа.Товары
		|
		|ГДЕ
		|	Ссылка = &Ссылка И
		|	Размещение ССЫЛКА Справочник.Склады  И
		|	ЕСТЬNULL(Размещение, &ПустойСклад) <> &ПустойСклад
		|
		|СГРУППИРОВАТЬ ПО
		|	Ссылка,
		|	Размещение,
		|	Номенклатура,
		|	Упаковка
		|ИМЕЮЩИЕ СУММА(Количество) <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период				КАК Период,
		|	&ВидДвиженияПриход ВидДвижения,
		|	Ссылка.Заказ		КАК ДокументРезерва,
		|	Размещение,
		|	Номенклатура,
		|	СУММА(Количество)	КАК Количество
		|ИЗ
		|	Документ.КорректировкаИнтернетЗаказа.РазмещениеТоваров	
		|ГДЕ
		|	Ссылка = &Ссылка И
		|	Размещение ССЫЛКА Справочник.Склады  И
		|	ЕСТЬNULL(Размещение, &ПустойСклад) <> &ПустойСклад
		|
		|СГРУППИРОВАТЬ ПО
		|	Ссылка, Размещение, Номенклатура"
	КонецЕсли;	
	
	
	Если ИменаРегистров.Найти("РазмещениеЗаказов")<>Неопределено ИЛИ ПоВсем Тогда
		ТекНомер=ТекНомер+1;
		Нтаб.Вставить("РазмещениеЗаказов",ТекНомер);
		
		
		ТекстЗапроса=ТекстЗапроса+Символы.ПС+";"+Символы.ПС+"/////////////////////////////////////////"+Символы.ПС+
		"ВЫБРАТЬ
		|	&Период				КАК Период,
		|	&ВидДвиженияПриход 	КАК ВидДвижения,
		|	Размещение 			КАК Очередь,
		|	Ссылка.Заказ		КАК Заказ,
		|	Номенклатура,
		|	Упаковка,
		|	СУММА(Количество)	КАК Количество
		|ИЗ
		|	Документ.КорректировкаИнтернетЗаказа.Товары
		|
		|ГДЕ
		|	Ссылка = &Ссылка И ТИПЗНАЧЕНИЯ(Размещение) = ТИП(ЧИСЛО)
		|
		|СГРУППИРОВАТЬ ПО
		|	Размещение,
		|	Ссылка.Заказ,
		|	Номенклатура,
		|	Упаковка
		|
		|ИМЕЮЩИЕ СУММА(Количество) <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период				КАК Период,
		|	&ВидДвиженияПриход ВидДвижения,
		|	Размещение 			Очередь,
		|	Ссылка.Заказ		КАК Заказ,
		|	Номенклатура,
		|	&ПустаяУпаковка,
		|	СУММА(Количество) Количество
	    |ИЗ
		|	Документ.КорректировкаИнтернетЗаказа.РазмещениеТоваров
		|
		|ГДЕ
		|	Ссылка = &Ссылка И ТИПЗНАЧЕНИЯ(Размещение) = ТИП(ЧИСЛО)
		|
		|СГРУППИРОВАТЬ ПО
		|	Ссылка, Размещение, Номенклатура"
	КонецЕсли;	
	
	
	Если ИменаРегистров.Найти("ДолгиПоЗаказам")<>Неопределено ИЛИ ПоВсем Тогда
		ТекНомер=ТекНомер+1;
		Нтаб.Вставить("ДолгиПоЗаказам",ТекНомер);
		
		ТекстЗапроса=ТекстЗапроса+Символы.ПС+";"+Символы.ПС+"/////////////////////////////////////////"+Символы.ПС+
		"ВЫБРАТЬ
		|	&Период				Период,
		|	&ВидДвиженияПриход	ВидДвижения,
		|	Заказ.Организация 	Организация,
		|	Заказ.Контрагент	Контрагент,
		|	Заказ				Заказ,
		|	&СуммаДокумента		Сумма
		|ИЗ
		|	Документ.КорректировкаИнтернетЗаказа
		|ГДЕ
		|	Ссылка = &Ссылка и &СуммаДокумента<>0 и (Заказ.Склад.ПередачаТовараМВЗ=Ложь И Заказ.МВЗ=Значение(Справочник.МВЗ.ПустаяСсылка) И Заказ.ПередачаТовара=Ложь)"
	КонецЕсли;	
	
	Если ИменаРегистров.Найти("ОтгруженныеЗаказы")<>Неопределено ИЛИ ПоВсем Тогда
		ТекНомер=ТекНомер+1;
		Нтаб.Вставить("ОтгруженныеЗаказы",ТекНомер);
		
		ТекстЗапроса=ТекстЗапроса+Символы.ПС+";"+Символы.ПС+"/////////////////////////////////////////"+Символы.ПС+
		"ВЫБРАТЬ
		|	&Период	Период,
		|	Ссылка.Заказ Заказ,
		|	СУММА(Номенклатура.Вес * ВЫБОР КОГДА Упаковка = &ПустаяУпаковка ТОГДА Количество ИНАЧЕ Количество * Упаковка.Коэффициент КОНЕЦ) 	ВесЗаказа,
		|	СУММА(Номенклатура.Объем * ВЫБОР КОГДА Упаковка = &ПустаяУпаковка ТОГДА Количество ИНАЧЕ Количество * Упаковка.Коэффициент КОНЕЦ) 	ОбъемЗаказа
		|ИЗ
		|	Документ.КорректировкаИнтернетЗаказа.Товары
		|ГДЕ
		|	Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	Ссылка"
	КонецЕсли;	
	
	Если ИменаРегистров.Найти("Лимиты")<>Неопределено ИЛИ ПоВсем Тогда
		ТекНомер=ТекНомер+1;
		Нтаб.Вставить("Лимиты",ТекНомер);
		
		ТекстЗапроса=ТекстЗапроса+Символы.ПС+";"+Символы.ПС+"/////////////////////////////////////////"+Символы.ПС+
		"ВЫБРАТЬ
		|   &Период,
		|	&ВидДвиженияРасход				ВидДвижения,
		|	Ссылка.Заказ.ОтветственноеЛицо	Инициатор,
		|	СУММА(Сумма) 					Сумма
		|ИЗ
		|	Документ.КорректировкаИнтернетЗаказа.Товары
		|ГДЕ
		|   Ссылка = &Ссылка И Ссылка.Заказ.ОтветственноеЛицо <> &ПустойИнициатор
		|СГРУППИРОВАТЬ ПО Ссылка
		|ИМЕЮЩИЕ СУММА(Сумма) <> 0"
	КонецЕсли;	
	
	Если ИменаРегистров.Найти("РазмещениеЗаказовВПути")<>Неопределено ИЛИ ПоВсем Тогда
		ТекНомер=ТекНомер+3;
		Нтаб.Вставить("РазмещениеЗаказовВПути",ТекНомер);
		
		ТекстЗапроса=ТекстЗапроса+Символы.ПС+";"+Символы.ПС+"/////////////////////////////////////////"+Символы.ПС+
		"ВЫБРАТЬ
		|	Номенклатура
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	Документ.КорректировкаИнтернетЗаказа.РазмещениеТоваров  
		|ГДЕ
		|	Ссылка = &Ссылка
		|	И Размещение ССЫЛКА Документ.ЗаказПоставщику
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Номенклатура,
		|	МАКСИМУМ(Очередь) КАК Очередь
		|ПОМЕСТИТЬ ТаблОчереди
		|ИЗ
		|	РегистрНакопления.РазмещениеЗаказовВПути.Остатки(
		|			&ПериодОчереди,
		|			Номенклатура В
		|				(ВЫБРАТЬ
		|					Товары.Номенклатура
		|				ИЗ
		|					Товары))  
		|
		|СГРУППИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	&ВидДвиженияПриход 		КАК ВидДвижения,
		|	ЕСТЬNULL(Оч.Очередь, 1) КАК Очередь,
		|	Док.Ссылка.Заказ 		КАК Размещение,
		|	Док.Номенклатура 		КАК Номенклатура,
		|	Док.Размещение 			КАК ЗаказПоставщику,
		|	СУММА(Док.Количество) 	КАК Количество
		|ИЗ
		|	Документ.КорректировкаИнтернетЗаказа.РазмещениеТоваров КАК Док
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблОчереди КАК Оч
		|		ПО Док.Номенклатура = Оч.Номенклатура
		|ГДЕ
		|	Док.Ссылка = &Ссылка
		|	И Док.Размещение ССЫЛКА Документ.ЗаказПоставщику
		|
		|СГРУППИРОВАТЬ ПО
		|	Док.Ссылка,
		|	ЕСТЬNULL(Оч.Очередь, 1),
		|	Док.Размещение,
		|	Док.Номенклатура"
	КонецЕсли;	
	
	
	

	Запрос=Новый Запрос;
	Запрос.Текст=ТекстЗапроса;
	
	Запрос.УстановитьПараметр("Организация", 			Ссылка.Заказ.Организация);
	Запрос.УстановитьПараметр("Контрагент", 			Ссылка.Заказ.Контрагент);
	Запрос.УстановитьПараметр("СуммаДокумента", 		Ссылка.Сумма);
	Запрос.УстановитьПараметр("Заказ", 					Ссылка.Заказ);
	
	Запрос.УстановитьПараметр("Ссылка", 			Ссылка);
	Запрос.УстановитьПараметр("Период", 			Ссылка.Дата);
	Запрос.УстановитьПараметр("ПериодОчереди", 		?(НачалоДня(Ссылка.Дата) = НачалоДня(ТекущаяДата()), НачалоДня(ТекущаяДата()), Ссылка.Дата));
	Запрос.УстановитьПараметр("ВидДвиженияПриход", 	ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("ВидДвиженияРасход", 	ВидДвиженияНакопления.Расход);
	Запрос.УстановитьПараметр("ПустойСклад", 		Справочники.Склады.ПустаяСсылка());
	
	Запрос.УстановитьПараметр("ПустаяУпаковка", 	Справочники.УпаковкиНоменклатуры.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустойИнициатор", 	Справочники.ФизическиеЛица.ПустаяСсылка());
	
	Пакет = Запрос.ВыполнитьПакет();
	ПараметрыСистемы = КонвертацияТипов.ПолучитьСтруктуруИзСТрокиТаблицыЗначений(Пакет[0].Выгрузить());
	
	ДополнительныеСвойства.Вставить("ПараметрыСистемы", 		ПараметрыСистемы);
	ДополнительныеСвойства.Вставить("Шапка", 					КонвертацияТипов.ПолучитьСтруктуруИзСТрокиТаблицыЗначений(Пакет[1].Выгрузить()));
	
	
	
	Для Каждого Элем Из Нтаб Цикл
		ДополнительныеСвойства.Вставить(Элем.Ключ,Пакет[Элем.Значение].Выгрузить());
	КонецЦикла;	
	
	ДополнительныеСвойства.Вставить("ДолгиПоОтгрузкам", Новый ТаблицаЗначений);  //очистка
	
	
	// Получим неудовлетворенный спрос
	Если ДополнительныеСвойства.Свойство("ИнтернетЗаказПокупателя") Тогда
		ДополнительныеСвойства.Вставить("НеудоволетворенныйСпрос", РаботаСНоменклатурой.ОпределитьНеудовлетворенныйСпрос(ДополнительныеСвойства.ИнтернетЗаказПокупателя, Ссылка.Заказ, Ссылка.Заказ.Склад, Ссылка.Заказ.Подразделение, Ссылка.Дата));
	КонецЕсли;
КонецПроцедуры


Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;     
	
	ПериодОтчета 		  = ПолучитьПараметрПользовательскихНастроек("ПериодОтчета");
	СписокЦенНоменклатуры = ПолучитьПараметрПользовательскихНастроек("СписокЦенНоменклатуры");
	МесяцевДо			  = ПолучитьПараметрПользовательскихНастроек("МесяцевДо");
	
	ПараметрыОтчета = Новый Структура;
	
	Если ТипЗнч(СписокЦенНоменклатуры) = Тип("СписокЗначений") Тогда
		ПараметрыОтчета.Вставить("СписокЦенНоменклатуры", СписокЦенНоменклатуры);
	Иначе  
		
		СписокТиповЦен = Новый СписокЗначений;
		
		Если ЗначениеЗаполнено(СписокЦенНоменклатуры) Тогда
			СписокТиповЦен.Добавить(СписокЦенНоменклатуры);
		КонецЕсли; 
		ПараметрыОтчета.Вставить("СписокЦенНоменклатуры", СписокТиповЦен);
	КонецЕсли; 
	
	ПараметрыОтчета.Вставить("МесяцевДо",			  МесяцевДо);
	ПараметрыОтчета.Вставить("ДатаНачала", 				НачалоДня(ПериодОтчета.ДатаНачала));
	ПараметрыОтчета.Вставить("ДатаОкончания", 		КонецДня(ПериодОтчета.ДатаОкончания));
	
	УстановитьПараметрНастройки("ДатаНачала",	 НачалоДня(ПериодОтчета.ДатаНачала));
	УстановитьПараметрНастройки("ДатаОкончания", КонецДня(ПериодОтчета.ДатаОкончания));
	УстановитьПараметрНастройки("ПериодОтчета",   	 ПериодОтчета);
	
	УстановитьОтборыПользовательскихНастроек();
	
	ТаблицаОсновнойНабор = ПолучитьТаблицуДанных(ПараметрыОтчета);
	   
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ОсновнойНабор", ТаблицаОсновнойНабор);
		
	Настройки = КомпоновщикНастроек.Настройки;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);

	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

Процедура УстановитьПараметрНастройки(ИмяПараметра, Значение, Использование = Истина)

	КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра).Использование = Использование;
	КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра).Значение      = Значение;

КонецПроцедуры
 
Функция ПолучитьПараметрПользовательскихНастроек(ИмяПараметра)

	Для каждого ЭлементНастройки Из КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
	
		Если ТипЗнч(ЭлементНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
		
			Если ЭлементНастройки.Параметр = Новый ПараметрКомпоновкиДанных(ИмяПараметра) Тогда
				Возврат ЭлементНастройки.Значение;
			КонецЕсли; 
		
		КонецЕсли; 
	
	КонецЦикла; 
	
	Возврат Неопределено;
	
КонецФункции // ПолучитьПериодПользовательскихНастроек()
 
Процедура УстановитьОтборыПользовательскихНастроек()

	Для каждого ЭлементПользовательскихНастроек Из КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
	
		Если ТипЗнч(ЭлементПользовательскихНастроек) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			                                   
			Для каждого ЭлементНастройки Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
			
				Если ЭлементНастройки.ИдентификаторПользовательскойНастройки = ЭлементПользовательскихНастроек.ИдентификаторПользовательскойНастройки Тогда
				
					ЭлементНастройки.ПравоеЗначение = ЭлементПользовательскихНастроек.ПравоеЗначение;
					ЭлементНастройки.Использование  = ЭлементПользовательскихНастроек.Использование;
					ЭлементНастройки.ВидСравнения   = ЭлементПользовательскихНастроек.ВидСравнения;
				
				КонецЕсли; 
			
			КонецЦикла; 			
			
		КонецЕсли; 
	
	КонецЦикла; 
		
КонецПроцедуры // УстановитьОтборыПользовательскихНастроек()

Функция ПолучитьТаблицуДанных(ПараметрыОтчета)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", 		   ПараметрыОтчета.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания",		   ПараметрыОтчета.ДатаОкончания);
	Запрос.УстановитьПараметр("МесяцевДо",			   ПараметрыОтчета.МесяцевДо);
	Запрос.УстановитьПараметр("СписокЦенНоменклатуры", ПараметрыОтчета.СписокЦенНоменклатуры);
	Запрос.УстановитьПараметр("КоличествоТиповЦен",    ПараметрыОтчета.СписокЦенНоменклатуры.Количество());
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПартииТоваров.Номенклатура КАК Номенклатура,
		|	СУММА(ПартииТоваров.Количество) КАК КоличествоПоступленияНовинок,
		|	МИНИМУМ(ПартииТоваров.Период) КАК ДатаПартии,
		|	СУММА(ЗаказыПоставщикам.Количество) КАК КоличествоЗаказаНовинок,
		|	ЗаказыПоставщикам.ЗаказПоставщику.Дата КАК ДатаЗаказа
		|ПОМЕСТИТЬ ВТ_ПоступленияИЗаказы
		|ИЗ
		|	РегистрНакопления.ПартииТоваров КАК ПартииТоваров
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыПоставщикам КАК ЗаказыПоставщикам
		|		ПО ПартииТоваров.Номенклатура = ЗаказыПоставщикам.Номенклатура
		|ГДЕ
		|	ПартииТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И (ПартииТоваров.Регистратор ССЫЛКА Документ.ПоступлениеТоваров
		|			ИЛИ ПартииТоваров.Регистратор ССЫЛКА Документ.ОприходованиеТоваров)
		|	И ЗаказыПоставщикам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПартииТоваров.Номенклатура,
		|	ЗаказыПоставщикам.ЗаказПоставщику.Дата
		|
		|ИМЕЮЩИЕ
		|	МИНИМУМ(ПартииТоваров.Период) МЕЖДУ ДОБАВИТЬКДАТЕ(&ДатаНачала, МЕСЯЦ, -&МесяцевДо) И &ДатаОкончания И
		|	МИНИМУМ(ЗаказыПоставщикам.ЗаказПоставщику.Дата) МЕЖДУ ДОБАВИТЬКДАТЕ(&ДатаНачала, МЕСЯЦ, -&МесяцевДо) И &ДатаОкончания
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗаказыПоставщикам.Номенклатура,
		|	СУММА(ПартииТоваров.Количество),
		|	ПартииТоваров.Период,
		|	СУММА(ЗаказыПоставщикам.Количество),
		|	МИНИМУМ(ЗаказыПоставщикам.ЗаказПоставщику.Дата)
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам КАК ЗаказыПоставщикам
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваров КАК ПартииТоваров
		|		ПО ЗаказыПоставщикам.Номенклатура = ПартииТоваров.Номенклатура
		|ГДЕ
		|	ЗаказыПоставщикам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ПартииТоваров.Номенклатура ЕСТЬ NULL 
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыПоставщикам.Номенклатура,
		|	ПартииТоваров.Период
		|
		|ИМЕЮЩИЕ
		|	МИНИМУМ(ЗаказыПоставщикам.ЗаказПоставщику.Дата) МЕЖДУ ДОБАВИТЬКДАТЕ(&ДатаНачала, МЕСЯЦ, -&МесяцевДо) И &ДатаОкончания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ПоступленияИЗаказы.Номенклатура,
		|	СУММА(ВТ_ПоступленияИЗаказы.КоличествоПоступленияНовинок) КАК КоличествоПоступленияНовинок,
		|	СУММА(ВТ_ПоступленияИЗаказы.КоличествоЗаказаНовинок) КАК КоличествоЗаказаНовинок
		|ПОМЕСТИТЬ ВТ_Номенклатура
		|ИЗ
		|	ВТ_ПоступленияИЗаказы КАК ВТ_ПоступленияИЗаказы
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ПоступленияИЗаказы.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИСТИНА КАК ЕстьКартинка,
		|	КартинкиНоменклатуры.Владелец
		|ПОМЕСТИТЬ ВТ_КартинкиНоменклатуры
		|ИЗ
		|	Справочник.КартинкиНоменклатуры КАК КартинкиНоменклатуры
		|ГДЕ
		|	НЕ КартинкиНоменклатуры.ПометкаУдаления
		|	И КартинкиНоменклатуры.ЭтоПредставлениеОбъекта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартииТоваров.Номенклатура,
		|	МИНИМУМ(ПартииТоваров.ДатаПартии) КАК ДатаПартии
		|ПОМЕСТИТЬ ВТ_ДатаПервогоПоступления
		|ИЗ
		|	РегистрНакопления.ПартииТоваров КАК ПартииТоваров
		|ГДЕ
		|	ПартииТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И (ПартииТоваров.Регистратор ССЫЛКА Документ.ПоступлениеТоваров
		|			ИЛИ ПартииТоваров.Регистратор ССЫЛКА Документ.ОприходованиеТоваров)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПартииТоваров.Номенклатура
		|
		|ИМЕЮЩИЕ
		|	МИНИМУМ(ПартииТоваров.ДатаПартии) МЕЖДУ ДОБАВИТЬКДАТЕ(&ДатаНачала, МЕСЯЦ, -&МесяцевДо) И &ДатаОкончания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказыПоставщикам.Номенклатура,
		|	МАКСИМУМ(ЗаказыПоставщикам.Регистратор.ДатаПоступления) КАК ДатаПоступленияИнвойс,
		|	МАКСИМУМ(ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПоступленияЗаказ
		|ПОМЕСТИТЬ ВТ_ДатыОжидаемогоПоступления
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам КАК ЗаказыПоставщикам
		|ГДЕ
		|	ЗаказыПоставщикам.Регистратор ССЫЛКА Документ.Инвойс
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыПоставщикам.Номенклатура
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗаказыПоставщикам.Номенклатура,
		|	МАКСИМУМ(ДАТАВРЕМЯ(1, 1, 1)),
		|	МАКСИМУМ(ЗаказыПоставщикам.Регистратор.ДатаПоступления)
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам КАК ЗаказыПоставщикам
		|ГДЕ
		|	ЗаказыПоставщикам.Регистратор ССЫЛКА Документ.ЗаказПоставщику
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыПоставщикам.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДатыОжидаемогоПоступления.Номенклатура,
		|	МАКСИМУМ(ВТ_ДатыОжидаемогоПоступления.ДатаПоступленияИнвойс) КАК ДатаПоступленияИнвойс,
		|	МАКСИМУМ(ВТ_ДатыОжидаемогоПоступления.ДатаПоступленияЗаказ) КАК ДатаПоступленияЗаказ,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ВТ_ДатыОжидаемогоПоступления.ДатаПоступленияИнвойс > ВТ_ДатыОжидаемогоПоступления.ДатаПоступленияЗаказ
		|				ТОГДА ВТ_ДатыОжидаемогоПоступления.ДатаПоступленияИнвойс
		|			КОГДА ВТ_ДатыОжидаемогоПоступления.ДатаПоступленияЗаказ > ВТ_ДатыОжидаемогоПоступления.ДатаПоступленияИнвойс
		|				ТОГДА ВТ_ДатыОжидаемогоПоступления.ДатаПоступленияЗаказ
		|		КОНЕЦ) КАК ДатаОжидаемогоПоступления
		|ПОМЕСТИТЬ ВТ_ДатыОжидаемогоПоступления_Итого
		|ИЗ
		|	ВТ_ДатыОжидаемогоПоступления КАК ВТ_ДатыОжидаемогоПоступления
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ДатыОжидаемогоПоступления.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказыПоставщикамОстатки.Номенклатура,
		|	ЗаказыПоставщикамОстатки.КоличествоОстаток КАК Заказано
		|ПОМЕСТИТЬ ВТ_Заказано
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам.Остатки(&ДатаОкончания, ) КАК ЗаказыПоставщикамОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыПоставщиковВПутиОстатки.Номенклатура,
		|	ТоварыПоставщиковВПутиОстатки.КоличествоОстаток КАК ВПути
		|ПОМЕСТИТЬ ВТ_ВПути
		|ИЗ
		|	РегистрНакопления.ТоварыПоставщиковВПути.Остатки(&ДатаОкончания, ) КАК ТоварыПоставщиковВПутиОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Номенклатура,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК Остаток
		|ПОМЕСТИТЬ ВТ_Остатки
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(&ДатаОкончания, ) КАК ТоварыНаСкладахОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЦеныНоменклатурыСрезПоследних.ТипЦен) КАК ТипЦен,
		|	ЦеныНоменклатурыСрезПоследних.Номенклатура
		|ПОМЕСТИТЬ ВТ_ЦеныНоменклатуры
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ДатаОкончания, ТипЦен В (&СписокЦенНоменклатуры)) КАК ЦеныНоменклатурыСрезПоследних
		|
		|СГРУППИРОВАТЬ ПО
		|	ЦеныНоменклатурыСрезПоследних.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Номенклатура.Номенклатура,
		|	ВТ_Номенклатура.КоличествоПоступленияНовинок,
		|	ВТ_Номенклатура.КоличествоЗаказаНовинок,
		|	ВТ_ДатаПервогоПоступления.ДатаПартии,
		|	ВТ_ДатыОжидаемогоПоступления_Итого.ДатаПоступленияИнвойс,
		|	ВТ_ДатыОжидаемогоПоступления_Итого.ДатаПоступленияЗаказ,
		|	ВТ_ДатыОжидаемогоПоступления_Итого.ДатаОжидаемогоПоступления,
		|	ВТ_Номенклатура.Номенклатура.Наименование КАК НоменклатураНаименование,
		|	ВТ_Заказано.Заказано,
		|	ВТ_ВПути.ВПути,
		|	ВТ_Номенклатура.Номенклатура.ВыгружатьНаСайт КАК СайтНаличие,
		|	ВТ_Номенклатура.Номенклатура.СрокПроизводства КАК СрокПроизводства,
		|	ВТ_Номенклатура.Номенклатура.СрокДоставки КАК СрокДоставки,
		|	ВТ_Остатки.Остаток,
		|	ВЫБОР
		|		КОГДА ВТ_КартинкиНоменклатуры.ЕстьКартинка = ИСТИНА
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЕстьКартинка,
		|	ВТ_Номенклатура.Номенклатура.Артикул,
		|	ВТ_Номенклатура.Номенклатура.КатегорияТовара,
		|	ВЫБОР
		|		КОГДА &КоличествоТиповЦен = ВТ_ЦеныНоменклатуры.ТипЦен
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ВсеЦены,
		|	ВТ_ЦеныНоменклатуры.ТипЦен КАК КоличествоЦен
		|ИЗ
		|	ВТ_Номенклатура КАК ВТ_Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДатаПервогоПоступления КАК ВТ_ДатаПервогоПоступления
		|		ПО ВТ_Номенклатура.Номенклатура = ВТ_ДатаПервогоПоступления.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДатыОжидаемогоПоступления_Итого КАК ВТ_ДатыОжидаемогоПоступления_Итого
		|		ПО ВТ_Номенклатура.Номенклатура = ВТ_ДатыОжидаемогоПоступления_Итого.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Заказано КАК ВТ_Заказано
		|		ПО ВТ_Номенклатура.Номенклатура = ВТ_Заказано.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВПути КАК ВТ_ВПути
		|		ПО ВТ_Номенклатура.Номенклатура = ВТ_ВПути.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Остатки КАК ВТ_Остатки
		|		ПО ВТ_Номенклатура.Номенклатура = ВТ_Остатки.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КартинкиНоменклатуры КАК ВТ_КартинкиНоменклатуры
		|		ПО ВТ_Номенклатура.Номенклатура = ВТ_КартинкиНоменклатуры.Владелец
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЦеныНоменклатуры КАК ВТ_ЦеныНоменклатуры
		|		ПО ВТ_Номенклатура.Номенклатура = ВТ_ЦеныНоменклатуры.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	НоменклатураНаименование";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();

КонецФункции // ПолучитьТаблицуДанных()
 



Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗадачаПользователю.Заказ КАК Заказ,
	|	ЗадачаПользователю.Склад
	|ПОМЕСТИТЬ Заказы
	|ИЗ
	|	Задача.ЗадачаПользователю КАК ЗадачаПользователю
	|ГДЕ
	|	ЗадачаПользователю.ТочкаМаршрута = ЗНАЧЕНИЕ(БизнесПроцесс.СборкаЗаказа.ТочкаМаршрута.СкорректироватьЗаказ)
	|	И НЕ ЗадачаПользователю.Выполнена
	|	И ЗадачаПользователю.Склад В(&Склады)
	|	И ЗадачаПользователю.Роль = ЗНАЧЕНИЕ(Справочник.Роли.Логист)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СостоянияЗаказовТекущее.Заказ,
	|	СостоянияЗаказовТекущее.Состояние
	|ПОМЕСТИТЬ ВТСостоянияЗаказовТекущее
	|ИЗ
	|	РегистрСведений.СостоянияЗаказовТекущее КАК СостоянияЗаказовТекущее
	|ГДЕ
	|	СостоянияЗаказовТекущее.Заказ В
	|			(ВЫБРАТЬ
	|				Заказы.Заказ
	|			ИЗ
	|				Заказы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказыНаПеремещенияОстатки.Номенклатура,
	|	ЗаказыНаПеремещенияОстатки.Упаковка,
	|	ЗаказыНаПеремещенияОстатки.СкладОтправитель КАК Размещение,
	|	ЗаказыНаПеремещенияОстатки.КоличествоОстаток - ЕСТЬNULL(ТоварыВСборкеОстатки.КоличествоОстаток, 0) КАК Количество,
	|	ЗаказыНаПеремещенияОстатки.КоличествоОстаток - ЕСТЬNULL(ТоварыВСборкеОстатки.КоличествоОстаток, 0) КАК КоличествоВычитаемоеИзРезерва,
	|	ЗНАЧЕНИЕ(Перечисление.СостояниесборкиЗаказа.ОжидаетПеремещения) КАК Состояние,
	|	ЗаказыНаПеремещенияОстатки.Заказ
	|ПОМЕСТИТЬ Состояния
	|ИЗ
	|	РегистрНакопления.ЗаказыНаПеремещения.Остатки(
	|			,
	|			Заказ В
	|				(ВЫБРАТЬ
	|					Заказы.Заказ
	|				ИЗ
	|					Заказы)) КАК ЗаказыНаПеремещенияОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыВСборке.Остатки(
	|				,
	|				Заказ В
	|					(ВЫБРАТЬ
	|						Заказы.Заказ
	|					ИЗ
	|						Заказы)) КАК ТоварыВСборкеОстатки
	|		ПО ЗаказыНаПеремещенияОстатки.Номенклатура = ТоварыВСборкеОстатки.Номенклатура
	|			И ЗаказыНаПеремещенияОстатки.Упаковка = ТоварыВСборкеОстатки.Упаковка
	|			И ЗаказыНаПеремещенияОстатки.СкладОтправитель = ТоварыВСборкеОстатки.Склад
	|			И ЗаказыНаПеремещенияОстатки.Заказ = ТоварыВСборкеОстатки.Заказ
	|ГДЕ
	|	ЗаказыНаПеремещенияОстатки.КоличествоОстаток - ЕСТЬNULL(ТоварыВСборкеОстатки.КоличествоОстаток, 0) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыВПутиОстатки.Номенклатура,
	|	ТоварыВПутиОстатки.Упаковка,
	|	ТоварыВПутиОстатки.СкладОтправитель,
	|	ТоварыВПутиОстатки.КоличествоОстаток,
	|	0,
	|	ЗНАЧЕНИЕ(Перечисление.СостояниесборкиЗаказа.Перемещается),
	|	ТоварыВПутиОстатки.Заказчик.Заказ
	|ИЗ
	|	РегистрНакопления.ТоварыВПути.Остатки(
	|			,
	|			Заказчик В
	|				(ВЫБРАТЬ
	|					бизнесПроцесс.СборкаЗаказа.Ссылка
	|				ИЗ
	|					бизнесПроцесс.СборкаЗаказа
	|				ГДЕ
	|					бизнесПроцесс.СборкаЗаказа.Заказ В
	|						(ВЫБРАТЬ
	|							Заказы.Заказ
	|						ИЗ
	|							Заказы))) КАК ТоварыВПутиОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыВСборкеОстатки.Номенклатура,
	|	ТоварыВСборкеОстатки.Упаковка,
	|	ТоварыВСборкеОстатки.Склад,
	|	ТоварыВСборкеОстатки.КоличествоОстаток,
	|	ТоварыВСборкеОстатки.КоличествоОстаток,
	|	ЗНАЧЕНИЕ(Перечисление.СостояниесборкиЗаказа.Собирается),
	|	ТоварыВСборкеОстатки.Заказ
	|ИЗ
	|	РегистрНакопления.ТоварыВСборке.Остатки(
	|			,
	|			Заказ В
	|				(ВЫБРАТЬ
	|					Заказы.Заказ
	|				ИЗ
	|					Заказы)) КАК ТоварыВСборкеОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыСобранныеОстатки.Номенклатура,
	|	ТоварыСобранныеОстатки.Упаковка,
	|	ТоварыСобранныеОстатки.Склад,
	|	ТоварыСобранныеОстатки.КоличествоОстаток,
	|	ТоварыСобранныеОстатки.КоличествоОстаток,
	|	ЗНАЧЕНИЕ(Перечисление.СостояниесборкиЗаказа.Собрано),
	|	ТоварыСобранныеОстатки.Заказ
	|ИЗ
	|	РегистрНакопления.ТоварыСобранные.Остатки(
	|			,
	|			(Заказ, Склад) В
	|				(ВЫБРАТЬ
	|					Заказы.Заказ,
	|					Заказы.Склад
	|				ИЗ
	|					Заказы)) КАК ТоварыСобранныеОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтменаСборкиЗаказаОстатки.Номенклатура,
	|	ОтменаСборкиЗаказаОстатки.Упаковка,
	|	ВЫБОР
	|		КОГДА ОтменаСборкиЗаказаОстатки.СкладЯчейка ССЫЛКА Справочник.Склады
	|			ТОГДА ОтменаСборкиЗаказаОстатки.СкладЯчейка
	|		ИНАЧЕ ОтменаСборкиЗаказаОстатки.СкладЯчейка.Владелец
	|	КОНЕЦ,
	|	ОтменаСборкиЗаказаОстатки.СобраноОстаток,
	|	ОтменаСборкиЗаказаОстатки.СобраноОстаток,
	|	ЗНАЧЕНИЕ(Перечисление.СостояниесборкиЗаказа.ОтменаСборки),
	|	ОтменаСборкиЗаказаОстатки.Заказ
	|ИЗ
	|	РегистрНакопления.ОтменаСборкиЗаказа.Остатки(
	|			,
	|			Заказ В
	|				(ВЫБРАТЬ
	|					Заказы.Заказ
	|				ИЗ
	|					Заказы)) КАК ОтменаСборкиЗаказаОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Продажи.Номенклатура,
	|	Продажи.Упаковка,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
	|	СУММА(Продажи.Количество),
	|	0,
	|	ЗНАЧЕНИЕ(Перечисление.СостояниесборкиЗаказа.Отгружен),
	|	Продажи.Заказ
	|ИЗ
	|	РегистрНакопления.Продажи КАК Продажи
	|ГДЕ
	|	Продажи.Заказ В
	|			(ВЫБРАТЬ
	|				Заказы.Заказ
	|			ИЗ
	|				Заказы)
	|
	|СГРУППИРОВАТЬ ПО
	|	Продажи.Номенклатура,
	|	Продажи.Упаковка,
	|	Продажи.Заказ
	|
	|ИМЕЮЩИЕ
	|	СУММА(Продажи.Количество) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВнутренниеЗаказы.Номенклатура,
	|	ВнутренниеЗаказы.Упаковка,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
	|	СУММА(ВнутренниеЗаказы.Количество),
	|	0,
	|	ЗНАЧЕНИЕ(Перечисление.СостояниесборкиЗаказа.Отгружен),
	|	ВнутренниеЗаказы.ВнутреннийЗаказ
	|ИЗ
	|	РегистрНакопления.ВнутренниеЗаказы КАК ВнутренниеЗаказы
	|ГДЕ
	|	ВнутренниеЗаказы.ВнутреннийЗаказ В
	|			(ВЫБРАТЬ
	|				Заказы.Заказ
	|			ИЗ
	|				Заказы)
	|	И ВнутренниеЗаказы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВнутренниеЗаказы.Номенклатура,
	|	ВнутренниеЗаказы.Упаковка,
	|	ВнутренниеЗаказы.ВнутреннийЗаказ
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВнутренниеЗаказы.Количество) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РазмещениеЗаказовОстатки.Номенклатура,
	|	РазмещениеЗаказовОстатки.Упаковка,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
	|	РазмещениеЗаказовОстатки.КоличествоОстаток,
	|	0,
	|	ЗНАЧЕНИЕ(Перечисление.СостояниесборкиЗаказа.ОжидаетПоступления),
	|	РазмещениеЗаказовОстатки.Заказ
	|ИЗ
	|	РегистрНакопления.РазмещениеЗаказов.Остатки(
	|			,
	|			Заказ В
	|				(ВЫБРАТЬ
	|					Заказы.Заказ
	|				ИЗ
	|					Заказы)) КАК РазмещениеЗаказовОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Продажи.Номенклатура,
	|	Продажи.Упаковка,
	|	Продажи.Склад,
	|	-СУММА(Продажи.Количество),
	|	0,
	|	ЗНАЧЕНИЕ(Перечисление.СостояниесборкиЗаказа.Возвращено),
	|	Продажи.Заказ
	|ИЗ
	|	РегистрНакопления.Продажи КАК Продажи
	|ГДЕ
	|	Продажи.Заказ В
	|			(ВЫБРАТЬ
	|				Заказы.Заказ
	|			ИЗ
	|				Заказы)
	|	И Продажи.Количество < 0
	|
	|СГРУППИРОВАТЬ ПО
	|	Продажи.Номенклатура,
	|	Продажи.Упаковка,
	|	Продажи.Склад,
	|	Продажи.Заказ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Состояния.Номенклатура,
	|	Состояния.Размещение,
	|	СУММА(Состояния.КоличествоВычитаемоеИзРезерва) КАК КоличествоВычитаемоеИзРезерва,
	|	Состояния.Заказ
	|ПОМЕСТИТЬ СостоянияВычитаемыеИзРезерва
	|ИЗ
	|	Состояния КАК Состояния
	|ГДЕ
	|	Состояния.КоличествоВычитаемоеИзРезерва > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	Состояния.Номенклатура,
	|	Состояния.Размещение,
	|	Состояния.Заказ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Резервы.Номенклатура КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) КАК Упаковка,
	|	Резервы.Размещение КАК Размещение,
	|	Резервы.КоличествоОстаток - ЕСТЬNULL(Состояния.КоличествоВычитаемоеИзРезерва, 0) КАК Количество,
	|	Резервы.ДокументРезерва КАК Заказ
	|ПОМЕСТИТЬ РезервыБезСостояния
	|ИЗ
	|	РегистрНакопления.ТоварыВРезерве.Остатки(
	|			,
	|			ДокументРезерва В
	|				(ВЫБРАТЬ
	|					Заказы.Заказ
	|				ИЗ
	|					Заказы)) КАК Резервы
	|		ЛЕВОЕ СОЕДИНЕНИЕ СостоянияВычитаемыеИзРезерва КАК Состояния
	|		ПО Резервы.Номенклатура = Состояния.Номенклатура
	|			И Резервы.Размещение = Состояния.Размещение
	|			И Резервы.ДокументРезерва = Состояния.Заказ
	|ГДЕ
	|	Резервы.КоличествоОстаток - ЕСТЬNULL(Состояния.КоличествоВычитаемоеИзРезерва, 0) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Доки.Номенклатура,
	|	Доки.Упаковка,
	|	СУММА(Доки.Количество) КАК Количество,
	|	Доки.Заказ
	|ПОМЕСТИТЬ ТабДок
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаказПокупателяТовары.Номенклатура КАК Номенклатура,
	|		ЗаказПокупателяТовары.Упаковка КАК Упаковка,
	|		ЗаказПокупателяТовары.Количество КАК Количество,
	|		ЗаказПокупателяТовары.Ссылка КАК Заказ
	|	ИЗ
	|		Документ.ЗаказПокупателя.Товары КАК ЗаказПокупателяТовары
	|	ГДЕ
	|		ЗаказПокупателяТовары.Ссылка В
	|				(ВЫБРАТЬ
	|					Заказы.Заказ
	|				ИЗ
	|					Заказы)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		КорректировкаВнутреннегоЗаказаТовары.Номенклатура,
	|		КорректировкаВнутреннегоЗаказаТовары.Упаковка,
	|		КорректировкаВнутреннегоЗаказаТовары.Количество,
	|		NULL
	|	ИЗ
	|		Документ.КорректировкаВнутреннегоЗаказа.Товары КАК КорректировкаВнутреннегоЗаказаТовары
	|	ГДЕ
	|		КорректировкаВнутреннегоЗаказаТовары.Ссылка.Заказ В
	|				(ВЫБРАТЬ
	|					Заказы.Заказ
	|				ИЗ
	|					Заказы)
	|		И КорректировкаВнутреннегоЗаказаТовары.Ссылка.Проведен) КАК Доки
	|
	|СГРУППИРОВАТЬ ПО
	|	Доки.Номенклатура,
	|	Доки.Упаковка,
	|	Доки.Заказ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабДок.Номенклатура КАК Номенклатура,
	|	ТабДок.Упаковка КАК Упаковка,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Размещение,
	|	ТабДок.Количество - ЕСТЬNULL(СостоянияГрп.Количество, 0) - ЕСТЬNULL(РезервыБезСостояния.Количество, 0) КАК Количество,
	|	ТабДок.Заказ
	|ПОМЕСТИТЬ БезРезервовБезСостояния
	|ИЗ
	|	ТабДок КАК ТабДок
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			Состояния.Номенклатура КАК Номенклатура,
	|			СУММА(Состояния.Количество) КАК Количество,
	|			Состояния.Заказ КАК Заказ
	|		ИЗ
	|			Состояния КАК Состояния
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Состояния.Номенклатура,
	|			Состояния.Заказ) КАК СостоянияГрп
	|		ПО ТабДок.Номенклатура = СостоянияГрп.Номенклатура
	|			И ТабДок.Заказ = СостоянияГрп.Заказ
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			РезервыБезСостояния.Номенклатура КАК Номенклатура,
	|			СУММА(РезервыБезСостояния.Количество) КАК Количество,
	|			РезервыБезСостояния.Заказ КАК Заказ
	|		ИЗ
	|			РезервыБезСостояния КАК РезервыБезСостояния
	|		
	|		СГРУППИРОВАТЬ ПО
	|			РезервыБезСостояния.Номенклатура,
	|			РезервыБезСостояния.Заказ) КАК РезервыБезСостояния
	|		ПО ТабДок.Номенклатура = РезервыБезСостояния.Номенклатура
	|			И ТабДок.Заказ = РезервыБезСостояния.Заказ
	|ГДЕ
	|	ТабДок.Количество - ЕСТЬNULL(СостоянияГрп.Количество, 0) - ЕСТЬNULL(РезервыБезСостояния.Количество, 0) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Состояния.Номенклатура КАК Номенклатура,
	|	Состояния.Упаковка КАК Упаковка,
	|	Состояния.Размещение КАК Размещение,
	|	Состояния.Количество КАК Количество,
	|	Состояния.Состояние КАК Состояние,
	|	Состояния.Заказ КАК Заказ
	|ПОМЕСТИТЬ ВТПромежуточныеСостояния
	|ИЗ
	|	Состояния КАК Состояния
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РезервыБезСостояния.Номенклатура,
	|	РезервыБезСостояния.Упаковка,
	|	РезервыБезСостояния.Размещение,
	|	РезервыБезСостояния.Количество,
	|	ЗНАЧЕНИЕ(Перечисление.СостояниесборкиЗаказа.НеСобрано),
	|	РезервыБезСостояния.Заказ
	|ИЗ
	|	РезервыБезСостояния КАК РезервыБезСостояния
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БезРезервовБезСостояния.Номенклатура,
	|	БезРезервовБезСостояния.Упаковка,
	|	БезРезервовБезСостояния.Размещение,
	|	БезРезервовБезСостояния.Количество,
	|	ЗНАЧЕНИЕ(Перечисление.СостояниесборкиЗаказа.НеСобрано),
	|	БезРезервовБезСостояния.Заказ
	|ИЗ
	|	БезРезервовБезСостояния КАК БезРезервовБезСостояния
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.Номенклатура.Артикул КАК артикул,
	|	ВТ.Номенклатура.Вес КАК Вес,
	|	ВТ.Количество,
	|	ЗНАЧЕНИЕ(Справочник.Маршруты.ПустаяСсылка) КАК Маршрут,
	|	ВТ.Номенклатура КАК Номенклатура,
	|	НЕОПРЕДЕЛЕНО КАК НоменклатураУчет,
	|	ВТ.Номенклатура.Объем КАК Объем,
	|	ВТ.Размещение,
	|	ВТ.Состояние КАК СостояниеНоменклатуры,
	|	ВТ.Упаковка,
	|	ВТ.Заказ,
	|	ВТСостоянияЗаказовТекущее.Состояние КАК СостояниеЗаказа
	|ПОМЕСТИТЬ ВТОбщаяТаблица
	|ИЗ
	|	ВТПромежуточныеСостояния КАК ВТ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСостоянияЗаказовТекущее КАК ВТСостоянияЗаказовТекущее
	|		ПО ВТ.Заказ = ВТСостоянияЗаказовТекущее.Заказ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОбщаяТаблица.Заказ,
	|	ВТОбщаяТаблица.СостояниеЗаказа,
	|	ВТОбщаяТаблица.СостояниеНоменклатуры,
	|	КОЛИЧЕСТВО(ВТОбщаяТаблица.Номенклатура) КАК КоличествоПозиций
	|ПОМЕСТИТЬ ВТСверткаПоСостояниюНоменклатуры
	|ИЗ
	|	ВТОбщаяТаблица КАК ВТОбщаяТаблица
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТОбщаяТаблица.СостояниеНоменклатуры,
	|	ВТОбщаяТаблица.СостояниеЗаказа,
	|	ВТОбщаяТаблица.Заказ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСверткаПоСостояниюНоменклатуры.СостояниеЗаказа КАК СостояниеЗаказа,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТСверткаПоСостояниюНоменклатуры.Заказ) КАК КоличествоЗаказов,
	|	ВТСверткаПоСостояниюНоменклатуры.СостояниеНоменклатуры КАК СостояниеНоменклатуры,
	|	СУММА(ВТСверткаПоСостояниюНоменклатуры.КоличествоПозиций) КАК КоличествоПозиций
	|{ВЫБРАТЬ
	|	СостояниеЗаказа,
	|	КоличествоЗаказов,
	|	СостояниеНоменклатуры,
	|	КоличествоПозиций}
	|ИЗ
	|	ВТСверткаПоСостояниюНоменклатуры КАК ВТСверткаПоСостояниюНоменклатуры
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТСверткаПоСостояниюНоменклатуры.СостояниеНоменклатуры,
	|	ВТСверткаПоСостояниюНоменклатуры.СостояниеЗаказа
	|
	|УПОРЯДОЧИТЬ ПО
	|	СостояниеЗаказа,
	|	СостояниеНоменклатуры";
	
	Склады = Неопределено;
	Для каждого ЭлементНастройки Из КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		Если ТипЗнч(ЭлементНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
			Если ЭлементНастройки.Параметр = Новый ПараметрКомпоновкиДанных("Склады") Тогда
				Склады = ЭлементНастройки.Значение;
				Прервать;
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла; 
	
	Запрос.УстановитьПараметр("Склады", Склады);
	
	РезультатЗапроса = Запрос.Выполнить();
	ТЗРезультат = РезультатЗапроса.Выгрузить();
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ТЗРезультат",ТЗРезультат);
	
	Настройки = КомпоновщикНастроек.Настройки;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	Заказ = ПолучитьРасшифровкуНаСервере(Расшифровка);
	
	Если Заказ = Неопределено Тогда Возврат; КонецЕсли;
	
	Контрагент = ПолучитьКонтрагентаНаСервере(Заказ);

    СтандартнаяОбработка = Ложь;
	
	ПользовательскиеНастройки = ПолучитьПользовательскиеНастройкиОтчета("АнализЗадолженностиПоЗаказам");
    УстановитьЗначениеПользовательскойНастройки(ПользовательскиеНастройки,	"Заказ" , Заказ, Истина);
    УстановитьЗначениеПользовательскойНастройки(ПользовательскиеНастройки,	"Контрагент" , Контрагент, Истина);
	
    ПараметрыФормыХ = Новый Структура("СформироватьПриОткрытии, Отбор, ПользовательскиеНастройки, КлючВарианта", Истина, , ПользовательскиеНастройки, "ДляРасшифровки");

    ОткрытьФорму("Отчет.АнализЗадолженностиПоЗаказам.Форма", ПараметрыФормыХ, ЭтаФорма);	

КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеПользовательскойНастройки(Настройки, Имя, Значение, Использование = Истина)
    Для Каждого Элемент Из Настройки.Элементы Цикл
        Если ТипЗнч(Элемент) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
            Если Строка(Элемент.Параметр) = Имя Тогда
                Элемент.Значение = Значение;
                Элемент.Использование = Использование;
            КонецЕсли;
        КонецЕсли;
    КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ПолучитьПользовательскиеНастройкиОтчета(ИмяОтчета)
    Возврат Отчеты[ИмяОтчета].Создать().КомпоновщикНастроек.ПользовательскиеНастройки;
КонецФункции

&НаСервере
Функция ПолучитьРасшифровкуНаСервере(Расшифровка)

   Данные = ПолучитьИзВременногоХранилища(ДанныеРасшифровки); 

   Поля = Данные.Элементы.Получить(Расшифровка).ПолучитьПоля(); 
   ПолеЗаказ = Поля.Найти("Заказ");

   Если ПолеЗаказ = Неопределено Тогда
       Возврат Неопределено;
   Иначе
       Возврат ПолеЗаказ.Значение;
   КонецЕсли;

КонецФункции

&НаСервере
Функция ПолучитьКонтрагентаНаСервере(Заказ)

    Возврат Заказ.Контрагент;
	
КонецФункции

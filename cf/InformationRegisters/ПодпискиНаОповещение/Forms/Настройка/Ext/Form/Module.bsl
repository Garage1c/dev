
&НаСервере
Функция СтруктураСобытияПоИмени(Имя) 
	
	Событие = Справочники.События[Имя];
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1 ИСТИНА ИЗ РегистрСведений.ПодпискиНаОповещение ГДЕ Событие = &Событие И Подписчик = &Пользователь И Ссылка = НЕОПРЕДЕЛЕНО");
	Запрос.УстановитьПараметр("Пользователь", 	ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("Событие", 		Событие);
	
	Возврат Новый Структура("Событие, ЭтоНовый", Событие, Запрос.Выполнить().Пустой());
	
КонецФункции
&НаКлиенте
Процедура ДекорацияНажатие(Элемент)
	
	Структура = СтруктураСобытияПоИмени(Сред(Элемент.Имя, 9));
	
	Если Структура.ЭтоНовый Тогда
			ОткрытьФорму("РегистрСведений.ПодпискиНаОповещение.ФормаЗаписи", Новый Структура("Событие, Пользователь", Структура.Событие, глТекущийПользователь), ЭтаФорма, , , , Новый ОписаниеОповещения("ТекстПодпискиЗакрытиеФормы", ЭтаФорма));
	Иначе	СобытияКлиент.ОткрытьФормуПодписки(Структура.Событие,,,ЭтаФорма); КонецЕсли;
	
КонецПроцедуры
&НаКлиенте
Процедура ТекстПодпискиЗакрытиеФормы(РезЗакрытия, ДопПараметры) Экспорт
	
	ОбновитьПодписки();
	
КонецПроцедуры

&НаСервере
Функция ДобавитьДекорацию(Событие, Имя)
	
	Декорация = ЭтаФорма.Элементы.Добавить(Имя, Тип("ДекорацияФормы"), Элементы.ГруппаДекораций);
	Декорация.Вид 						= ВидДекорацииФормы.Надпись;
	Декорация.Гиперссылка	 			= Истина;
	Декорация.АвтоМаксимальнаяШирина	= Ложь;
	
	Декорация.УстановитьДействие("Нажатие", "ДекорацияНажатие");
	
	Возврат Декорация;
	
КонецФункции
&НаСервере
Процедура ОбновитьПодписки()
	
	// Обновим подписки в декорациях
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ 	ИмяПредопределенныхДанных Имя, *,
	|			ВЫБОР КОГДА Подп.Событие ЕСТЬ NULL ТОГДА ЛОЖЬ ИНАЧЕ ИСТИНА КОНЕЦ Подписан
	|ИЗ 		Справочник.События Спр
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	(ВЫБРАТЬ * ИЗ РегистрСведений.ПодпискиНаОповещение ГДЕ Ссылка = НЕОПРЕДЕЛЕНО И Подписчик = &Пользователь) Подп
	|ПО	Спр.Ссылка = Подп.Событие
	|
	|ГДЕ НЕ ПометкаУдаления И Не Отключено
	|");
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Имя 		= "Подписка" + Выборка.Имя;
		Декорация 	= Элементы.Найти(Имя);
		Если Декорация = Неопределено Тогда
			Декорация = ДобавитьДекорацию(Выборка.Ссылка, Имя); КонецЕсли;
			
		Если Выборка.Подписан Тогда
				Декорация.Заголовок = "Все " + События.ПолучитьОписаниеПодписки(Выборка);
		Иначе	Декорация.Заголовок = "Не подписан - все события """ + НРег(Выборка.Наименование) + """"; КонецЕсли; КонецЦикла;
	
	// Обновим подписки в таблице
	
	Подписки.Очистить();
	
	Запрос = Новый Запрос("ВЫБРАТЬ * ИЗ РегистрСведений.ПодпискиНаОповещение ГДЕ Подписчик = &Пользователь И Ссылка <> НЕОПРЕДЕЛЕНО УПОРЯДОЧИТЬ ПО ДатаПодписки Убыв");
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НовСтрока = Подписки.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрока, Выборка);
		НовСтрока.Описание = События.ПолучитьОписаниеПодписки(Выборка); КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьПодписки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодпискиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Строка = Подписки.НайтиПоИдентификатору(ВыбраннаяСтрока);
	СобытияКлиент.ОткрытьФормуПодписки(Строка.Событие, Строка.Ссылка,, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьНастройкиОповещения" Тогда
		ОбновитьПодписки(); КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодпискиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ОткрытьФорму("РегистрСведений.ПодпискиНаОповещение.ФормаЗаписи",, ЭтаФорма,);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьАктивность()
	
	ПолеФормы 	= Тип("ПолеФормы");
	Для Каждого Элемент Из Элементы Цикл
		Если ТипЗнч(Элемент) = ПолеФормы И Элемент.Имя <> "Активно" Тогда
			Элемент.ТолькоПросмотр = Не Запись.Активно КонецЕсли; КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьРеквизиты()
	
	Если Запись.Событие = Справочники.События.НазначенОсновнойМенеджер Тогда
		
		// Настройка при назначении основного менеджера  1 1 36 36 ....
		//
		//	1 символ - оповещать если основной менеджер текущий пользователь
		//	2 символ - оповещать о снятии меня с основного менеджера
		//	с 3 символа гуиды по 36 символов, ссылки на пользоватей на которых подписан
		
		НазначилиОсновнымМенеджером = Лев(Запись.Настройки, 1) = "1";
		СнялиОсновногоМенеджера = Сред(Запись.Настройки, 2, 1) = "1";
		
		СписокПользователей.ЗагрузитьЗначения(События.ПолучитьПользователейИзСтроки(Запись.Настройки));
		
	КонецЕсли;
	
КонецПроцедуры
&НаСервере
Процедура ВидимостьЭлементовСогласноСобытию()
	
	Если Запись.Событие = Справочники.События.НазначенОсновнойМенеджер Тогда
		
		Элементы.ГруппаНазнОсновногоМенеджера.Видимость = Истина;
		Элементы.ГруппаСсылка.Видимость 	= Ложь;
		Элементы.ГруппаНастройки.Видимость 	= Ложь;
		Элементы.ГруппаМесто.Видимость 		= Ложь;
		
		Элементы.ДекорацияОповещатьОСменеосновногоВсегда.Видимость = Не НазначилиОсновнымМенеджером И Не СнялиОсновногоМенеджера И Не СписокПользователей.Количество();
		
	Иначе
		
		Элементы.ГруппаНазнОсновногоМенеджера.Видимость = Ложь;
		Элементы.ГруппаСсылка.Видимость 	= Истина;
		Элементы.ГруппаНастройки.Видимость 	= Истина;
		Элементы.ГруппаМесто.Видимость 		= Истина; КонецЕсли;
	
КонецПроцедуры
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Событие.Пустая() Тогда Запись.Событие = Параметры.Событие КонецЕсли;
	Если Не Параметры.Пользователь.Пустая() Тогда 	
		Запись.Подписчик 	= Параметры.Пользователь; 
		Если Запись.ДатаПодписки = '00010101' Тогда
			Запись.ДатаПодписки = ТекущаяДата() КонецЕсли; КонецЕсли;
	
	ОбновитьАктивность();
	
	// Прочитаем куда сообщать
	
	Длина = СтрДлина(Запись.Способы);
	Для Ном = 1 По Длина Цикл
		Если Сред(Запись.Способы, Ном, 1) = "1" Тогда
			
			ЭтотОбъект["Оповещать" + Справочники.СпособыСообщений.НайтиПоКоду(Ном).ИмяПредопределенныхДанных] = Истина; КонецЕсли; КонецЦикла;
	
	// Сформируем форму согласно событию
	
	ПрочитатьРеквизиты();
	ВидимостьЭлементовСогласноСобытию();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Сохраним куда сообщать
	
	ПолеФормы 	= Тип("ПолеФормы");
	Способы 	= "";
	Для Каждого Элемент Из Элементы Цикл
		
		Если ТипЗнч(Элемент) = ПолеФормы И СтрНачинаетсяС(Элемент.Имя, "Оповещать") Тогда
			Способы = События.стрСпособыИзСпособа(Справочники.СпособыСообщений[Сред(Элемент.Имя, 10)], Способы, ЭтотОбъект[Элемент.Имя]); КонецЕсли; КонецЦикла;
	
	ТекущийОбъект.Способы = Способы;
	
КонецПроцедуры

&НаКлиенте
Процедура АктивноПриИзменении(Элемент)
	
	ОбновитьАктивность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ОбновитьНастройкиОповещения");
	
КонецПроцедуры

&НаСервере
Функция УПодписчикаЗаполнено(Реквизит)
	
	Возврат ЗначениеЗаполнено(Запись.Подписчик[Реквизит]);
	
КонецФункции
&НаКлиенте
Процедура ОповещатьTgrПриИзменении(Элемент)
	
	Если ОповещатьTgr И Не УПодписчикаЗаполнено("idДиалогаТелеграм") Тогда
		ПоказатьПредупреждение(,"Диалог с ботом не заполнен. Обратитесь в отдел ИТ для установки связи с ботом.",,"Не достаточно данных");
		ОповещатьTgr = Ложь; КонецЕсли;
	
КонецПроцедуры
&НаКлиенте
Процедура ОповещатьMailПриИзменении(Элемент)
	
	Если ОповещатьMail И Не УПодписчикаЗаполнено("Почта") Тогда
		ПоказатьПредупреждение(,"Почта не указана. Обратитесь в отдел ИТ для добавления почты.",,"Не достаточно данных");
		ОповещатьMail = Ложь; КонецЕсли;
	
КонецПроцедуры

#Область Смена_основного_менеджера

&НаКлиенте
Процедура ЕслиМеняСнялиОсновногоМенеджераПриИзменении(Элемент)
	
	Запись.Настройки = ?(ПустаяСтрока(Запись.Настройки), "0", Лев(Запись.Настройки, 1)) + Строка(Число(СнялиОсновногоМенеджера)) + Сред(Запись.Настройки, 3);
	Если Запись.Настройки = "00" Тогда Запись.Настройки = "" КонецЕсли;
	ВидимостьЭлементовСогласноСобытию();
	
КонецПроцедуры
&НаКлиенте
Процедура ЕслиМеняНазначилиОсновнымМенеджеромПриИзменении(Элемент)
	
	Запись.Настройки = Строка(Число(НазначилиОсновнымМенеджером)) + ?(ПустаяСтрока(Запись.Настройки), "0", Сред(Запись.Настройки, 2));
	Если Запись.Настройки = "00" Тогда Запись.Настройки = "" КонецЕсли;
	ВидимостьЭлементовСогласноСобытию();
	
КонецПроцедуры
&НаСервере
Процедура ОбновитьпараметрыСменысновногоменеджера()
	
	стр = ?(ПустаяСтрока(Запись.Настройки), "00", Лев(Запись.Настройки, 2));
	
	Для Каждого Элемент Из СписокПользователей Цикл стр = стр + XMLСтрока(Элемент.Значение) КонецЦикла;
	Запись.Настройки = стр;
	
	ВидимостьЭлементовСогласноСобытию();
	
КонецПроцедуры
&НаКлиенте
Процедура СписокПользователейПриИзменении(Элемент)
	
	ОбновитьпараметрыСменысновногоменеджера();
		
КонецПроцедуры

&НаКлиенте
Процедура СобытиеПриИзменении(Элемент)
	
	ВидимостьЭлементовСогласноСобытию();
	
КонецПроцедуры

#КонецОбласти
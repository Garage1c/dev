Процедура УстановитьЗначение(ИмяФормы,Счетчик,ПредСчетчик,Всего,Состояние) Экспорт
	
	Прогресс=?(Всего=0,0,Окр(100*Счетчик/Всего,0));
	ПредПрогресс=?(Всего=0,0,Окр(100*ПредСчетчик/Всего,0));
	Если Прогресс<>ПредПрогресс Тогда
		
		//затратная операция - без необходимости не делаем
		Мен=РегистрыСведений.ПрогрессБар.СоздатьМенеджерЗаписи();
		Мен.Пользователь=ПараметрыСеанса.ТекущийПользователь;
		Мен.ИмяФормы=ИмяФормы;
		Мен.Прогресс=Прогресс;
		Мен.Состояние=Состояние;
		Мен.Записать(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьЗначение(ИмяФормы) Экспорт
	Запрос = Новый Запрос("Выбрать Прогресс,Состояние из РегистрСведений.ПрогрессБар ГДЕ Пользователь=&Пользователь и ИмяФормы=&ИмяФормы");
	Запрос.УстановитьПараметр("Пользователь",ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("ИмяФормы",ИмяФормы);
	ТЗ=Запрос.Выполнить().Выгрузить();
	Если ТЗ.Количество()>0 Тогда
		Стр=ТЗ[0];
		Струк=Новый Структура("Прогресс,Состояние",Стр.Прогресс,Стр.Состояние);
	Иначе
		Струк=Новый Структура("Прогресс,Состояние",0,"");
	КонецЕсли;	
	
	Возврат Струк;
	
КонецФункции	

Процедура СброситьСчетчик(ИмяФормы) Экспорт
	
	Мен=РегистрыСведений.ПрогрессБар.СоздатьНаборЗаписей();
	Мен.Отбор.Пользователь.Установить(ПараметрыСеанса.ТекущийПользователь);
	Мен.Отбор.ИмяФормы.Установить(ИмяФормы);
	Мен.Записать();
		
КонецПроцедуры

Процедура УстановитьПризнакЗавершения(ИмяФормы,Комментарий="") Экспорт
	
		Мен=РегистрыСведений.ПрогрессБар.СоздатьМенеджерЗаписи();
		Мен.Пользователь=ПараметрыСеанса.ТекущийПользователь;
		Мен.ИмяФормы=ИмяФормы;
		Мен.Прогресс=100;
		Мен.Состояние="Выполнено. "+Комментарий;
		Мен.Записать(Истина);
		
КонецПроцедуры




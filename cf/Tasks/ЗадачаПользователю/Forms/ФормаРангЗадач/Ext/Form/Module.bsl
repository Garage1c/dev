
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Пользователь = ПараметрыСеанса.ТекущийПользователь; 
	Список.Параметры.УстановитьЗначениеПараметра("ТекущаяДата", ТекущаяДата());
	
	ПерезаполнитьНастройкиПользователя();
	ОбновитьСписокЗадач();
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьНастройкиПользователя()
	
	ВыбранныеСклады.ЗагрузитьЗначения(ПолучитьСкладыПользователя(Пользователь));
	ВыбранныеТочкиМаршрута.ЗагрузитьЗначения(ПолучитьТочкиМаршрутаВыбранныхРолей());
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеСкладыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("ОбщаяФорма.ОтметитьСписок", 
		Новый Структура("Список", ПолучитьСписокСУстановленнымиОтметками(ВыбранныеСклады, ПолучитьСкладыПользователя(Пользователь))), 
		ЭтаФорма,,,, Новый ОписаниеОповещения("ОтмеченСписок", ЭтаФорма, Новый Структура("Список", "ВыбранныеСклады")));

КонецПроцедуры
	
&НаКлиенте
Функция ПолучитьСписокСУстановленнымиОтметками(Список, МассивЗначений)
	
	// Возвращает список с отмеченными элементами
	// Создается новый список и заполняет его МассивЗначений
	// дальше выставляется галки тем элементам которые есть в списке и массиве или только в массиве
	
	новСписок = Новый СписокЗначений;
	Для Каждого Значение Из МассивЗначений Цикл новСписок.Добавить(Значение,,Список.НайтиПоЗначению(Значение) <> Неопределено) КонецЦикла;
	
	Возврат новСписок;
	
КонецФункции

&НаСервере
Функция ПолучитьСкладыПользователя(выбПользователь) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ Склад ИЗ РегистрСведений.СкладыПользователя ГДЕ Пользователь = &ТекПользователь");	
	Запрос.УстановитьПараметр("ТекПользователь", выбПользователь);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Склад");
	
КонецФункции

&НаКлиенте
Процедура ОтмеченСписок(Список, ДопПараметры) Экспорт
	
	Если Список <> Неопределено Тогда
		
		текСписок = ЭтотОбъект[ДопПараметры.Список];
		текСписок.Очистить();
		Для Каждого Элемент Из Список Цикл Если Элемент.Пометка Тогда текСписок.Добавить(Элемент.Значение) КонецЕсли; КонецЦикла; 
		
		ОбновитьСписокЗадач(); КонецЕсли;
	
КонецПроцедуры
	
&НаСервере
Процедура ОбновитьСписокЗадач()
	
	текПользователи = Новый Массив;
	текПользователи.Добавить(Справочники.Пользователи.ПустаяСсылка());
	текПользователи.Добавить(Пользователь);
	
	текСклады = ВыбранныеСклады.ВыгрузитьЗначения();
	текСклады.Добавить(Справочники.Склады.ПустаяСсылка());
	
	Список.Параметры.УстановитьЗначениеПараметра("Склады", 				текСклады);
		
	Список.Параметры.УстановитьЗначениеПараметра("ТочкиМаршрута", 		ВыбранныеТочкиМаршрута.ВыгрузитьЗначения());
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТочкиМаршрутаВыбранныхРолей() Экспорт
	
	Точки = Новый Массив;
	
	Точки.Добавить(БизнесПроцессы.СборкаТовара.ТочкиМаршрута.СобратьТовар);
	Точки.Добавить(БизнесПроцессы.ПеремещениеТоваров.ТочкиМаршрута.ОтправитьТовар);
	
	Возврат Точки;
	
КонецФункции

&НаКлиенте
Процедура ВыбранныеТочкиМаршрутаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("ОбщаяФорма.ОтметитьСписок", 
	Новый Структура("Список", ПолучитьСписокСУстановленнымиОтметками(ВыбранныеТочкиМаршрута, ПолучитьТочкиМаршрутаВыбранныхРолей())), 
	ЭтаФорма,,,, Новый ОписаниеОповещения("ОтмеченСписок", ЭтаФорма, Новый Структура("Список", "ВыбранныеТочкиМаршрута")));
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Парам = Новый Структура("Ключ", Элемент.ТекущиеДанные.Ссылка); 
	ОткрытьФорму("Задача.ЗадачаПользователю.Форма.ФормаЗадачи", Парам);
КонецПроцедуры

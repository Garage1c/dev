
&НаСервере
Функция ПроверитьИПолучитьТовар(Ссылка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Позиция 	= ПараметрыСеанса.ТекущаяПозицияОбласти;
	Символ 		= МодульОбластей.СимволДоступ();
	текОбласть 	= Ссылка.НастройкаОбластей;
	
	Если Сред(текОбласть, Позиция, 1) <> Символ Тогда
		
		Объект = Ссылка.ПолучитьОбъект();
		Объект.НастройкаОбластей = Лев(текОбласть, Позиция - 1) + Символ + Сред(текОбласть, Позиция + 1); 
		
		Если Не ОбщиеФункции.ЗаписатьОбъектИСообщитьЕслиОшибка(Объект) Тогда
			УстановитьПривилегированныйРежим(Ложь);
			Возврат Ложь; КонецЕсли; КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ПолучитьСписокТоваров(СсылкиНаДокументы)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Определим виды документов, и распределим по запросам в зависимости от вида документа
	
	Структура = Новый Структура;
	
	Для Каждого Ссылка Из СсылкиНаДокументы Цикл
		
		ИмяДока 	= Ссылка.Метаданные().Имя;
		Параметры 	= Неопределено;
		
		Если Структура.Свойство(ИмяДока, Параметры) Тогда
			
			Ссылки 			= Параметры.Ссылки;
			ТекстЗапроса 	= Параметры.ТекстЗапроса;
			
		Иначе
			
			Ссылки 			= Новый Массив;
			ТекстЗапроса 	= Ссылка.ПолучитьОбъект().ПолучитьТекстЗапросаПолученияСпискаТоваров(); КонецЕсли;
		
		Ссылки.Добавить(Ссылка);
		Структура.Вставить(ИмяДока, Новый Структура("ТекстЗапроса, Ссылки", ТекстЗапроса, Ссылки)); КонецЦикла;
	
	// Состряпаем запрос
	
	Запрос = Новый Запрос;
	Текст = ""; Инд = -1;
	Для КАждого Элемент Из Структура Цикл Инд = Инд + 1;
		
		ИмяПараметра = "Документы" + Формат(Инд, "ЧГ=");
		
		Текст = Текст + ?(Текст = "","","
		|ОБЪЕДИНИТЬ ВСЕ
		|") + СтрЗаменить(Элемент.Значение.ТекстЗапроса, "Ссылка = &ДокументСсылка", "Ссылка В(&" + ИмяПараметра + ")");
		
		Запрос.УстановитьПараметр(ИмяПараметра, Элемент.Значение.Ссылки); КонецЦикла;
	
	Запрос.Текст = Текст;
	
	// Вернем результат
	
	ПолученныеСсылки = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Номенклатура");
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ПолученныеСсылки;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	// Получим список товаров
	
	Ссылки = ПолучитьСписокТоваров(ПараметрКоманды);
	
	Всего 	= Ссылки.Количество();
	Ном 	= 0;
	
	Для Каждого Ссылка Из Ссылки Цикл Ном = Ном + 1;
		
		Если Не ПроверитьИПолучитьТовар(Ссылка) Тогда
			ПоказатьПредупреждение(,"Произошла ошибка"); 
			Возврат; КонецЕсли;
		
		Состояние(НСтр("ru = 'Поиск ""Объект не найден""'; de = 'Поиск ""Объект не найден""' "), Ном / Всего * 100, Строка(Ссылка), БиблиотекаКартинок.Найти); КонецЦикла;
	
	ПоказатьОповещениеПользователя("Товары просканированы",,"В количестве: " + Всего, БиблиотекаКартинок.Найти);
		
КонецПроцедуры

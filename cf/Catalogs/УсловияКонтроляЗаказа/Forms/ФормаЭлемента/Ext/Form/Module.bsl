&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	спрУсловияКонтроляЗаказаОбъект = РеквизитФормыВЗначение("Объект");
	СКД = спрУсловияКонтроляЗаказаОбъект.ПолучитьМакет("Макет");
	АдресСКДВоВременномХранилище = ПоместитьВоВременноеХранилище(СКД, УникальныйИдентификатор);
	
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСКДВоВременномХранилище);
	КомпоновщикНастроек.Инициализировать(ИсточникНастроек);
	
	НастройкиКомпоновщика = спрУсловияКонтроляЗаказаОбъект.НастройкиКомпоновщика.Получить();
	Если НЕ ЗначениеЗаполнено(НастройкиКомпоновщика) Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(СКД.НастройкиПоУмолчанию);
	Иначе
		КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиКомпоновщика);
		КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.НастройкиКомпоновщика = Новый ХранилищеЗначения(КомпоновщикНастроек.Настройки);
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьНастройкиПоУмолчанию(Команда)
	ВосстановитьНастройкиПоУмолчаниюНаСервере();
КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройкиПоУмолчаниюНаСервере()
	спрУсловияКонтроляЗаказаОбъект = РеквизитФормыВЗначение("Объект");
	СКД = спрУсловияКонтроляЗаказаОбъект.ПолучитьМакет("Макет");
	КомпоновщикНастроек.ЗагрузитьНастройки(СКД.НастройкиПоУмолчанию);
КонецПроцедуры


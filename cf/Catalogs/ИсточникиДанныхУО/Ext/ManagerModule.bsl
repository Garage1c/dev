
Функция ПолучитьРезультат(Ссылка, ДатаНач = Неопределено, ДатаКон = Неопределено) Экспорт
	
	Если Ссылка.ЭтоГруппа Тогда
		
		// Получим сумму всех элементов
		
		Результат = 0;
		
		Запрос = Новый Запрос("ВЫБРАТЬ Ссылка ИЗ Справочник.ИсточникиДанныхУО ГДЕ Родитель = &Ссылка");
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат = Результат + Справочники.ИсточникиДанныхУО.ПолучитьРезультат(Выборка.Ссылка, ДатаНач, ДатаКон); КонецЦикла;
		
		Возврат Результат;
		
	Иначе
	
		Если ПустаяСтрока(Ссылка.ТекстЗапроса) Тогда
			ОбщиеФункции.СообщитьТекст(строка(Ссылка) + " не заполнен запрос!");
			ВОзврат Неопределено; КонецЕсли;
		
		// Установим пользовательские параметры запроса
		
		Запрос = Новый Запрос(Ссылка.ТекстЗапроса);
		УстановленныеПараметры = Новый Соответствие;
		
		Для Каждого Строка Из Ссылка.ПараметрыЗапроса Цикл
			Если УстановленныеПараметры[Строка.Имя] <> Истина Тогда
			
				Строки = Ссылка.ПараметрыЗапроса.НайтиСтроки(Новый Структура("Имя", Строка.Имя));
				Если Строки.Количество() > 1 Тогда 	// Это список параметров
					Список = Новый СписокЗначений;
					Для КАждого СтрокаСписка Из Строки Цикл Список.Добавить(СтрокаСписка.Значение) КонецЦикла; 
					Запрос.УстановитьПараметр(Строка.Имя, Список); 
				Иначе									// Это обычный параметр
					Запрос.УстановитьПараметр(Строка.Имя, Строка.Значение) КонецЕсли; КонецЕсли; КонецЦикла;
		
		// Установим предопределенные параметры запроса
		
		Запрос.УстановитьПараметр("ДатаНачала", 	ДатаНач);
		Запрос.УстановитьПараметр("ДатаОкончания", 	?(ЗначениеЗаполнено(ДатаКон), ДатаКон, ТекущаяДата()));
		
		// Выполним
		
		Попытка
			Таблица = Запрос.Выполнить().Выгрузить();
		Исключение
			стрОшибки = ОписаниеОшибки();
			ОбщиеФункции.СообщитьТекст(Строка(Ссылка) + " ошибка выполнения запроса
														|" + стрОшибки); 
			Возврат Неопределено; КонецПопытки;
		
		Если Таблица.Колонки.Найти("Результат") = Неопределено Тогда
			ОбщиеФункции.СообщитьТекст(Строка(Ссылка) + " в запросе нет результирующей колонки ""Результат""");
			Возврат Неопределено КонецЕсли;
			
		Возврат Таблица.Итог("Результат"); КонецЕсли;
	
КонецФункции
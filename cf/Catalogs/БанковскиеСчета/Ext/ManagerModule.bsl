// Функция определяет банковский счет выбранного контрагента или физического лица.
//
// Возвращает банковский счет получателя, если найден один банковский счет.
// Возвращает Неопределено, если банковский счет не найден или счетов больше одного
//
// Параметры:
//  Получатель - СправочникСсылка.Контрагенты или СправочникСсылка.ФизическиеЛица - Ссылка на получателя
//	Валюта - СправочникСсылка.Валюты - Валюта банковского счета
//
// Возвращаемое значение:
//	СправочникСсылка.БанковскиеСчетаКонтрагентов - Найденный банковский счет получателя
//
Функция ПолучитьБанковскийСчетПоУмолчанию(Получатель, Валюта = Неопределено) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Ссылка КАК БанковскийСчетПолучателя
	|ИЗ
	|	Справочник.БанковскиеСчета
	|ГДЕ
	|	Не ПометкаУдаления
	|	И Владелец = &Получатель
	|	И (ВалютаДенежныхСредств = &Валюта
	|		ИЛИ &Валюта = Неопределено)
	|УПОРЯДОЧИТЬ ПО ЗначениеПоУмолчанию Убыв
	|");
	
	Запрос.УстановитьПараметр("Получатель", Получатель);
	Запрос.УстановитьПараметр("Валюта", ?(ЗначениеЗаполнено(Валюта), Валюта, Неопределено));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 1 
	   И Выборка.Следующий()
	Тогда
		БанковскийСчетПолучателя = Выборка.БанковскийСчетПолучателя;
	Иначе
		БанковскийСчетПолучателя = Справочники.БанковскиеСчета.ПустаяСсылка();
	КонецЕсли;
	
	Возврат БанковскийСчетПолучателя;

КонецФункции // ПолучитьБанковскийСчетПоУмолчанию()

// Функция получает реквизиты банковского счета.
//
// Параметры:
//  БанковскийСчет - СправочникСсылка.БанковскиеСчета - Ссылка на банковский счет
//
// Возвращаемое значение:
//	Структура - реквизиты банковского счета
//
Функция ПолучитьРеквизитыБанковскогоСчета(БанковскийСчет) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	БанковскиеСчетаКонтрагентов.ВалютаДенежныхСредств КАК Валюта,
	|	БанковскиеСчетаКонтрагентов.ТекстНазначения КАК ТекстНазначения,
	|
	|	БанковскиеСчетаКонтрагентов.НомерСчета КАК НомерСчета,
	|	БанковскиеСчетаКонтрагентов.Банк КАК Банк,
	|	БанковскиеСчетаКонтрагентов.Банк.Код КАК БИК,
	|	БанковскиеСчетаКонтрагентов.Банк.КоррСчет КАК КоррСчет
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчетаКонтрагентов
	|ГДЕ
	|	БанковскиеСчетаКонтрагентов.Ссылка = &БанковскийСчет
	|");
	
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Валюта = Выборка.Валюта;
		ТекстНазначения = Выборка.ТекстНазначения;
		НомерСчета = Выборка.НомерСчета;
		Банк = Выборка.Банк;
		БИК = Выборка.БИК;
		КоррСчет = Выборка.КоррСчет;
	Иначе
		Валюта = Справочники.Валюты.ПустаяСсылка();
		ТекстНазначения = "";
		НомерСчета = "";
		Банк = Неопределено;
		БИК = "";
		КоррСчет = "";
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура("Валюта, ТекстНазначения, НомерСчета, Банк, БИК, КоррСчет",
		Валюта,
		ТекстНазначения,
		НомерСчета,
		Банк,
		БИК,
		КоррСчет
	);
	
	Возврат СтруктураРеквизитов;

КонецФункции // ПолучитьРеквизитыБанковскогоСчета()
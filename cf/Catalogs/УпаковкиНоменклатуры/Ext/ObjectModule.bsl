
Процедура ПередЗаписью(Отказ)
	
	// нельзя менять коэффициент когда вздумается, так что...
	
	Если НЕ ЭтотОбъект.Ссылка.Пустая() И ЭтотОбъект.Коэффициент <> ЭтотОбъект.Ссылка.Коэффициент Тогда		
	
		// найдем документы, изменение значения упаковки в которых приведет к изменениям в регистрах, учитывающих Количество товара вне разреза Упаковки
		
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТИПЗНАЧЕНИЯ(Регистратор) КАК ТипРегистратора
			|ИЗ РегистрНакопления.Продажи
			|
			|ОБЪЕДИНИТЬ
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТИПЗНАЧЕНИЯ(Регистратор) КАК ТипРегистратора
			|ИЗ РегистрНакопления.РазмещениеЗаказов
			|
			|ОБЪЕДИНИТЬ
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТИПЗНАЧЕНИЯ(Регистратор) КАК ТипРегистратора
			|ИЗ РегистрНакопления.ТоварыВРезерве
			|
			|ОБЪЕДИНИТЬ
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТИПЗНАЧЕНИЯ(Регистратор) КАК ТипРегистратора
			|ИЗ РегистрНакопления.ТоварыВЯчейках
			|
			|ОБЪЕДИНИТЬ
	        |ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТИПЗНАЧЕНИЯ(Регистратор) КАК ТипРегистратора
			|ИЗ РегистрНакопления.ТоварыНаСкладах");
			
		МассивНужных = Новый Массив;	
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Документ = Метаданные.НайтиПоТипу(Выборка.ТипРегистратора);
			Если Документ <> Неопределено Тогда
				Если Документ.ТабличныеЧасти.Найти("Товары") <> Неопределено И Документ.ТабличныеЧасти.Товары.Реквизиты.Найти("Упаковка") <> Неопределено Тогда
					МассивНужных.Добавить(Документ.Имя);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		ТекстЗапроса = "";
		
		Для Каждого Элемент ИЗ МассивНужных Цикл
			ТекстЗапроса = ТекстЗапроса + ?(ПустаяСтрока(ТекстЗапроса), "", " ОБЪЕДИНИТЬ " + Символы.ПС);	
		    ТекстЗапроса = ТекстЗапроса + "ВЫБРАТЬ ПЕРВЫЕ 1 ИСТИНА ИЗ Документ." + Элемент + ".Товары ГДЕ Упаковка = &Упаковка";
		КонецЦикла;
		
		// найдем среди нужных объектов, те объекты где есть ссылка на данную упаковку
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Упаковка", Ссылка);
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			
			Отказ = Истина;

			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Изменение коэффициента упаковки невозможно, т.к. в информационной базе на данный объект ссылаются другие объекты";
			Сообщение.Поле = "Коэффициент";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
				
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


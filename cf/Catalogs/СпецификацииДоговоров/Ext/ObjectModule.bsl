
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если НЕ ЭтоГруппа Тогда
		
		ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);

		НаименованиеСпецификации = "Спецификация";
		
		Если ТипДанныхЗаполнения = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
			
			Поставщик = ДанныеЗаполнения.Владелец;
			
			Если ДанныеЗаполнения.Владелец.СтраницыСайта.Количество() > 0 Тогда
				Бренд = ДанныеЗаполнения.Владелец.СтраницыСайта[0].Бренд;
			КонецЕсли;
		
			Договор = ДанныеЗаполнения;

			НаименованиеСпецификации = НаименованиеСпецификации 
										+ " " 
										+ ДанныеЗаполнения.Владелец.Наименование
										+ " по договору №"
										+ ДанныеЗаполнения.Наименование;
			
			
		КонецЕсли;
		
		ДатаСоздания = ТекущаяДата();
		Автор = ПараметрыСеанса.ТекущийПользователь;
		СтатусСпецификации = Перечисления.СтатусыСпецификаций.Создана;
		Наименование = НаименованиеСпецификации;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ПометкаУдаления Тогда 
		СтатусСпецификации = Перечисления.СтатусыСпецификаций.Закрыта;
	КонецЕсли;
	
	ТаблицаНоменклатуры = Состав.Выгрузить(, "Номенклатура");
	ТаблицаНоменклатуры.Колонки.Добавить("КоличествоПозиций");
	
	Для Каждого ТекСтрока Из ТаблицаНоменклатуры Цикл 
		ТекСтрока.КоличествоПозиций = 1;	
	КонецЦикла;	
	
	ТаблицаНоменклатуры.Свернуть("Номенклатура", "КоличествоПозиций");
	
	Для Каждого ТекСтрока Из ТаблицаНоменклатуры Цикл 
		Если ТекСтрока.КоличествоПозиций > 1 Тогда
			Сообщить("Номенклатура " + Строка(ТекСтрока.Номенклатура) + " указана более одного раза!");
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЭтоНовый() И 
			(Поставщик 	<> Ссылка.Поставщик               
				Или Договор 	<> Ссылка.Договор) Тогда 
			
		ДополнительныеСвойства.Вставить("ЗаписатьСтатус", Истина);	
		
	КонецЕсли;
	
	Если СтатусСпецификации <> Ссылка.СтатусСпецификации Или ПометкаУдаления Тогда 
		ДополнительныеСвойства.Вставить("СменаСтатуса", Истина);
		Если Ссылка.СтатусСпецификации = Перечисления.СтатусыСпецификаций.Активна Или ПометкаУдаления Тогда 
			ДополнительныеСвойства.Вставить("СделатьНеАктивной", Истина);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	 СтатусСпецификации = Перечисления.СтатусыСпецификаций.Создана;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	ПеренестиСтопЗаказыВНоменклатуру();
	
КонецПроцедуры

Процедура ПеренестиСтопЗаказыВНоменклатуру()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СпецификацииДоговоровСостав.Номенклатура,
	|	СпецификацииДоговоровСостав.СтопЗаказ
	|ИЗ
	|	Справочник.СпецификацииДоговоров.Состав КАК СпецификацииДоговоровСостав
	|ГДЕ
	|	СпецификацииДоговоровСостав.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		
		Если Выборка.Номенклатура.БольшеНеПоставляется <> Выборка.СтопЗаказ Тогда
			НоменклатураОбъект = Выборка.Номенклатура.ПолучитьОбъект();
			НоменклатураОбъект.БольшеНеПоставляется = Выборка.СтопЗаказ;
			НоменклатураОбъект.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#Область Метаданные

&НаСервере
Функция ОбновитьСписок()
	
	Попытка
		Объекты.ТекстЗапроса = "ВЫБРАТЬ ССЫЛКА ИЗ " + МетаБъект;
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Ложь; КонецПопытки;
	
	Объекты.ОсновнаяТаблица = МетаБъект;
	
	Возврат Истина;
	
КонецФункции
&НаКлиенте
Процедура МетаБъектПриИзменении(Элемент)
	
	Если ОбновитьСписок() Тогда
		Элементы.Объекты.Обновить(); КонецЕсли;
	
КонецПроцедуры
&НаКлиенте
Процедура ВыборМетаданного(ВыбрЗначение, ДополнительныеПараметры) Экспорт
	
	Если ВыбрЗначение <> Неопределено Тогда
		
		МетаБъект = ВыбрЗначение;
		МетаБъектПриИзменении(Элементы.МетаБъект); КонецЕсли;
	
КонецПроцедуры
&НаКлиенте
Процедура МетаБъектНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Список = Новый СписокЗначений;
	Список.Добавить("Справочники");
	Список.Добавить("Документы");
	Список.Добавить("ПланыВидовХарактеристик");
	Список.Добавить("БизнесПроцессы");
	Список.Добавить("Задачи");
	
	ОткрытьФорму("ОбщаяФорма.обм_ВыборОбъектовМетаданных", Новый Структура("ВыбранныеМетаданные", Список), ЭтаФорма,,,,Новый ОписаниеОповещения("ВыборМетаданного", ЭтаФорма));
	
КонецПроцедуры
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ПустаяСтрока(МетаБъект) Тогда
		МетаБъектПриИзменении(Элементы.МетаБъект); КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ВыгрузитьВыделенные(Команда)
	
	ВыдСсылки = Элементы.Объекты.ВыделенныеСтроки;
	Если ВыдСсылки.Количество() Тогда
		обм_ОбменКлиент.ОбработкаВыбораБазыПриемника(ВыдСсылки, ОбменДанными); 
	Иначе
		ПоказатьПредупреждение(,"Нужно отметить объекты для выгрузки",,"Нет данных"); КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПоМетаданным(Команда)
	
	ОткрытьФорму("Справочник.обм_ОбменДанными.Форма.НастройкаПоМетаданным",,ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Функция ВыгрузитьВсеНаСервере(КолСтрок = 0)
	
	Схема = Элементы.Объекты.ПолучитьИсполняемуюСхемуКомпоновкиДанных();
	Настройки = Элементы.Объекты.ПолучитьИсполняемыеНастройкиКомпоновкиДанных();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	МакетКомпоновки = КомпоновщикМакета.Выполнить(Схема, Настройки, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
 
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	ТЗ = Новый ТаблицаЗначений;
	ПроцессорВывода.УстановитьОбъект(ТЗ); 
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	КолСтрок = ТЗ.Количество();
	
	Возврат обм_Обмен.Выгрузить(ТЗ.ВыгрузитьКолонку("Ссылка"), ОбменДанными, Новый Соответствие);
	
КонецФункции
&НаКлиенте
Процедура ВыгрузитьВсеОтвет(Ответ, ДопПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
	
		КолСтрок = 0;
		Если ВыгрузитьВсеНаСервере(КолСтрок) Тогда ПоказатьПредупреждение(,"Выгружено - " + КолСтрок,,"Окей") КонецЕсли; КонецЕсли;
	
КонецПроцедуры
&НаКлиенте
Процедура ВыгрузитьВсе(Команда)
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ВыгрузитьВсеОтвет", ЭтаФорма), "Выгрузить весь список?" , РежимДиалогаВопрос.ДаНет)
	
КонецПроцедуры

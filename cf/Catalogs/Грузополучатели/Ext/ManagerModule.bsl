
 // Функция определяет значения реквизитов выбранного контрагента.
//
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты - Ссылка на контрагента
//
// Возвращаемое значение:
//	Структура - реквизиты выбранного контрагента
//
Функция ПолучитьРеквизитыГрузополучателя(Грузополучатель) Экспорт
	Если ЗначениеЗаполнено(Грузополучатель.Владелец) Тогда
		Представление	= ?(Не ПустаяСтрока(Грузополучатель.НаименованиеПолное), Грузополучатель.НаименованиеПолное, Грузополучатель.Наименование);
		Наименование	= Представление;
		ИНН				= Грузополучатель.Владелец.ИНН;
		КПП				= ?(НЕ ПустаяСтрока(Грузополучатель.КПП),Грузополучатель.КПП,Грузополучатель.Владелец.КПП);
		КодПоОКПО		= Грузополучатель.Владелец.КодПоОКПО;
		ЮрФизЛицо		= ?(ТипЗнч(Грузополучатель.Владелец)=Тип("СправочникСсылка.Контрагенты"),Грузополучатель.Владелец.ЮрФизЛицо,Перечисления.ЮрФизЛицо.ПустаяСсылка());
		Если ТипЗнч(Грузополучатель.Владелец)=Тип("СправочникСсылка.Организации") Тогда
			КодПоОКАТО = Грузополучатель.Владелец.КодПоОКАТО;
			Если ЗначениеЗаполнено(Грузополучатель.Владелец.СвидетельствоСерияНомер) И ЗначениеЗаполнено(Грузополучатель.Владелец.СвидетельствоДатаВыдачи) Тогда
				Свидетельство = "Свидетельство " + Грузополучатель.Владелец.СвидетельствоСерияНомер + " от " + Формат(Грузополучатель.Владелец.СвидетельствоДатаВыдачи, "ДФ=dd.MM.yyyy");
			Иначе
				Свидетельство = "";
			КонецЕсли;
		Иначе
			КодПоОКАТО = "";
			Свидетельство = "";
		КонецЕсли;
	Иначе
		Представление	= "";
		Наименование	= "";
		ИНН				= "";
		КПП				= "";
		КодПоОКПО		= "";
		ЮрФизЛицо		= Перечисления.ЮрФизЛицо.ПустаяСсылка();
		КодПоОКАТО 		= "";
		Свидетельство 	= "";
	КонецЕсли;
	
	Возврат Новый Структура("Представление, НаименованиеПолное, ИНН, КПП, КодПоОКПО, ЮрФизЛицо, КодПоОКАТО, Свидетельство",
		Представление,
		Наименование,
		ИНН,
		КПП,
		КодПоОКПО,
		ЮрФизЛицо,
		КодПоОКАТО,
		Свидетельство
	);
КонецФункции // ПолучитьРеквизитыКонтрагента()

//Переносим контактную информацию из владельца в грузополучателя
//Переносятся следующие сведения: Фактический адрес, Юридический адрес, Телефон
Процедура СоздатьГрузополучателяПоВладельцу(НовыйВладелец,БылВладелец) Экспорт
	НачатьТранзакцию();
	Попытка
		соВидыКИ = Новый Соответствие;
		Если НЕ Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию("Фактический адрес грузополучателя").Пустая() Тогда
			соВидыКИ.Вставить(Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию("Фактический адрес контрагента"),Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию("Фактический адрес грузополучателя"));
			соВидыКИ.Вставить(Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию("Фактический адрес организации"),Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию("Фактический адрес грузополучателя"));
		КонецЕсли;
		Если НЕ Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию("Юридический адрес грузополучателя").Пустая() Тогда
			соВидыКИ.Вставить(Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию("Юридический адрес контрагента"),Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию("Юридический адрес грузополучателя"));
			соВидыКИ.Вставить(Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию("Юридический адрес организации"),Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию("Юридический адрес грузополучателя"));
		КонецЕсли;
		Если НЕ Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию("Телефон грузополучателя").Пустая() Тогда
			соВидыКИ.Вставить(Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию("Телефон контрагента"),Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию("Телефон грузополучателя"));
			соВидыКИ.Вставить(Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию("Телефон организации"),Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию("Телефон грузополучателя"));
		КонецЕсли;
		
		НовГрузополучатель = Справочники.Грузополучатели.СоздатьЭлемент();
		НовГрузополучатель.Наименование = БылВладелец.Наименование;
		НовГрузополучатель.НаименованиеПолное = БылВладелец.НаименованиеПолное;
		НовГрузополучатель.КПП = БылВладелец.КПП;
		НовГрузополучатель.Владелец = НовыйВладелец;
		НовГрузополучатель.Записать();
		
		//Представление контактной информации
		соСтарыйНовый = Новый Соответствие;		
		
		новПредставление = РегистрыСведений.ПредставлениеКонтактнойИнформации.СоздатьНаборЗаписей();
		новПредставление.Отбор.Объект.Установить(НовГрузополучатель.Ссылка);
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Объект",БылВладелец);
		Запрос.Текст = "ВЫБРАТЬ
		               |	ПредставлениеКонтактнойИнформации.Вид,
		               |	ПредставлениеКонтактнойИнформации.Представление,
		               |	ПредставлениеКонтактнойИнформации.ЗначениеПоУмолчанию,
		               |	ПредставлениеКонтактнойИнформации.Комментарий,
		               |	ПредставлениеКонтактнойИнформации.ID
		               |ИЗ
		               |	РегистрСведений.ПредставлениеКонтактнойИнформации КАК ПредставлениеКонтактнойИнформации
		               |ГДЕ
		               |	ПредставлениеКонтактнойИнформации.Объект = &Объект";
		Результат = Запрос.Выполнить().Выбрать();
		Пока Результат.Следующий() Цикл
			видКИ = соВидыКИ.Получить(Результат.Вид);
			Если видКИ = Неопределено Тогда Продолжить; КонецЕсли;
			
			НовЗапись = новПредставление.Добавить();
			НовЗапись.Объект = НовГрузополучатель.Ссылка;
			НовЗапись.Вид = ВидКИ;
			НовЗапись.ID = Новый УникальныйИдентификатор;
			НовЗапись.Представление = Результат.Представление;
			НовЗапись.ЗначениеПоУмолчанию = Результат.ЗначениеПоУмолчанию;
			НовЗапись.Комментарий = Результат.Комментарий;
			
			соСтарыйНовый.Вставить(Результат.ID,НовЗапись.ID);
		КонецЦикла;
		новПредставление.Записать(Истина);
		
		//Контактная информация
		НовРег = РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей();
		НовРег.Отбор.Объект.Установить(НовГрузополучатель.Ссылка);
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Объект",БылВладелец);
		Запрос.Текст = "ВЫБРАТЬ
		               |	КонтактнаяИнформация.Вид КАК Вид,
		               |	КонтактнаяИнформация.Поле,
		               |	КонтактнаяИнформация.Значение,
		               |	КонтактнаяИнформация.ID КАК ID
		               |ИЗ
		               |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		               |ГДЕ
		               |	КонтактнаяИнформация.Объект = &Объект
		               |ИТОГИ
		               |	МАКСИМУМ(Вид)
		               |ПО
		               |	ID";
		Результат = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока Результат.Следующий() Цикл
			видКИ = соВидыКИ.Получить(Результат.Вид);
			Если видКИ = Неопределено Тогда Продолжить; КонецЕсли;
			
			новID = соСтарыйНовый.Получить(Результат.ID);
			Если новID = Неопределено Тогда Продолжить; КонецЕсли;
			
			РезЗначения = Результат.Выбрать();
			Пока РезЗначения.Следующий() Цикл
				НовЗапись = НовРег.Добавить();
				НовЗапись.Объект = НовГрузополучатель.Ссылка;
				НовЗапись.ID = новID;
				НовЗапись.Вид = видКИ;
				НовЗапись.Поле = РезЗначения.Поле;
				НовЗапись.Значение = РезЗначения.Значение;
			КонецЦикла;
		КонецЦикла;
		
		НовРег.Записать(Истина);
		
		//Банковский счёт
		БанковскийСчет = Справочники.БанковскиеСчета.ПолучитьБанковскийСчетПоУмолчанию(БылВладелец);
		Если Не БанковскийСчет.Пустая() Тогда
			НовБанковскийСчет = БанковскийСчет.Скопировать();
			НовБанковскийСчет.Владелец = НовГрузополучатель.Ссылка;
			НовБанковскийСчет.Записать();
		КонецЕсли;
		
		//Менеджер
		НовМенеджер = РегистрыСведений.ОсновнойМенеджерГрузополучателя.СоздатьНаборЗаписей();
		НовМенеджер.Отбор.Грузополучатель.Установить(НовГрузополучатель.Ссылка);
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ОсновнойМенеджерПартнера.Период,
		               |	ОсновнойМенеджерПартнера.Менеджер
		               |ИЗ
		               |	РегистрСведений.ОсновнойМенеджерПартнера КАК ОсновнойМенеджерПартнера
		               |ГДЕ
		               |	ОсновнойМенеджерПартнера.Контрагент = &Контрагент
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ОсновнойМенеджерПартнера.Период,
		               |	ОсновнойМенеджерПартнера.Менеджер";
		Запрос.УстановитьПараметр("Контрагент",БылВладелец);
		Результат = Запрос.Выполнить().Выбрать();
		
		Пока Результат.Следующий() Цикл
			ДобМенеджер = НовМенеджер.Добавить();
			ДобМенеджер.Грузополучатель = НовГрузополучатель.Ссылка;
			ДобМенеджер.Менеджер = Результат.Менеджер;
			ДобМенеджер.Период = Результат.Период;
		КонецЦикла;
		
		Если НовМенеджер.Количество()=0 И ТипЗнч(БылВладелец)=Тип("СправочникСсылка.Контрагенты") И ЗначениеЗаполнено(БылВладелец.ОсновнойМенеджер) Тогда
			ДобМенеджер = НовМенеджер.Добавить();
			ДобМенеджер.Грузополучатель = НовГрузополучатель.Ссылка;
			ДобМенеджер.Менеджер = БылВладелец.ОсновнойМенеджер;
			ДобМенеджер.Период = Дата(2016,1,1);
		КонецЕсли;
		
		НовМенеджер.Записать();
		
		Если НовыйВладелец = БылВладелец Тогда
			НовГруз = РегистрыСведений.ОсновнойГрузополучатель.СоздатьНаборЗаписей();
			НовГруз.Отбор.Контрагент.Установить(НовыйВладелец);
			НовГруз.Прочитать();
			
			Если НовГруз.Количество()=0 Тогда
				ДобГруз = НовГруз.Добавить();
				ДобГруз.Контрагент = НовыйВладелец;
				ДобГруз.Грузополучатель = НовГрузополучатель.Ссылка;
				
				НовГруз.Записать();
			КонецЕсли;
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		Сообщить("Ошибка при создании грузополучателя "+ОписаниеОшибки());
	КонецПопытки;
КонецПроцедуры

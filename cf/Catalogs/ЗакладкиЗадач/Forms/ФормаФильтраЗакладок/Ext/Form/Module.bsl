
&НаСервере
Процедура ОбновитьСписокЗадач()
	
	// Получим закладки
	
	Запрос = Новый Запрос("ВЫБРАТЬ Ссылка ИЗ Справочник.ЗакладкиЗадач ГДЕ Пользователь = &текПользователь");
	Запрос.УстановитьПараметр("текПользователь", Пользователь);
	СписокЗакладок.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	СписокЗакладок.СортироватьПоЗначению();
	СписокЗакладок.Вставить(0, Справочники.ЗакладкиЗадач.НеУказана);
	
	// Отметим закладки
	
	ЗагрНастройки = ХранилищеОбщихНастроек.Загрузить("Фильтр закладок пользователя");
	Если 	ТипЗнч(ЗагрНастройки) = Тип("Структура") И
			ЗагрНастройки.Свойство("ОтмеченныеЗакладки") Тогда
			
		Для Каждого Элемент Из СписокЗакладок Цикл Элемент.Пометка = ЗагрНастройки.ОтмеченныеЗакладки.Найти(Элемент.Значение) <> Неопределено КонецЦикла; КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗапретитьРедактировать 	= Параметры.ЗапретитьРедактировать;
	Пользователь			= Параметры.Пользователь;
	
	Если ЗапретитьРедактировать Тогда
		Элементы.ДобавитьНовуюЗакладку.Видимость = Ложь; КонецЕсли;
	
	СсылкаНаЗакладкуПоУмолчанию = Справочники.ЗакладкиЗадач.НеУказана;
	
	ОбновитьСписокЗадач();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	
	ХранилищеОбщихНастроек.Сохранить(
			"Фильтр закладок пользователя",,
			Новый Структура("ОтмеченныеЗакладки", КонвертацияТипов.ПолучитьОтмеченныеЗначенияСписка(СписокЗакладок)));
	
КонецПроцедуры
&НаКлиенте
Процедура Применить(Команда)
	
	// Сохраним настройки
	
	СохранитьНастройки();
	
	// Скажем
	
	Оповестить(СобытияСистемы.Событие_ИзменилисьФильтрыЗакладокЗадач(),,ЭтаФорма);
	
	// Закроем
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНовуюЗакладку(Команда)
	
	ОткрытьФорму("Справочник.ЗакладкиЗадач.ФормаОбъекта");	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = СобытияСистемы.Событие_ЗаписанаЗакладакаЗадачи() Тогда
		ОбновитьСписокЗадач(); КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВсеЗакладки(Команда)
	
	Для Каждого Элемент Из СписокЗакладок Цикл Элемент.Пометка = Истина КонецЦикла;
	
КонецПроцедуры
&НаКлиенте
Процедура НиОднойЗакладки(Команда)
	
	Для Каждого Элемент Из СписокЗакладок Цикл Элемент.Пометка = Ложь КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗакладокЗначениеОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Не ЗапретитьРедактировать И Элементы.СписокЗакладок.ТекущиеДанные.Значение <> СсылкаНаЗакладкуПоУмолчанию;
	
КонецПроцедуры

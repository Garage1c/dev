//ФОРМИРОВАНИЕ И ОТПРАВКА ПАКЕТОВ
Процедура  СформироватьПакет(Ссылка, ТЗОшибок,ТЗКорзинаПисем, Excel = Неопределено) Экспорт
	
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("ИмяЛиста");
	ТЗ.Колонки.Добавить("ФорматФайла");
	ТЗ.Колонки.Добавить("ТабДок");
	
	Попытка
		
		Если Excel = Неопределено Тогда
			ФлагВыйтиИзExcel = Истина;
			Excel = Новый COMОбъект("Excel.Application");    
			Excel.Visible = 0;    
			Excel.DisplayAlerts = 0;
		КонецЕсли;
		
		
	
		Если Ссылка.ЭтоГруппа Тогда //соберем отчеты по группе
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	НастройкиPostman.Ссылка
			|ИЗ
			|	Справочник.НастройкиPostman КАК НастройкиPostman
			|ГДЕ
			|	НастройкиPostman.Родитель = &Родитель
			|	И НЕ НастройкиPostman.ПометкаУдаления
			|	И НастройкиPostman.Использование
			|	И НЕ НастройкиPostman.Заблокирован";
			
			Запрос.УстановитьПараметр("Родитель", Ссылка);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			Выборка = РезультатЗапроса.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				ЗаполнитьТаблицуMXLДокументами(ТЗ, Выборка.Ссылка, ТЗОшибок);
			КонецЦикла;
		Иначе
			ЗаполнитьТаблицуMXLДокументами(ТЗ, Ссылка, ТЗОшибок);
		КонецЕсли;
		
		Если ТЗ.Количество() Тогда
			// MXL1, MXL2... -> XLS
			Попытка
				ИмяФайлаExcel = СохранитьПакетВExcelФайл(Excel,ТЗ);	
			Исключение
				ЗаписатьОшибку(ТЗОшибок,Ссылка, "Неудалось сохранить пакет в Excel файл" + ОписаниеОшибки(), Истина);
			КонецПопытки;
			
			//Добавляем к Отправке
			Если ИмяФайлаExcel <> Неопределено Тогда
				Попытка
					ДополнитьКорзинуПисем(Ссылка, ИмяФайлаExcel, ТЗКорзинаПисем)
				Исключение
					ЗаписатьОшибку(ТЗОшибок,Ссылка, "Неудалось добавить письмо к отправке" + ОписаниеОшибки(), Истина);
				КонецПопытки;

				Попытка
					УдалитьФайлы(ИмяФайлаExcel);	
				Исключение
					ЗаписатьОшибку(ТЗОшибок,Ссылка, "Ошибка функции СформироватьПакет   " + ОписаниеОшибки(), Истина);
				КонецПопытки;
				
			КонецЕсли;
				
		КонецЕсли;
		
	Исключение
		ЗаписатьОшибку(ТЗОшибок,Ссылка, "Ошибка функции СформироватьПакет   " + ОписаниеОшибки(), Истина);
	КонецПопытки;
	
	Если ФлагВыйтиИзExcel = Истина Тогда
		Excel.Quit();	
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьТаблицуMXLДокументами(ТЗ, Ссылка, ТЗОшибок)
	ТабДок = Новый ТабличныйДокумент;
	СчНеудач = 0;
	Пока Не СформироватьОтчет(Ссылка, ТабДок) Цикл
		СчНеудач = СчНеудач + 1;
		Если СчНеудач >= Ссылка.КоличествоНеудач Тогда
			

			СпрЭл = Ссылка.ПолучитьОбъект();
			СпрЭл.Заблокирован = Истина;
			СпрЭл.Записать();
			//Лог
			Описание = "Ошибка формирования отчета. Превышен лимит попыток = " + Ссылка.КоличествоНеудач;
			ЗаписатьОшибку(ТЗОшибок, Ссылка, Описание, Истина );
			
		    Область = ТабДок.Область("R1C1");
			Область.Текст = Описание;
			Область.Шрифт = Новый Шрифт(Область.Шрифт,,20,Истина);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	НоваяСтрока = ТЗ.Добавить();
	НоваяСтрока.ИмяЛиста = Ссылка.ИмяЛиста;
	НоваяСтрока.ФорматФайла =  Ссылка.ФорматФайла;
	НоваяСтрока.ТабДок = ТабДок;
КонецПроцедуры

Процедура ЗаписатьОшибку(ТЗОшибок, Источник, Описание, ОповеститьАдминистратора = Ложь)
	
	НоваяСтрока = ТЗОшибок.Добавить();
	НоваяСтрока.Дата = ТекущаяДата();
	НоваяСтрока.Источник = Источник;
	НоваяСтрока.Описание = Описание;
	НоваяСтрока.ОповеститьАдминистратора = ОповеститьАдминистратора;
	
	
КонецПроцедуры

Функция СформироватьОтчет(Ссылка, ТабДок) Экспорт
	
	Попытка
		Настройки = Ссылка.НастройкиОтчета.Получить();
		Справочники.ОтчетыПользователей.СформироватьНаСервере(Ссылка.Отчет,Настройки,ТабДок);	
	Исключение
		Возврат Ложь
	КонецПопытки;
	Возврат Истина
КонецФункции // ()

Функция  СохранитьПакетВExcelФайл(Excel,ТЗ)
    
	ИмяВрФайла0 = ПолучитьИмяВременногоФайла(ТЗ[0].ФорматФайла);
	ТЗ[0].ТабДок.Записать(ИмяВрФайла0,?(ТЗ[0].ФорматФайла = "xlsx", ТипФайлаТабличногоДокумента.XLSX,ТипФайлаТабличногоДокумента.XLS));
	
	Попытка                          
		Книга = Excel.Workbooks.Open(ИмяВрФайла0);
		Книга.Worksheets(1).Name = ТЗ[0].ИмяЛиста;		 
		
	Исключение
		Книга = Excel.Workbooks.Add();
		Книга.SaveAs(ИмяВрФайла0);
	КонецПопытки;
	
	Для Каждого Эл Из ТЗ Цикл
		Если ТЗ.Индекс(Эл) = 0 Тогда
			Продолжить;
		КонецЕсли;
		ИмяВрФайла = ПолучитьИмяВременногоФайла(Эл.ФорматФайла);
		Эл.ТабДок.Записать(ИмяВрФайла,?(Эл.ФорматФайла = "xlsx", ТипФайлаТабличногоДокумента.XLSX,ТипФайлаТабличногоДокумента.XLS));
		
		Попытка                          
			Лист = Excel.Workbooks.Open(ИмяВрФайла);
		Исключение
			Продолжить;
		КонецПопытки;
		
		Попытка
			Лист.Worksheets(1).Name = Эл.ИмяЛиста;
		Исключение
			Лист.Worksheets(1).Name = Эл.ИмяЛиста+"A";
		КонецПопытки;
		Лист.Worksheets(1).Copy(Книга.Worksheets(Книга.Worksheets.Count));	
		Книга.Sheets(Книга.Sheets.Count).Move(Книга.Sheets(Книга.Sheets.Count-1));
		Лист.Close(0);
		УдалитьФайлы(ИмяВрФайла);
	КонецЦикла;
	
	Книга.Save();
	Книга.Close(-1);
	
	Возврат ИмяВрФайла0
	
	
КонецФункции

Процедура ДополнитьКорзинуПисем(Ссылка, ИмяФайлаExcel, ТЗКорзинаПисем)
	Для каждого Эл Из Ссылка.Получатели Цикл
		Вложение = Новый ДвоичныеДанные(ИмяФайлаExcel);
		 Если Эл.Email <> "" Тогда
		 	 НоваяСтрока = ТЗКорзинаПисем.Добавить();
			 НоваяСтрока.Email = СокрЛП(Эл.Email);
			 НоваяСтрока.ПредставлениеВложения = Ссылка.ИмяФайла + ?(Найти(ИмяФайлаExcel,".xlsx"),".xlsx",".xls" );
		 	 НоваяСтрока.Вложение = Вложение;
		 КонецЕсли;
	 КонецЦикла;
КонецПроцедуры

Процедура ОтправитьКорзинуПисем(ТЗОшибок, ТЗКорзинаПисем) Экспорт
	//Группируем по Email
	ТЗШапка = ТЗКорзинаПисем.Скопировать(,"Email");
	ТЗШапка.Свернуть("Email");

	УчетнаяЗапись = РегистрыСведений.НастройкиPostman.Получить(Новый Структура("Ключ","УчетнаяЗаписьЭлектроннойПочты")).Значение;
	//Шапка письма
	Для Каждого ЭлПисьмо Из ТЗШапка Цикл
		Попытка
			ПриведенныйПочтовыйАдрес = ОбщегоНазначенияКлиентСервер.РазобратьСтрокуСПочтовымиАдресами(ЭлПисьмо.Email);
		Исключение
			
			ЗаписатьОшибку(ТЗОшибок,,"Не удалось оработать Email:" + ЭлПисьмо.Email, Истина);
			
			Продолжить;
		КонецПопытки;
		
		ПараметрыПисьма = Новый Структура;
		ПараметрыПисьма.Вставить("Кому", ПриведенныйПочтовыйАдрес);
		ПараметрыПисьма.Вставить("Тема", "POSTMAN");
		Вложения = Новый Соответствие;
		
		//Вложения
		МассивВложений = ТЗКорзинаПисем.НайтиСтроки(Новый Структура("Email", ЭлПисьмо.Email));
		Для каждого Эл Из МассивВложений Цикл
		
			Вложения.Вставить(Эл.ПредставлениеВложения, Эл.Вложение);
		
		КонецЦикла;
	
		ПараметрыПисьма.Вставить("Вложения", Вложения);
		Попытка
			ЭлектроннаяПочта.ОтправитьПочтовоеСообщение(УчетнаяЗапись, ПараметрыПисьма);	
		Исключение
			ЗаписатьОшибку(ТЗОшибок,,"Не удалось отправить письмо на Email:" + ЭлПисьмо.Email, Истина);
		    Продолжить;
		КонецПопытки;
	КонецЦикла;

КонецПроцедуры


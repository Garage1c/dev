
&НаСервере
Процедура ОбновитьСтрокуАдресатов()
	
	Кол = Объект.Адресация.Количество();
	
	// Кнопки управления
	
	Строки = Новый Массив;
	Строки.Добавить(Новый ФорматированнаяСтрока("Доб.",,WebЦвета.Синий,,"Действие:Добавить"));
	Строки.Добавить(Новый ФорматированнаяСтрока(" "));
	Если Кол > 1 Тогда
		Строки.Добавить(Новый ФорматированнаяСтрока("X",,WebЦвета.Серый,,"Действие:Очистить"));
		Строки.Добавить(Новый ФорматированнаяСтрока(" ")); КонецЕсли;
	
	// Конкрентные пользователи
	
	Инд = -1;
	Для Каждого Строка ИЗ Объект.Адресация Цикл Инд = Инд + 1;
		
		Строки.Добавить(Новый ФорматированнаяСтрока(Строка(Строка.Адресат),,,,ПолучитьНавигационнуюСсылку(Строка.Адресат)));
		Строки.Добавить(Новый ФорматированнаяСтрока(" ")); 
		Строки.Добавить(Новый ФорматированнаяСтрока("x",,WebЦвета.Красный,,"Действие:Удалить" + Формат(Инд, "ЧН=0; ЧГ=")));
		Если Инд + 1 < Кол Тогда Строки.Добавить(Новый ФорматированнаяСтрока("; ")); КонецЕсли; КонецЦикла;
	
	стрПолучатели = Новый ФорматированнаяСтрока(Строки);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Прочитаем реквизиты
	
	//Сообщение.УстановитьHTML(Объект.Сообщение, Новый Структура);
	
	ОбновитьСтрокуАдресатов();
	
КонецПроцедуры
&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПустаяСтрока(Объект.Наименование) Тогда
		
		//Текст = Сообщение.ПолучитьТекст();
		Текст = Объект.Сообщение;
		Если ПустаяСтрока(Текст) Тогда
			
			ПоказатьОповещениеПользователя("Ошибка",,"Сообщение не заполнено", БиблиотекаКартинок.Олень);
			Отказ = Истина;
		Иначе
			Объект.Наименование = Текст КонецЕсли; КонецЕсли;
	
КонецПроцедуры
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Перем Вложение;
	
	// Сохраним реквизиты
	
	//Сообщение.ПолучитьHTML(ТекущийОбъект.Сообщение, Вложение);
	
КонецПроцедуры

&НаКлиенте
Процедура стрПолучателиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если Лев(НавигационнаяСсылкаФорматированнойСтроки, 8) = "Действие" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Если Сред(НавигационнаяСсылкаФорматированнойСтроки, 10) = "Добавить" Тогда
			
			ОткрытьФорму("Справочник.ПользователиИнтернет.ФормаВыбора", Новый Структура("МножественныйВыбор", Истина), ЭтаФорма,,,, Новый ОписаниеОповещения("ВыборНовыхАдресатов", ЭтаФорма)); 
			
		ИначеЕсли Сред(НавигационнаяСсылкаФорматированнойСтроки, 10) = "Очистить" Тогда
			
			ПоказатьВопрос(Новый ОписаниеОповещения("ВыборОчистки", ЭтаФорма), "Очистить всех адресатов?", РежимДиалогаВопрос.ДаНет,,,"Вопрос"); 
			
		ИначеЕсли Сред(НавигационнаяСсылкаФорматированнойСтроки, 10, 7) = "Удалить" Тогда
			
			Объект.Адресация.Удалить(Число(Сред(НавигационнаяСсылкаФорматированнойСтроки, 17)));
			Модифицированность = Истина;
			ОбновитьСтрокуАдресатов(); КонецЕсли; КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборНовыхАдресатов(Адресаты, ДополнительныеПараметры) Экспорт
	
	Если Адресаты <> Неопределено Тогда
		
		Для Каждого Адресат Из Адресаты Цикл
			Если Объект.Адресация.НайтиСтроки(Новый Структура("Адресат", Адресат)).Количество() = 0 Тогда
				Объект.Адресация.Добавить().Адресат = Адресат; КонецЕсли; КонецЦикла; 
		
		ОбновитьСтрокуАдресатов(); 
		Модифицированность = Истина; КонецЕсли;
	
КонецПроцедуры
&НаКлиенте
Процедура ВыборОчистки(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Объект.Адресация.Очистить();
		Модифицированность = Истина;
		ОбновитьСтрокуАдресатов(); КонецЕсли;
	
КонецПроцедуры



&НаКлиенте
Процедура ВидмостьЭлементов()
	
	СТэгами = Объект.РедактированиеТэгами;

	Элементы.КнопкаВключитьУпращенныйРежим.Видимость 					= СТэгами;
	Элементы.КнопкаВключитьРежимРедактированияСПомощьюТэгов.Видимость 	= Не СТэгами;
	
	Элементы.ДлиныйТекстТэг.Видимость 	= СТэгами;
	Элементы.ДлиныйТекстВорд.Видимость 	= Не СТэгами;
	
	Элементы.КороткийТекстТэг.Видимость 	= СТэгами;
	Элементы.КороткийТекстВорд.Видимость 	= Не СТэгами;
	
КонецПроцедуры

// КАРТИНКА

&НаКлиенте
Процедура ПолученФайлКартинкиПредставления(Результат, текАдрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
		
		АдресКартинки = текАдрес;
		Модифицированность = Истина; КонецЕсли;
	
КонецПроцедуры
&НаКлиенте
Процедура КартинкаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НачатьПомещениеФайла(Новый ОписаниеОповещения("ПолученФайлКартинкиПредставления", ЭтаФорма),,"*.jpg;*.png", Истина, УникальныйИдентификатор); 
	
	//новАдресКартинки = "";
	//Если ПоместитьФайл(новАдресКартинки, "", "", Истина, УникальныйИдентификатор) Тогда
	//	АдресКартинки = новАдресКартинки;
	//	Модифицированность = Истина;
	//КонецЕсли;
	
КонецПроцедуры
&НаКлиенте
Процедура КартинкаПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыПеретаскивания.ДопустимыеДействия = ?(
					ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Файл"),
								ДопустимыеДействияПеретаскивания.Копирование,
								ДопустимыеДействияПеретаскивания.НеОбрабатывать);
	
КонецПроцедуры
&НаКлиенте
Процедура КартинкаПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	АдресКартинки = "";
	Если ПоместитьФайл(АдресКартинки, ПараметрыПеретаскивания.Значение.ПолноеИмя, "", Ложь, УникальныйИдентификатор) Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

// ПРЕДОПРЕДЕЛЕННЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	GUID = Строка(Объект.Ссылка.УникальныйИдентификатор());
	АдресКартинки = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "Картинка");
	
	Если Не Объект.РедактированиеТэгами Тогда
	
		// Загрузим данные в короткий текст
		
		ДиалогиСПользователем.ЗагрузитьДанныеВФорматированныйДокумент(
								Объект,
								КортокийТекст,
								"Статьи",
								"КраткоеСодержание");
								
		// Загрузим данные в длинный текст
		
		ДиалогиСПользователем.ЗагрузитьДанныеВФорматированныйДокумент(
								Объект,
								ДлинныйТекст,
								"Статьи",
								"ТекстСтатьи");
								
	КонецЕсли;
							
КонецПроцедуры
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Обновим красивость
	
	ВидмостьЭлементов();
	
	// Получим картинку из базы
	
	АдресКартинки = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "Картинка");
		
КонецПроцедуры
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Запишем картинку
	
	Если ЭтоАдресВременногоХранилища(АдресКартинки) Тогда
		
		ТекущийОбъект.Картинка = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресКартинки));
		
	КонецЕсли;
	
	Если Не ТекущийОбъект.РедактированиеТэгами Тогда
	
		// Запишем короткий текст

		ДиалогиСПользователем.ЗаписатьОтформатированныйДокументНаСервере(
							ТекущийОбъект,
							"Статьи",
							КортокийТекст,
							"КраткоеСодержание",
							"k");
							
		// Запишем длинный текст

		ДиалогиСПользователем.ЗаписатьОтформатированныйДокументНаСервере(
							ТекущийОбъект,
							"Статьи",
							ДлинныйТекст,
							"ТекстСтатьи",
							"d");
	КонецЕсли;
	
КонецПроцедуры

// КНОПКИ

&НаКлиенте
Процедура ВключитьРежимРедактированияСПомощьюТэгов(Команда)
	
	Если Вопрос("При переключении в режим Тэгов, может измениться форматирование существующих текстов, всеравно продолжить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
		
		// Перебросим в текст
		
		КортокийТекст.ПолучитьHTML(Объект.КраткоеСодержание, Новый Структура);
		ДлинныйТекст.ПолучитьHTML(Объект.ТекстСтатьи, Новый Структура);
		
		Объект.КраткоеСодержание 	= ДиалогиСПользователем.ПолучитьТелоHTML(Объект.КраткоеСодержание);
		Объект.ТекстСтатьи 			= ДиалогиСПользователем.ПолучитьТелоHTML(Объект.ТекстСтатьи);
		
		// Включим режим
		
		Объект.РедактированиеТэгами = Истина;
		
		// Установим элементы
		
		ВидмостьЭлементов();
		
	КонецЕсли;
		
КонецПроцедуры
&НаКлиенте
Процедура ВключитьУпращенныйРежим(Команда)
	
	Если Вопрос("При переключении в управщенный режим, может измениться форматирование существующих текстов, всеравно продолжить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
		
		// Перебросим в текст
		
		УстановитьТекстHTMLНаСервере();
		
		// Включим режим
		
		Объект.РедактированиеТэгами = Ложь;
		
		// Установим элементы
		
		ВидмостьЭлементов();
		
	КонецЕсли;
	
КонецПроцедуры
&НаСервере
Процедура УстановитьТекстHTMLНаСервере()
	
	КортокийТекст.УстановитьHTML(ДиалогиСПользователем.СформироватьТекстHTML(Объект.КраткоеСодержание), Новый Структура);
	ДлинныйТекст.УстановитьHTML(ДиалогиСПользователем.СформироватьТекстHTML(Объект.ТекстСтатьи), Новый Структура);
			
КонецПроцедуры


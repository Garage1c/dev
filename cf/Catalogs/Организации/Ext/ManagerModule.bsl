// Функция определяет значения реквизитов выбранной организации.
//
// Параметры:
//  Организация - СправочникСсылка.Организации - Ссылка на организацию
//
// Возвращаемое значение:
//	Структура - реквизиты выбранной организации
//
Функция ПолучитьРеквизитыОрганизации(Организация) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Организации.Наименование КАК Наименование,
	|	Организации.НаименованиеПолное КАК НаименованиеПолное,
	|	Организации.ИНН КАК ИНН,
	|	Организации.КПП КАК КПП,
	|	Организации.КодПоОКПО КАК КодПоОКПО,
	|	Организации.КодПоОКАТО КАК КодПоОКАТО,
	|	Организации.СвидетельствоДатаВыдачи КАК СвидетельствоДатаВыдачи,
	|	Организации.СвидетельствоСерияНомер КАК СвидетельствоСерияНомер
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка = &Организация
	|");
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Представление =  Выборка.Наименование;
		Наименование = 	 Выборка.Наименование;
		НаименованиеПолное = Выборка.НаименованиеПолное;
		ИНН = Выборка.ИНН;
		КПП = Выборка.КПП;
		КодПоОКПО = Выборка.КодПоОКПО;
		КодПоОКАТО = Выборка.КодПоОКАТО;
		Если ЗначениеЗаполнено(Выборка.СвидетельствоСерияНомер) И ЗначениеЗаполнено(Выборка.СвидетельствоДатаВыдачи) Тогда
			Свидетельство = "Свидетельство " + Выборка.СвидетельствоСерияНомер + " от " + Формат(Выборка.СвидетельствоДатаВыдачи, "ДФ=dd.MM.yyyy");
		Иначе
			Свидетельство = "";
		КонецЕсли;
	Иначе
		Представление = "";
		//Наименование = "";
		НаименованиеПолное = "";
		ИНН = "";
		КПП = "";
		КодПоОКПО = "";
		КодПоОКАТО = "";
		Свидетельство = "";
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура("Представление, Наименование, НаименованиеПолное, ИНН, КПП, КодПоОКПО, КодПоОКАТО, Свидетельство",
		Представление,
		Наименование,
		НаименованиеПолное,
		ИНН,
		КПП,
		КодПоОКПО,
		КодПоОКАТО,
		Свидетельство
	);
	
	Возврат СтруктураРеквизитов;

КонецФункции // ПолучитьРеквизитыОрганизации()


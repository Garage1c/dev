
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация 	= Параметры.Организация;
	Контрагент 		= Параметры.Контрагент;
	ДатаНачала = ДобавитьМесяц(ТекущаяДата(),-12);
	СуммаДокумента = Параметры.СуммаДокумента;
	
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаСервере()
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДолгиПоОтгрузкам.Период КАК Дата,
	|	ДолгиПоОтгрузкам.Регистратор КАК ДокументОплаты,
	|	ДолгиПоОтгрузкам.Заказ КАК Заказ,
	|	ДолгиПоОтгрузкам.ДокументОтгрузки КАК ДокументОтгрузки,
	|	ДолгиПоОтгрузкам.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	РегистрНакопления.ДолгиПоОтгрузкам КАК ДолгиПоОтгрузкам
	|ГДЕ
	|	ДолгиПоОтгрузкам.Контрагент = &Контрагент
	|	И ДолгиПоОтгрузкам.Организация = &Организация
	|	И ДолгиПоОтгрузкам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	//|	И ДолгиПоОтгрузкам.Период > &ДатаНачала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.Заказ КАК Заказ,
	|	ВТ.ДокументОтгрузки КАК ДокументОтгрузки,
	|	СУММА(ВТ.Сумма) КАК Сумма
	|ПОМЕСТИТЬ Возвраты
	|ИЗ
	|	ВТ КАК ВТ
	|ГДЕ
	|	ВТ.Сумма < 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ.Заказ,
	|	ВТ.ДокументОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.Дата,
	|	ВТ.ДокументОплаты,
	|	ВТ.Заказ,
	|	ВТ.ДокументОтгрузки,
	|	ВТ.Сумма,
	|	Возвраты.Сумма КАК Возврат
	|ИЗ
	|	ВТ КАК ВТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Возвраты КАК Возвраты
	|		ПО ВТ.Заказ = Возвраты.Заказ
	|			И ВТ.ДокументОтгрузки = Возвраты.ДокументОтгрузки
	|ГДЕ
	|	ВТ.Сумма > 0"	                      
	);
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	
	Таблица.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьНаСервере();
КонецПроцедуры


&НаКлиенте
Процедура ТаблицаВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	АдрВХ = ОбработатьВыборНаСервере(Значение);
	Закрыть(АдрВХ);
КонецПроцедуры

&НаСервере
Функция ОбработатьВыборНаСервере(МассивСтрок)
	ТЗ=РеквизитФормыВЗначение("Таблица");
	ТабВыб=ТЗ.СкопироватьКолонки();
	
	Для Каждого ИндексСтроки Из МассивСтрок Цикл
		ТекСтр=ТЗ[ИндексСтроки];
		
		НовСтр=ТабВыб.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр,ТекСтр);
	КонецЦикла;	
	
	Если ТабВыб.Количество()=1 И СуммаДокумента<>0 И ТабВыб[0].Сумма>СуммаДокумента Тогда
		ТабВыб[0].Сумма=СуммаДокумента;
	КонецЕсли;	
	
	Адр=ПоместитьВоВременноеХранилище(ТабВыб);
	Возврат Адр;
КонецФункции



&НаСервере
Процедура ПрочитатьНастройки()
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Ссылка КАК Настройка, Значение, ЭтоГруппа
	|ИЗ
	|	ПланВидовХарактеристик.НастройкиПользователя КАК План
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	(	
	|		ВЫБРАТЬ Настройка, Значение 
	|		ИЗ РегистрСведений.ЗначенияНастроекПользователя
	|		ГДЕ Пользователь = &Пользователь
	|	) КАК Рег
	|
	|ПО
	|	Рег.Настройка = План.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Настройка ИЕРАРХИЯ
	|");
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	ЗначениеВРеквизитФормы(Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией), "НастройкиПользователя");
	
КонецПроцедуры


&НаСервере
Процедура ДобавитьВНаборЗапись(Набор, Древо)
	
	Для Каждого Строка ИЗ Древо.Строки Цикл
		
		Если Строка.ЭтоГруппа Тогда
			
			ДобавитьВНаборЗапись(Набор, Строка);
			
		Иначе
		
			НовЗапись = Набор.Добавить();
			
			НовЗапись.пользователь 	= Пользователь;
			НовЗапись.Настройка 	= Строка.Настройка;
			НовЗапись.Значение 		= Строка.Значение;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
&НаСервере
Функция СохранитьНастройки()
	
	Набор = РегистрыСведений.ЗначенияНастроекПользователя.СоздатьНаборЗаписей();
	Набор.Отбор.Пользователь.Установить(Пользователь);
	
	ДобавитьВНаборЗапись(Набор, РеквизитФормыВЗначение("НастройкиПользователя"));
	
	Попытка
		Набор.Записать();
	Исключение
		стрОшибки = ОписаниеОшибки();
		ОбщиеФункции.СообщитьТекст("Ошибка при записи настроек пользователя
				|" + стрОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	Модифицированность = Ложь;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//СкладПоУмолчанию = ХранилищеОбщихНастроек.Загрузить("ЗначенияПоУмолчанию", "Склад");
	
	//Если Склад = Неопределено Тогда
	//	СкладПоУмолчанию = Справочники.Склады.ПустаяСсылка();
	//	ХранилищеОбщихНастроек.Сохранить("ЗначенияПоУмолчанию", "Склад", СкладПоУмолчанию);
	//КонецЕсли;
	
	Пользователь = ПараметрыСеанса.ТекущийПользователь;
	
	// Прочитаем
	
	ПрочитатьНастройки();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройку(КлючОбъекта, КлючНастроек, Настройки)
	
	ХранилищеОбщихНастроек.Сохранить(КлючОбъекта, КлючНастроек, Настройки);
	
КонецПроцедуры

&НаКлиенте
Процедура СкладПоУмолчаниюПриИзменении(Элемент)
	
	//СохранитьНастройку("ЗначенияПоУмолчанию", "Склад", СкладПоУмолчанию)
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПользователяПередНачаломИзменения(Элемент, Отказ)
	
	текДанные = Элементы.НастройкиПользователя.ТекущиеДанные;
	
	Если 	текДанные <> Неопределено И
			текДанные.ЭтоГруппа Тогда
			
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Запомнить(Команда)
	
	СохранитьНастройки();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Если 	Вопрос("Сохранить настройки?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да И
				Не СохранитьНастройки() Тогда
			
			Отказ = Истина;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательПриИзменении(Элемент)
	
	ПрочитатьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьНастройки(Команда)
	
	ВыбПользователь = ОткрытьФорму("Справочник.Пользователи.ФормаВыбора",,ЭтаФорма,,,,Новый ОписаниеОповещения("ОбработкаОткрытияФормыСпр",ЭтаФорма,));
		
КонецПроцедуры
&НаКлиенте
Процедура ОбработкаОткрытияФормыСпр(Результат, Параметры) Экспорт
	Если Результат <> Неопределено Тогда
		
		Пользователь = Результат;
		ОбщиеФункции.СообщитьТекст("Пользователь выбран но настройки сохраняйте сами.");
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если СохранитьНастройки() Тогда
		ПоказатьПредупреждение(,"Настройки сохранены",,"Предупреждение");
	КонецЕсли;
	
КонецПроцедуры
